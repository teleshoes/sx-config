#!/usr/bin/perl
use strict;
use warnings;

my $ipmagicName = "sx";
my $user = "nemo";
my $commToolsDir = "$ENV{HOME}/Code/sx/comm-tools";

my @HOME_RSYNC_FILES = qw(
  .bashrc
  .gitconfig
  .gpg
  .profile
  .secrets
  .vimrc

  .config/wemo-devices.conf

  .config/qtemail/
  .vim/
  resolv/
);

my $USRLOCALBIN_LOCAL_DIR = "$ENV{HOME}/install/root-files/usr/local/bin";
my $USRLOCALBIN_REMOTE_DIR = "/usr/local/bin";
my @USRLOCALBIN_EXECS = qw(
  alarm
  gst-play
  ipmagic
  off-cmd
  term
  pulse-enable
  pulse-vol
  sshc
  vpn
);

my $HOMEBIN_LOCAL_DIR = "$ENV{HOME}/bin";
my $HOMEBIN_REMOTE_DIR = "/home/$user/bin";
my @HOMEBIN_EXCLUDES = qw(
  alert-email
  bat
  chromium
  touchClick
);

my @SPECIAL_USER_RSYNCS = (
  ["$ENV{HOME}/.cache/robinhood/", "/home/$user/.cache/robinhood/", []],
  ["$ENV{HOME}/.klomp/config-$ipmagicName", "/media/sdcard/phone/klomp/config", []],
  ["$ENV{HOME}/.klomp/lib-$ipmagicName", "/media/sdcard/phone/klomp/lib", []],
  ["$ENV{HOME}/DCIM/xddr/videos/good/", "/media/sdcard/phone/ddr-videos/", []],
  ["$ENV{HOME}/Music/sheet_music/shows/sheet_1080p/", "/media/sdcard/phone/sheet_music/shows/sheet_1080p/", []],
  ["$ENV{HOME}/Music/sheet_music/shows/music/", "/media/sdcard/phone/sheet_music/shows/music/", []],
  ["$ENV{HOME}/Music/sheet_music/trumpet/", "/media/sdcard/phone/sheet_music/trumpet/", []],
  ["/usr/share/alarms/", "/media/sdcard/phone/alarms/", []],
  ["/usr/share/sounds/custom/", "/media/sdcard/phone/sounds/", []],

  #transform symlinks to referent file/dir
  ["$ENV{HOME}/.config/ipmagic/", "/home/$user/.config/ipmagic/", ["-L"]],
);
my @SPECIAL_ROOT_RSYNCS = (
  ["$commToolsDir/sms_db_importer.py", "$USRLOCALBIN_REMOTE_DIR/", []],
);

my $INITIAL_USER_CMD = "
  mkdir -p /home/$user/.config/qtemail && \
  mkdir -p /home/$user/.klomp && \
  echo ok
";
my $FINAL_USER_CMD = "
  echo ok
";

my $INITIAL_ROOT_CMD = "
  mkdir -p /root/.config/qtemail && \
  if [ -L /root/bin ]; then rm -f /root/bin; fi && \
  if [ -d /root/bin ]; then rmdir /root/bin; fi && \
  ln -s /home/$user/bin/ /root/bin && \
  echo ok
";
my $FINAL_ROOT_CMD = "
  echo ok
";

sub run(@);

sub main(@){
  die "Usage: $0\n" if @_ > 0;

  print "making sure $ipmagicName is ssh-able\n";
  system "ipmagic", $ipmagicName, "-s", "echo found $ipmagicName!\n";
  die "failed" if $? != 0;

  my $host = `ipmagic $ipmagicName`;
  chomp $host;

  print "\n\n===INITIAL COMMANDS\n";
  run "ipmagic", $ipmagicName, "-u", $user, $INITIAL_USER_CMD;
  run "ipmagic", $ipmagicName, "-u", "root", $INITIAL_ROOT_CMD;

  print "\n\n===RSYNC /usr/local/bin FILES\n";
  my @usrlocalbinFiles = map {"$USRLOCALBIN_LOCAL_DIR/$_"} @USRLOCALBIN_EXECS;
  run "rsync", "-avP", @usrlocalbinFiles, "root\@$host:$USRLOCALBIN_REMOTE_DIR/";

  print "\n\n===RSYNC ~/bin FILES\n";
  run "rsync", "-avP", "$HOMEBIN_LOCAL_DIR/", "$user\@$host:$HOMEBIN_REMOTE_DIR/",
    map{"--exclude=$_"} @HOMEBIN_EXCLUDES;

  print "\n\n===RSYNC HOME FILES\n";
  for my $file(@HOME_RSYNC_FILES){
    run "rsync", "-avP", "$ENV{HOME}/$file", "$user\@$host:~/$file";
    run "rsync", "-avP", "$ENV{HOME}/$file", "root\@$host:~/$file";
  }

  print "\n\n===RSYNC SPECIAL FILES\n";
  for my $rsync(@SPECIAL_USER_RSYNCS){
    my ($src, $dest, $extraRsyncArgs) = @$rsync;
    run "rsync", "-avP", @$extraRsyncArgs, $src, "$user\@$host:$dest";
  }
  for my $rsync(@SPECIAL_ROOT_RSYNCS){
    my ($src, $dest, $extraRsyncArgs) = @$rsync;
    run "rsync", "-avP", @$extraRsyncArgs, $src, "root\@$host:$dest";
  }

  print "\n\n===FINAL COMMANDS\n";
  run "ipmagic", $ipmagicName, "-u", $user, $FINAL_USER_CMD;
  run "ipmagic", $ipmagicName, "-u", "root", $FINAL_ROOT_CMD;
}

sub run(@){
  print "@_\n";
  system @_;
  if($? != 0){
    die "ERROR: \"@_\" failed\n";
  }
}

&main(@ARGV);
