#!/usr/bin/perl
use strict;
use warnings;
use List::Util qw(uniq);

sub ipmagic($$@);
sub rsync($$@);
sub getAllParentDirs($);
sub run(@);

my $IPMAGIC_NAME = "sx";
my $USER = "nemo";
my $COMM_TOOLS_DIR = "$ENV{HOME}/Code/sx/comm-tools";

my @HOME_RSYNC_INCLUDES = qw(
  .bashrc
  .gitconfig
  .gpg
  .profile
  .secrets
  .vimrc
  wifi.conf
  wifi-auto.conf

  .config/gehomesdk.config
  .config/ha.conf
  .config/midea.conf
  .config/predictit-markets.conf
  .config/tasmota.conf
  .config/wemo-devices.conf

  .cache/robinhood/***

  .cache/workouts/viewfit-workouts-json/***
  .cache/workouts/hr-cache-amazfish/***
  .cache/workouts/hr-cache-hrmon/***
  .cache/workouts/viewfit-workouts-combine
  .cache/workouts/viewfit-workouts-extra-info

  .config/qtemail/***
  .vim/***
  resolv/***
);
my @HOME_RSYNC_INCLUDE_PARENT_DIRS = sort(uniq(
  map {getAllParentDirs($_)} @HOME_RSYNC_INCLUDES)
);

my @USR_LOCAL_BIN_INCLUDES = qw(
  alarm
  gst-play
  ipmagic
  off-cmd
  term
  pulse-enable
  pulse-vol
  sshc
  vpn
);

my @HOME_BIN_EXCLUDES = qw(
  alert-email
  bat
  chromium
  touchClick
);

my $RSYNCS_ARR = [
  home_user => {
    user => $USER,
    src  => "$ENV{HOME}/",
    dest => "/home/$USER/",
    args => [
      "-avP", "-O",
      "--no-perms", "--executability",
      (map {"--include=$_"} @HOME_RSYNC_INCLUDE_PARENT_DIRS),
      (map {"--include=$_"} @HOME_RSYNC_INCLUDES),
      "--exclude=**",
    ],
  },

  home_root => {
    user => "root",
    src  => "$ENV{HOME}/",
    dest => "/root/",
    args => [
      "-avP", "-O",
      "--no-perms", "--executability",
      (map {"--include=$_"} @HOME_RSYNC_INCLUDE_PARENT_DIRS),
      (map {"--include=$_"} @HOME_RSYNC_INCLUDES),
      "--exclude=**",
    ],
  },

  usr_local_bin => {
    user => "root",
    src  => "$ENV{HOME}/install/root-files/usr/local/bin/",
    dest => "/usr/local/bin/",
    args => [
      "-avP", "-O",
      (map {"--include=$_"} @USR_LOCAL_BIN_INCLUDES),
      "--exclude=**",
    ],
  },

  home_bin => {
    user => $USER,
    src  => "$ENV{HOME}/bin/",
    dest => "/home/$USER/bin/",
    args => [
      "-avP", "-O",
      (map {"--exclude=$_"} @HOME_BIN_EXCLUDES)
    ],
  },

  klomp_config => {
    user => $USER,
    src  => "$ENV{HOME}/.klomp/config-$IPMAGIC_NAME",
    dest => "/media/sdcard/phone/klomp/config",
    args => ["-avP"],
  },
  klomp_lib => {
    user => $USER,
    src  => "$ENV{HOME}/.klomp/lib-$IPMAGIC_NAME",
    dest => "/media/sdcard/phone/klomp/lib",
    args => ["-avP"],
  },
  ddr_videos => {
    user => $USER,
    src  => "$ENV{HOME}/DCIM/xddr/videos/good/",
    dest => "/media/sdcard/phone/ddr-videos/",
    args => ["-avP"],
  },
  sheet_music => {
    user => $USER,
    src  => "$ENV{HOME}/Music/sheet_music/",
    dest => "/media/sdcard/phone/sheet_music/",
    args => [
      "-avP", "-O",
      "--include=shows/",
      "--include=trumpet/",
      "--include=shows/sheet_1080p/***",
      "--include=shows/music/***",
      "--include=trumpet/***",
      "--exclude=**",
    ],
  },
  alarms => {
    user => $USER,
    src  => "/usr/share/alarms/",
    dest => "/media/sdcard/phone/alarms/",
    args => ["-avP"],
  },
  sounds => {
    user => $USER,
    src  => "/usr/share/sounds/custom/",
    dest => "/media/sdcard/phone/sounds/",
    args => ["-avP"],
  },

  #transform symlinks to referent file/dir with -L
  ipmagic_unsymlink => {
    user => $USER,
    src  => "$ENV{HOME}/.config/ipmagic/",
    dest => "/home/$USER/.config/ipmagic/",
    args => ["-avP", "-L"],
  },

  sms_db_importer => {
    user => "root",
    src  => "$COMM_TOOLS_DIR/sms_db_importer.py",
    dest => "/usr/local/bin/",
    args => ["-avP"],
  },
];

my %RSYNCS = @$RSYNCS_ARR;
my @RSYNC_NAME_ORDER = map {$$RSYNCS_ARR[$_]} grep {$_ % 2 == 0} (0..$#$RSYNCS_ARR);

my $USAGE = "USAGE:
  $0 -h | --help
    show this message

  $0 [OPTS]
    run all rsyncs

  $0 [OPTS] RSYNC_NAME_REGEX
    match all RSYNC_NAME entries against RSYNC_NAME_REGEX,
    including only those entries that match (case-insensitive) the regex

  RSYNC_NAME
    one of: @RSYNC_NAME_ORDER

  OPTS
    -n | -s | --dry-run | --simulate
      pass '-n' to all rsync commands
";

sub main(@){
  my $opts = {
    rsyncNameRegex => undef,
    simulate       => 0,
  };
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-n|-s|--dry-run|--simulate)$/){
      $$opts{simulate} = 1;
    }elsif(not defined $$opts{rsyncNameRegex}){
      $$opts{rsyncNameRegex} = $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  print "making sure $IPMAGIC_NAME is ssh-able\n";
  ipmagic($IPMAGIC_NAME, $USER, "echo found $IPMAGIC_NAME!\n");
  die "failed" if $? != 0;

  print "\n\n===RSYNC FILES\n";
  for my $rsyncName(@RSYNC_NAME_ORDER){
    my $regexFilter = $$opts{rsyncNameRegex};
    if(defined $regexFilter and $rsyncName !~ /$regexFilter/i){
      next;
    }
    print "\n=== $rsyncName\n";
    my $rsyncInfo = $RSYNCS{$rsyncName};
    my @maybeSimulateArg = $$opts{simulate} ? ("-n") : ();
    rsync($IPMAGIC_NAME, $$rsyncInfo{user},
      "--chown=$$rsyncInfo{user}:$$rsyncInfo{user}",
      @{$$rsyncInfo{args}},
      "$$rsyncInfo{src}",
      ":$$rsyncInfo{dest}",
      @maybeSimulateArg,
    );
  }
}

sub ipmagic($$@){
  my ($ipmagicName, $user, @cmd) = @_;
  run "ipmagic", $ipmagicName, "-u", $user, "--sshc", @cmd;
}
sub rsync($$@){
  my ($ipmagicName, $user, @cmd) = @_;
  run "ipmagic", $ipmagicName, "-u", $user, "--sshc", "--rsync", @cmd;
}

# recursively list all parent dirs
#   -list up to root for abspaths
#   -list up to the first relative element for relpaths (do not add './' if not given)
#   -all dirs end in a trailing slash
#   -dirs do not need to exist and filesystem is not checked
#
# e.g.:   a/b/c    =>  a/b/, a/
#         /a/b/c   =>  /a/b/, /a/, /
#         /a/b/c/  =>  /a/b/, /a/, /
#         ./a      =>  ./
sub getAllParentDirs($){
  my ($path) = @_;
  my @dirs;
  $path =~ s/\/+$//; #remove trailing slashes on last path element
  while($path =~ /^(.+)\/[^\/]+$/){
    $path = $1;
    push @dirs, "$path/";
  }
  return @dirs;
}

sub run(@){
  print "@_\n";
  system @_;
  if($? != 0){
    die "ERROR: \"@_\" failed\n";
  }
}

&main(@ARGV);
