#!/usr/bin/perl
use strict;
use warnings;
use List::Util qw(uniq);

sub ipmagic($$@);
sub rsync($$@);
sub getAllParentDirs($);

my $IPMAGIC_NAME = "sx";
my $USER = "nemo";
my $COMM_TOOLS_DIR = "$ENV{HOME}/Code/sx/comm-tools";

my @HOME_RSYNC_INCLUDES = qw(
  .bashrc
  .gitconfig
  .gpg
  .profile
  .secrets
  .vimrc
  wifi.conf
  wifi-auto.conf

  .config/gehomesdk.config
  .config/ha.conf
  .config/midea.conf
  .config/predictit-markets.conf
  .config/tasmota.conf
  .config/wemo-devices.conf

  .cache/workouts/viewfit-workouts-json/***
  .cache/workouts/hr-cache-amazfish/***
  .cache/workouts/hr-cache-hrmon/***
  .cache/workouts/viewfit-workouts-combine
  .cache/workouts/viewfit-workouts-extra-info

  .config/qtemail/***
  .vim/***
  resolv/***
);
my @HOME_RSYNC_INCLUDE_PARENT_DIRS = sort(uniq(
  map {getAllParentDirs($_)} @HOME_RSYNC_INCLUDES)
);

my $USRLOCALBIN_LOCAL_DIR = "$ENV{HOME}/install/root-files/usr/local/bin";
my $USRLOCALBIN_REMOTE_DIR = "/usr/local/bin";
my @USRLOCALBIN_EXECS = qw(
  alarm
  gst-play
  ipmagic
  off-cmd
  term
  pulse-enable
  pulse-vol
  sshc
  vpn
);

my $HOMEBIN_LOCAL_DIR = "$ENV{HOME}/bin";
my $HOMEBIN_REMOTE_DIR = "/home/$USER/bin";
my @HOMEBIN_EXCLUDES = qw(
  alert-email
  bat
  chromium
  touchClick
);

my @SPECIAL_RSYNCS = (
  [$USER, "$ENV{HOME}/.cache/robinhood/", "/home/$USER/.cache/robinhood/", []],
  [$USER, "$ENV{HOME}/.klomp/config-$IPMAGIC_NAME", "/media/sdcard/phone/klomp/config", []],
  [$USER, "$ENV{HOME}/.klomp/lib-$IPMAGIC_NAME", "/media/sdcard/phone/klomp/lib", []],
  [$USER, "$ENV{HOME}/DCIM/xddr/videos/good/", "/media/sdcard/phone/ddr-videos/", []],
  [$USER, "$ENV{HOME}/Music/sheet_music/shows/sheet_1080p/", "/media/sdcard/phone/sheet_music/shows/sheet_1080p/", []],
  [$USER, "$ENV{HOME}/Music/sheet_music/shows/music/", "/media/sdcard/phone/sheet_music/shows/music/", []],
  [$USER, "$ENV{HOME}/Music/sheet_music/trumpet/", "/media/sdcard/phone/sheet_music/trumpet/", []],
  [$USER, "/usr/share/alarms/", "/media/sdcard/phone/alarms/", []],
  [$USER, "/usr/share/sounds/custom/", "/media/sdcard/phone/sounds/", []],

  #transform symlinks to referent file/dir
  [$USER, "$ENV{HOME}/.config/ipmagic/", "/home/$USER/.config/ipmagic/", ["-L"]],

  ["root", "$COMM_TOOLS_DIR/sms_db_importer.py", "$USRLOCALBIN_REMOTE_DIR/", []],
);

my $INITIAL_USER_CMD = "
  echo ok
";
my $FINAL_USER_CMD = "
  echo ok
";

my $INITIAL_ROOT_CMD = "
  echo ok
";
my $FINAL_ROOT_CMD = "
  echo ok
";

sub run(@);

sub main(@){
  die "Usage: $0\n" if @_ > 0;

  print "making sure $IPMAGIC_NAME is ssh-able\n";
  ipmagic($IPMAGIC_NAME, $USER, "echo found $IPMAGIC_NAME!\n");
  die "failed" if $? != 0;

  print "\n\n===INITIAL COMMANDS\n";
  ipmagic($IPMAGIC_NAME, $USER, $INITIAL_USER_CMD);
  ipmagic($IPMAGIC_NAME, "root", $INITIAL_ROOT_CMD);

  print "\n\n===RSYNC /usr/local/bin FILES\n";
  my @usrlocalbinFiles = map {"$USRLOCALBIN_LOCAL_DIR/$_"} @USRLOCALBIN_EXECS;
  rsync($IPMAGIC_NAME, "root", "-avP", @usrlocalbinFiles, ":$USRLOCALBIN_REMOTE_DIR/");

  print "\n\n===RSYNC ~/bin FILES\n";
  rsync($IPMAGIC_NAME, $USER, "-avP", "$HOMEBIN_LOCAL_DIR/", ":$HOMEBIN_REMOTE_DIR/",
    map{"--exclude=$_"} @HOMEBIN_EXCLUDES);

  print "\n\n===RSYNC HOME FILES\n";
  rsync($IPMAGIC_NAME, $USER,
    "-avP",
    "-O", "--no-perms", "--executability",
    "--chown", "$USER:$USER",
    "$ENV{HOME}/",
    ":/home/$USER/",
    (map {"--include=$_"} @HOME_RSYNC_INCLUDE_PARENT_DIRS),
    (map {"--include=$_"} @HOME_RSYNC_INCLUDES),
    "--exclude=**",
  );
  rsync($IPMAGIC_NAME, "root",
    "-avP",
    "-O", "--no-perms", "--executability",
    "--chown", "root:root",
    "$ENV{HOME}/",
    ":/root/",
    (map {"--include=$_"} @HOME_RSYNC_INCLUDE_PARENT_DIRS),
    (map {"--include=$_"} @HOME_RSYNC_INCLUDES),
    "--exclude=**",
  );

  print "\n\n===RSYNC SPECIAL FILES\n";
  for my $rsync(@SPECIAL_RSYNCS){
    my ($remoteUser, $src, $dest, $extraRsyncArgs) = @$rsync;
    rsync($IPMAGIC_NAME, $remoteUser, "-avP", @$extraRsyncArgs, $src, ":$dest");
  }

  print "\n\n===FINAL COMMANDS\n";
  ipmagic($IPMAGIC_NAME, $USER, $FINAL_USER_CMD);
  ipmagic($IPMAGIC_NAME, "root", $FINAL_ROOT_CMD);
}

sub ipmagic($$@){
  my ($ipmagicName, $user, @cmd) = @_;
  run "ipmagic", $ipmagicName, "-u", $user, "--sshc", @cmd;
}
sub rsync($$@){
  my ($ipmagicName, $user, @cmd) = @_;
  run "ipmagic", $ipmagicName, "-u", $user, "--sshc", "--rsync", @cmd;
}

# recursively list all parent dirs
#   -list up to root for abspaths
#   -list up to the first relative element for relpaths (do not add './' if not given)
#   -all dirs end in a trailing slash
#   -dirs do not need to exist and filesystem is not checked
#
# e.g.:   a/b/c    =>  a/b/, a/
#         /a/b/c   =>  /a/b/, /a/, /
#         /a/b/c/  =>  /a/b/, /a/, /
#         ./a      =>  ./
sub getAllParentDirs($){
  my ($path) = @_;
  my @dirs;
  $path =~ s/\/+$//; #remove trailing slashes on last path element
  while($path =~ /^(.+)\/[^\/]+$/){
    $path = $1;
    push @dirs, "$path/";
  }
  return @dirs;
}

sub run(@){
  print "@_\n";
  system @_;
  if($? != 0){
    die "ERROR: \"@_\" failed\n";
  }
}

&main(@ARGV);
