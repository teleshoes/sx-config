#!/usr/bin/perl
use strict;
use warnings;

sub readProxyCache();
sub updateProxyCache();
sub deleteProxyCache();
sub resetProxyCache();
sub parseContexts();
sub getContextNames();
sub getContextProperties($);
sub formatContextProperties($);
sub formatConfig($);
sub isConfigMatchesContexts($$);
sub diffContextProperties($$);
sub performConfigReplacements($$);
sub readProcLines(@);
sub run(@);

#vars: <PROXY>   <MMS_HOST>   <PROXY_OR_MMS_HOST>
my $CFG = {
  internet => {
    Name                 => "T-Mobile",
    AccessPointName      => "fast.t-mobile.com",
    Protocol             => "dual", #ip, ipv6, dual
    AuthenticationMethod => "none",
    Username             => "",
    Password             => "",
  },
  mms => {
    Name                 => "T-Mobile MMS",
    AccessPointName      => "fast.t-mobile.com",
    Protocol             => "dual", #ip, ipv6, dual
    AuthenticationMethod => "none",
    Username             => "",
    Password             => "",
    MessageProxy         => "<PROXY>",
    MessageCenter        => "http://<PROXY_OR_MMS_HOST>/mms/wapenc",
  },
};

my $MMS_HOST = "mms.msg.eng.t-mobile.com";
my $MMS_PROXY_CACHE_FILE = "/etc/mms-proxy-cache";
my $MMS_DEFAULT_PROXY = "10.188.239.145";

my @PROPERTIES_INTERNET = qw(
  Name Type Active
  Protocol
  AccessPointName
  AuthenticationMethod
  Username Password
  Settings IPv6.Settings
);
my @PROPERTIES_MMS = (@PROPERTIES_INTERNET, qw(
  MessageProxy MessageCenter
));
my @PROPERTIES_ALL = @PROPERTIES_MMS;

my $USAGE = "Usage:
  $0 -h|--help
    show this message

  $0
  $0 -g|--get
    print APN+MMS settings

  $0 -s|--set
    set APN+MMS settings
      -use <PROXY> in $MMS_PROXY_CACHE_FILE
      -if $MMS_PROXY_CACHE_FILE does not exist or is invalid:
        remove MessageProxy and use $MMS_HOST

  $0 -d|--delete-proxy
    remove proxy, and use $MMS_HOST

  $0 -u|--update-proxy
    ping $MMS_HOST and put address in $MMS_PROXY_CACHE_FILE

  $0 -r|--reset-proxy
    write $MMS_DEFAULT_PROXY to $MMS_PROXY_CACHE_FILE

  config is:\n=====\n" . formatConfig($CFG) . "=====
";

my $MODE_GET = "get";
my $MODE_SET = "set";
my $MODE_DELETE_PROXY = "delete-proxy";
my $MODE_UPDATE_PROXY = "update-proxy";
my $MODE_RESET_PROXY = "reset-proxy";

sub main(@){
  my $mode = $MODE_GET;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-g|--get)$/){
      $mode = $MODE_GET;
    }elsif($arg =~ /^(-s|--set)$/){
      $mode = $MODE_SET;
    }elsif($arg =~ /^(-d|--delete-proxy)$/){
      $mode = $MODE_DELETE_PROXY;
    }elsif($arg =~ /^(-u|--update-proxy)$/){
      $mode = $MODE_UPDATE_PROXY;
    }elsif($arg =~ /^(-r|--reset-proxy)$/){
      $mode = $MODE_RESET_PROXY;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  my $contexts = parseContexts();
  die "ERROR: could not find internet context\n" if not defined $$contexts{internet};
  die "ERROR: could not find mms context\n" if not defined $$contexts{mms};

  if($mode eq $MODE_GET){
    print formatContextProperties($contexts);
  }elsif($mode eq $MODE_SET){
    print "\n\n====OLD\n";
    print formatContextProperties($contexts);
    print "\n";

    my $proxy = readProxyCache();
    my $replacementsByType = {
      "internet" => {},
      "mms" => {
        "<PROXY>" => defined $proxy ? $proxy : "",
        "<MMS_HOST>" => $MMS_HOST,
        "<PROXY_OR_MMS_HOST>" => (defined $proxy ? $proxy : $MMS_HOST),
      },
    };

    my $config = performConfigReplacements($CFG, $replacementsByType);

    for my $contextType(sort keys %$config){
      my ($contextName, $oldProps) = @{$$contexts{$contextType}};
      my $newProps = $$CFG{$contextType};
      for my $prop(sort keys %{$$config{$contextType}}){
        my $val = $$config{$contextType}{$prop};
        run "sudo", "qdbus",
          "--system",
          "org.ofono",
          $contextName,
          "org.ofono.ConnectionContext.SetProperty",
          $prop,
          $val;
      }
    }

    my $newContexts = parseContexts();
    die "ERROR: could not find internet context\n" if not defined $$newContexts{internet};
    die "ERROR: could not find mms context\n" if not defined $$newContexts{mms};

    print "\n\n====NEW\n";
    print formatContextProperties($newContexts);
    print "\n";

    if(not isConfigMatchesContexts($config, $newContexts)){
      die "\nERROR: new context values do not reflect config\n";
    }

    my $diff = diffContextProperties($contexts, $newContexts);
    if($diff eq ""){
      print "\n\ncontexts unchanged\n";
    }else{
      print "\n\ncontexts updated:\n$diff\n";
    }
  }elsif($mode eq $MODE_DELETE_PROXY){
    deleteProxyCache();
  }elsif($mode eq $MODE_UPDATE_PROXY){
    updateProxyCache();
  }elsif($mode eq $MODE_RESET_PROXY){
    resetProxyCache();
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub readProxyCache(){
  my $proxy = `cat $MMS_PROXY_CACHE_FILE`;
  chomp $proxy;
  if($proxy =~ /^(\d+\.\d+\.\d+\.\d+)$/){
    return $1;
  }else{
    return undef;
  }
}
sub deleteProxyCache(){
  system "sudo", "rm", "-f", $MMS_PROXY_CACHE_FILE;
}
sub updateProxyCache(){
  my $proxy = getIP($MMS_HOST);
  if($proxy =~ /^10\.\d+\.\d+\.\d+$/){
    print "UPDATING PROXY => $proxy\n";
    system "echo $proxy | sudo tee $MMS_PROXY_CACHE_FILE >/dev/null";
  }else{
    print "skipping proxy update\n";
  }
}
sub resetProxyCache(){
  system "echo $MMS_DEFAULT_PROXY | sudo tee $MMS_PROXY_CACHE_FILE >/dev/null";
}

sub getIP($){
  my ($host) = @_;
  my $out = `host '$host'`;
  if($out =~ /^$host has address (.+)$/){
    return $1;
  }else{
    return undef;
  }
}

sub parseContexts(){
  my $contexts = {};
  my @contextNames = getContextNames();
  for my $contextName(sort @contextNames){
    my $props = getContextProperties($contextName);
    my $type = $$props{Type};
    if(not defined $type){
      print STDERR "WARNING: skipping context $contextName - no Type defined\n";
      next;
    }
    if(defined $$contexts{$type}){
      print STDERR "WARNING: duplicate context type $type ($contextName + $$contexts{$type})\n";
      next;
    }
    $$contexts{$type} = [$contextName, $props];
  }
  return $contexts;
}

sub getContextNames(){
  my @contextNames;
  for my $line(readProcLines(qw(sudo qdbus --system org.ofono))){
    chomp $line;
    push @contextNames, $line if $line =~ /context\d+$/;
  }
  return @contextNames;
}

sub getContextProperties($){
  my ($contextName) = @_;
  my @cmd = ("dbus-send",
    "--system",
    "--print-reply",
    "--dest=org.ofono",
    $contextName,
    "org.ofono.ConnectionContext.GetProperties",
  );

  my $out = join '', readProcLines @cmd;

  my $props = {};
  for my $p(@PROPERTIES_ALL){
    if($out =~ /
      \s* dict \s+ entry \s* \(
      \s*  string \s* "$p"  \s+  variant
      \s*    ( string \s+ "[^"]*"
             | boolean \s+ (?:true|false)
             | array \s+ \[  [^\]]*  \]
             )
      \s* \)
    /x){
      $$props{$p} = $1;
    }
  }

  for my $key(sort keys %$props){
    my $val = $$props{$key};
    if($val =~ /^string "(.*)"$/){
      $val = $1;
    }elsif($val =~ /^boolean (true|false)$/){
      $val = $1;
    }elsif($val =~ /^array \[\s*([^\]]*?)\s*\]$/){
      $val = "[$1]";
    }else{
      die "ERROR: could not parse $contextName.$key=$val in:\n$out\n";
    }
    $$props{$key} = $val;
  }

  return $props;
}

sub formatContextProperties($){
  my ($contexts) = @_;
  my $fmt = "";
  $fmt .= "internet:\n";
  for my $p(@PROPERTIES_INTERNET){
    my $sp = ' ' x (25-(length $p));
    my ($contextName, $props) = @{$$contexts{internet}};
    my $val = $$props{$p};
    next if not defined $val;
    $fmt .= "  $p:$sp$val\n";
  }
  $fmt .= "mms:\n";
  for my $p(@PROPERTIES_MMS){
    my $sp = ' ' x (25-(length $p));
    my ($contextName, $props) = @{$$contexts{mms}};
    my $val = $$props{$p};
    next if not defined $val or $val eq "";
    $fmt .= "  $p:$sp$val\n";
  }
  return $fmt;
}
sub formatConfig($){
  my ($config) = @_;
  my $fmt = "";
  for my $contextType(sort keys %$config){
    for my $prop(sort keys %{$$config{$contextType}}){
      my $val = $$config{$contextType}{$prop};
      $fmt .= "$contextType.$prop = $val\n";
    }
  }
  return $fmt;
}

sub isConfigMatchesContexts($$){
  my ($config, $contexts) = @_;

  for my $contextType(sort keys %$config){
    if(not defined $$contexts{$contextType}){
      return 0;
    }
    my ($contextName, $props) = @{$$contexts{$contextType}};
    for my $prop(sort keys %{$$config{$contextType}}){
      if(not defined $$props{$prop} or $$props{$prop} ne $$config{$contextType}{$prop}){
        return 0;
      }
    }
  }

  return 1;
}

sub diffContextProperties($$){
  my ($oldContexts, $newContexts) = @_;

  my $fmt = "";

  my %okContextTypes = (keys %$oldContexts, keys %$newContexts);
  for my $contextType(sort keys %okContextTypes){
    if(not defined $$oldContexts{$contextType}){
      $fmt .= "added $contextType context\n"
    }elsif(not defined $$newContexts{$contextType}){
      $fmt .= "removed $contextType context\n"
    }else{
      my ($oldContextName, $oldProps) = @{$$oldContexts{$contextType}};
      my ($newContextName, $newProps) = @{$$newContexts{$contextType}};

      if($oldContextName ne $newContextName){
        $fmt .= "changed $contextType context $oldContextName => $newContextName\n";
      }

      my %okProps = (keys %$oldProps, keys %$newProps);
      for my $prop(sort keys %okProps){
        if(not defined $$oldProps{$prop}){
          $fmt .= "added $contextType.$prop=$$newProps{$prop}\n";
        }elsif(not defined $$newProps{$prop}){
          $fmt .= "removed $contextType.$prop=$$oldProps{$prop}\n";
        }elsif($$oldProps{$prop} ne $$newProps{$prop}){
          $fmt .= "$contextType.$prop  \"$$oldProps{$prop}\" => \"$$newProps{$prop}\"\n";
        }
      }
    }
  }

  return $fmt;
}

sub performConfigReplacements($$){
  my ($origConfig, $replacementsByType) = @_;

  my $replConfig = {};
  for my $contextType(sort keys %$origConfig){
    $$replConfig{$contextType} = {};
    for my $prop(sort keys %{$$origConfig{$contextType}}){
      my $val = $$origConfig{$contextType}{$prop};
      my $replacements = $$replacementsByType{$contextType};
      for my $ptrn(sort keys %$replacements){
        my $repl = $$replacements{$ptrn};
        $val =~ s/$ptrn/$repl/g;
      }
      $$replConfig{$contextType}{$prop} = $val;
    }
  }

  return $replConfig;
}

sub readProcLines(@){
  my @cmd = @_;
  open FH, "-|", @cmd or die "ERROR: could not run @cmd\n$!\n";
  my @lines = <FH>;
  close FH;
  return @lines;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
