#!/usr/bin/perl
use strict;
use warnings;
use File::Basename;

my $ipmagicName = "sx";

my $USE_WAYDROID = 0;
my $USE_ALIENDALVIK = 1;

my $SDCARD_DIR = "/media/sdcard/phone";

my @config = qw(
  voicecall-ui
  jolla-messages
  fingerterm
  sailfish-browser

  qtbtn-klomp
  bromite
  opencamera-wrapper
  jolla-settings

  harbour-pure-maps
  tmobile-digits
  gpsinfo
  coolreader

  stellarium
  jolla-clock
  jolla-contacts
  qtemail

  jolla-calculator
  harbour-advanced-camera
  jolla-calendar
  waydroid-wd

  jolla-gallery
  unplayer
  filetug
  screenshot

  facebook
  google-maps
  element
  jolla-camera



  sailfish-office
  jolla-notes
  retrofit-barcode
  kill-lipstick2vnc

  okboard
  qtscreen-daemon
  jollacompass
  compass-calib

  lirr_isp_pen
  lirr_pen_isp
  lirr_bab_pen
  lirr_pen_bab

  lirr_isp_gc
  lirr_gc_isp
  lirrgui
  sailfish-audiorecorder

  mimer
  waydroid-runner
  jolla-mediaplayer
  meecast

  storeman
  store-client
  sailfishos-chum-gui
  aptoide

  fdroid
  kingdom
  nesoid
  snes9x



  chrome
  tor
  discord
  fbmessenger

  fairynest
  midea
  mystrom
  jitsi

  textfree
  tkts
  traintime

  vlc
  youtube
  youtube-music
  youtube-vanced-mgr

  android-settings
  modrana
  qtbtn-lap
  rheem

  harbour-mlsmanager
  sfos-forum-viewer
  amazfish-ui
  audio-output

  sailfish-filemanager

  aurora-store
  axs
  band-chat
  cavestory
  chess
  ge-ac
  librera-fd-pdf
  microg-settings
  mobile-sheets
  mpv
  petsafe
  retro-fitness
  riot
  runpee
  seatgeek
  stubhub
  ticketmaster
  tribit
  whisker
);

my @unused = qw(
  jolla-email
  sailfish-weather
  sailfish-tutorial
  sailfish-archive
  sailfish-installationhandler
  openconnect-import
  sailfish-captiveportal
  vpnc-import
  google-tts
  vanced-microg
  ivona-tts
  ivona-tts-amy
  opencamera
);

########################################################
#arbitrary depth nested dirs, with icons
#  if icon name is blank 'dir%name%[', use the defaultIconName
#e.g.:
#  appname0-launcher
#  dir%FolderName%folder-icon-name[
#    appname1-launcher
#    appname2-launcher
#    appname3-launcher
#    dir%SubFolder%icon-bird-nest[
#      appname4-launcher
#    ]
#  ]
#  appname5-launcher
#  appname6-launcher
########################################################


# APP_NAME => [WAIT_MOUNT, PACKAGE, ACTION, ICON_PATH]
# these get created as "/usr/shared/applications/<PREFIX>-<APP_NAME>.desktop"
#   PREFIX is "android-ad" for USE_ALIENDALVIK and "android-wd" for USE_WAYDROID
my %androidLaunchers = (
  "android-settings"   => [0, "com.android.settings",                      ".Settings",                                                       undef],
  "aptoide"            => [0, "cm.aptoide.pt",                             ".view.MainActivity",                                              undef],
  "aurora-store"       => [0, "com.aurora.store",                          ".MainActivity",                                                   undef],
  "axs"                => [0, "com.axs.android",                           ".ui.content.main.MainActivity",                                   undef],
  "band-chat"          => [0, "com.nhn.android.band",                      ".SplashActivity",                                                 undef],
  "bromite"            => [0, "org.bromite.bromite",                       "com.google.android.apps.chrome.Main",                             undef],
  "cavestory"          => [0, "io.github.doukutsu_rs",                     ".MainActivity",                                                   undef],
  "chess"              => [0, "jwtc.android.chess",                        ".start",                                                          undef],
  "chrome"             => [0, "com.android.chrome",                        "com.google.android.apps.chrome.Main",                             undef],
  "coolreader"         => [1, "org.coolreader",                            ".CoolReader",                                                     undef],
  "discord"            => [0, "com.discord",                               ".app.AppActivity\$Main",                                          undef],
  "element"            => [0, "im.vector.app",                             "im.vector.app.features.Alias",                                    undef],
  "facebook"           => [0, "com.facebook.katana",                       ".LoginActivity",                                                  undef],
  "fairynest"          => [0, "com.spled.aicontrol",                       ".activity.main.MainActivity",                                     undef],
  "fbmessenger"        => [0, "com.facebook.orca",                         ".auth.StartScreenActivity",                                       undef],
  "fdroid"             => [0, "org.fdroid.fdroid",                         ".views.main.MainActivity",                                        undef],
  "ge-ac"              => [0, "com.ge.kitchen",                            "com.ge.cafe.SplashActivity",                                      undef],
  "google-maps"        => [0, "com.google.android.apps.maps",              "com.google.android.maps.MapsActivity",                            undef],
  "google-tts"         => [0, "com.google.android.tts",                    "android.app.AppDetailsActivity",                                  undef],
  "ivona-tts"          => [0, "com.ivona.tts",                             ".WelcomeActivity",                                                undef],
  "ivona-tts-amy"      => [0, "com.ivona.tts.voicebeta.eng.gbr.amy",       ".IvonaVoice",                                                     undef],
  "jitsi"              => [0, "org.jitsi.meet",                            ".MainActivity",                                                   undef],
  "kingdom"            => [0, "com.rawfury.kingdom2crowns",                "com.unity3d.player.UnityPlayerActivity",                          undef],
  "librera-fd-pdf"     => [0, "com.foobnix.pro.pdf.reader",                "com.foobnix.ui2.MainTabs2",                                       undef],
  "microg-settings"    => [0, "com.google.android.gms",                    "org.microg.gms.ui.SettingsActivity",                              undef],
  "midea"              => [0, "com.midea.aircondition.obm",                ".activity.LoginActivity",                                         undef],
  "mobile-sheets"      => [1, "com.zubersoft.mobilesheetsfree",            ".MobileSheetsFree",                                               undef],
  "mpv"                => [1, "is.xyz.mpv",                                ".MainActivity",                                                   undef],
  "mystrom"            => [0, "com.cogniance.asoka.srs.android",           ".MainActivity",                                                   undef],
  "nesoid"             => [0, "com.androidemu.nes",                        ".MainActivity",                                                   undef],
  "opencamera"         => [1, "net.sourceforge.opencamera",                ".MainActivity",                                                   undef],
  "petsafe"            => [0, "net.petsafe.platform",                      ".views.ActSplash",                                                undef],
  "retro-fitness"      => [0, "com.netpulse.mobile.retro",                 "com.netpulse.mobile.launch.LaunchActivity",                       undef],
  "rheem"              => [0, "com.rheem.econetconsumerandroid",           "com.rheem.econet.view.splash.SplashActivity",                     undef],
  "riot"               => [0, "im.vector.alpha",                           "im.vector.activity.LauncherActivity",                             undef],
  "runpee"             => [0, "air.RunPee",                                "io.flutter.embedding.android.FlutterActivity",                    undef],
  "seatgeek"           => [0, "com.seatgeek.android",                      ".ui.activities.SplashActivity",                                   undef],
  "snes9x"             => [0, "com.explusalpha.Snes9xPlus",                "com.imagine.BaseActivity",                                        undef],
  "stubhub"            => [0, "com.stubhub",                               ".MainActivity",                                                   undef],
  "textfree"           => [0, "com.pinger.textfree",                       ".call.activities.TFSplash",                                       undef],
  "ticketmaster"       => [0, "com.ticketmaster.mobile.android.na",        "com.ticketmaster.mobile.android.library.activity.MainActivity",   undef],
  "tkts"               => [0, "com.TDF.AndroidTdf",                        ".applicationwide.TktsSplashScreenActivity",                       undef],
  "tmobile-digits"     => [0, "com.tmobile.digits",                        "com.mavenir.digits.digits3x.MainActivity",                        undef],
  "tor"                => [0, "org.torproject.torbrowser",                 ".App",                                                            undef],
  "traintime"          => [0, "com.mta.mobileapp",                         ".MainActivity",                                                   undef],
  "tribit"             => [0, "com.thousandshores.tribit",                 ".modulelogin.activity.FirstActivity",                             undef],
  "vanced-microg"      => [0, "com.mgoogle.android.gms",                   "android.app.AppDetailsActivity",                                  undef],
  "vlc"                => [1, "org.videolan.vlc",                          ".StartActivity",                                                  undef],
  "whisker"            => [0, "com.whisker.android",                       ".MainActivity",                                                   undef],
  "youtube"            => [0, "com.vanced.android.youtube",                "com.google.android.youtube.app.honeycomb.Shell\$HomeActivity",    undef],
  "youtube-music"      => [0, "com.vanced.android.apps.youtube.music",     "com.google.android.apps.youtube.music.activities.MusicActivity",  undef],
  "youtube-vanced-mgr" => [0, "com.vanced.manager",                        ".ui.MainActivity",                                                undef],
);



# APP_NAME_ALIAS => REAL_DESKTOP_FILE_REGEX
my %replacementRegexsWaydroid = map {$_ => "^android-wd-$_\$"} sort keys %androidLaunchers;

# APP_NAME_ALIAS => REAL_DESKTOP_FILE_REGEX
my %replacementRegexsAliendalvik = map {$_ => "^android-ad-$_\$"} sort keys %androidLaunchers;

# APP_NAME_ALIAS => REAL_DESKTOP_FILE_REGEX
my %replacementRegexs = (
  'lirrgui'          => qw/^lirrtraintime$/,

  ($USE_WAYDROID ? %replacementRegexsWaydroid : ()),
  ($USE_ALIENDALVIK ? %replacementRegexsAliendalvik : ()),
);

#desktop files with no launchers
my @extraIncludePatterns = qw(
  apkd-mime-handler.desktop
  csd.desktop
  harbour-pure-maps-uri-handler.desktop
  jolla-calendar-import.desktop
  jolla-camera-lockscreen.desktop
  jolla-camera-viewfinder.desktop
  jolla-contacts-import.desktop
  jolla-gallery-openfile.desktop
  jolla-gallery-playvideostream.desktop
  jolla-mediaplayer-openfile.desktop
  jolla-messages-openurl.desktop
  jolla-notes-import.desktop
  l2tp-import.desktop
  new-mail.desktop
  open-url.desktop
  ovpn-import.desktop
  pptp-import.desktop
  sailfish-captiveportal.desktop
  sailfish-office-openfile.desktop
  sailfish-share.desktop
  simkit.desktop
  store-client-install.desktop
  voicecall-ui-openurl.desktop
  webcal-import.desktop
);

my $user = "nemo";
my $defaultIconName = "icon-launcher-folder-14";
my $activeDir = '/usr/share/applications';
my $allDir = '/opt/app-launchers';
my $userDir = "/home/$user/.local/share/applications";
my $origLaunchersUserDir = '/opt/app-launchers-user';
my $directoryFileDir = "/home/$user/.config/lipstick";
my $confFileDest = "/home/$user/.config/lipstick/applications.menu";

my $waydroidSrcAppIconDir = "/home/waydroid/data/icons";
my $waydroidBackupAppIconDir = "$SDCARD_DIR/android-app-icons-wd";
my $waydroidDestAppIconDir = "/usr/share/icons/waydroid";

my $aliendalvikSrcAppIconDir = "/home/$user/.local/share/apkd-bridge/launcherIcon";
my $aliendalvikBackupAppIconDir = "$SDCARD_DIR/android-app-icons-ad";
my $aliendalvikDestAppIconDir = "/usr/share/icons/aliendalvik";

sub rearrangeLauncherButtons();
sub createAndroidDesktopFiles();
sub createAndroidDesktopFile($$);
sub ensureExecScriptOnDevice(@);
sub applyConfigReplacements($$);
sub getAppNamesInDir($);
sub getValidAppNames();
sub addMissingPrefix($$);
sub createAppDesktopFiles(@);
sub writeLipstickConfig(@);
sub run(@);

#in case running in a gui terminal
$SIG{HUP} = sub { print STDERR "Caught a sighup $!" };

my $usage = "Usage:
  $0
    re-arrange launcher buttons and launcher dirs, potentially removing some
      backs up desktop files to $allDir
      before changing $activeDir and lipstick

  $0 -g
    print a list of all app names from all desktop files
      in $allDir and $activeDir

  $0 --missing-config
    -get active desktop files from:
      $activeDir
      $userDir
    -do not include empty files (user dir placeholders)
    -do not include desktop files that are in config
    -do not include desktop files that are marked extra (no launchers)
    -print the remaining desktop files that would be removed by rearrange

  $0 --inactive
    -get all desktop files from:
      $allDir
    -do not include desktop files that are in:
      $activeDir
    -do not include desktop files that are specifically marked unused
    -print the remaining inactive desktop files
";

sub main(@){
  my $cmd;
  if(@_ == 1 and $_[0] =~ /^(-g)$/){
    $cmd = "get";
  }elsif(@_ == 1 and $_[0] =~ /^(--inactive)$/){
    $cmd = "inactive";
  }elsif(@_ == 1 and $_[0] =~ /^(--missing-config)$/){
    $cmd = "missing-config";
  }elsif(@_ == 0){
    $cmd = "rearrange";
  }else{
    die $usage;
  }

  ensureExecScriptOnDevice(@_);

  die "must be run as root\n" if `whoami` ne "root\n";

  if($cmd eq "get"){
    print map {"$_\n"} getValidAppNames();
  }elsif($cmd eq "inactive"){
    my @allAppNames = getAppNamesInDir $allDir;
    my %okActiveAppNames = map {$_ => 1} getAppNamesInDir $activeDir;
    my @inactive = grep {not defined $okActiveAppNames{$_}} @allAppNames;

    my @validAppNames = getValidAppNames();
    my @unusedItems = @unused;
    @unusedItems = applyConfigReplacements \@unusedItems, \@validAppNames;
    my %okUnusedAppNames = map {$_ => 1} @unusedItems;
    @inactive = grep {not defined $okUnusedAppNames{$_}} @inactive;

    print "\n\ninactive launchers:\n";
    print map {"$_\n"} @inactive;
  }elsif($cmd eq "missing-config"){
    my @configItems = @config;
    my @validAppNames = getValidAppNames();
    @configItems = applyConfigReplacements \@configItems, \@validAppNames;
    @configItems = addMissingPrefixes([@configItems], [@validAppNames]);
    my %okConfigAppNames = map {$_ => 1} @configItems;

    my @extraAppNames = map {$_ =~ s/\.desktop$//; $_} @extraIncludePatterns;
    my %okExtraAppNames = map {$_ => 1} @extraAppNames;

    my @activeAppNames = getAppNamesInDir $activeDir;

    my @nonEmptyUserLaunchers = grep {not -z $_} glob "$userDir/*.desktop";
    my @nonEmptyUserAppNames = map {
      $_ =~ s/$userDir\///;
      $_ =~ s/\.desktop$//;
      $_
    } @nonEmptyUserLaunchers;


    my @missingAppNames = (@activeAppNames, @nonEmptyUserAppNames);
    @missingAppNames = grep {not defined $okConfigAppNames{$_}} @missingAppNames;
    @missingAppNames = grep {not defined $okExtraAppNames{$_}} @missingAppNames;

    print "\n\nmissing config entries:\n";
    print map {"$_\n"} @missingAppNames;
  }elsif($cmd eq "rearrange"){
    rearrangeLauncherButtons();
  }else{
    die "invalid command: $cmd\n";
  }
}

sub rearrangeLauncherButtons(){
  createAndroidDesktopFiles();

  my @validAppNames = getValidAppNames();

  my @configItems = @config;
  @configItems = applyConfigReplacements \@configItems, \@validAppNames;
  @configItems = addMissingPrefixes([@configItems], [@validAppNames]);

  my @appNames = grep {$_ !~ /^(dir%.*%.*\[|\])$/} @configItems;

  my %okAllAppNamesOld = map {$_ => 1} getAppNamesInDir $allDir;
  createAppDesktopFiles(@appNames);
  my %okAllAppNamesNew = map {$_ => 1} getAppNamesInDir $allDir;

  writeLipstickConfig(@configItems);

  run "pkill -9 lipstick";

  my %okActiveAppNames = map {$_ => 1} getAppNamesInDir $activeDir;

  my @unusedItems = @unused;
  @unusedItems = applyConfigReplacements \@unusedItems, \@validAppNames;
  my %okUnusedAppNames = map {$_ => 1} @unusedItems;

  my (@removedNew, @removedOld);
  for my $appName(sort keys %okAllAppNamesNew){
    if(not defined $okActiveAppNames{$appName}){
      if(defined $okAllAppNamesOld{$appName} or defined $okUnusedAppNames{$appName}){
        push @removedOld, $appName;
      }else{
        push @removedNew, $appName;
      }
    }
  }

  my @missing;
  for my $appName(@appNames){
    push @missing, $appName if not defined $okActiveAppNames{$appName};
  }

  print "\n\nMISSING (not installed): @missing\n" if @missing > 0;
  print "\n\nREMOVED (newly installed): @removedNew\n" if @removedNew > 0;

  run "udo systemctl --user restart lipstick";
}

sub createAndroidDesktopFiles(){
  run "rm -f $activeDir/android-wd-*.desktop";
  run "rm -f $allDir/android-wd-*.desktop";

  run "rm -f $activeDir/android-ad-*.desktop";
  run "rm -f $allDir/android-ad-*.desktop";

  if($USE_WAYDROID){
    run "mkdir", "-p", $waydroidBackupAppIconDir;
    run "rsync", "-avP",
      "$waydroidSrcAppIconDir/",
      "$waydroidBackupAppIconDir/",
    ;
    run "mkdir", "-p", $waydroidDestAppIconDir;
    run "rsync", "-avP",
      "$waydroidBackupAppIconDir/",
      "$waydroidDestAppIconDir/",
    ;
    run "chown",
      "-R",
      "$waydroidDestAppIconDir/",
      "--reference=$waydroidDestAppIconDir/../",
    ;
  }
  if($USE_ALIENDALVIK){
    my $adIcons = {};
    for my $iconFile(glob "$aliendalvikSrcAppIconDir/*.png"){
      if($iconFile =~ /apkd_launcher_(\w+)-(.+).png/){
        my ($pkgNameFmt, $intentFmt) = ($1, $2);
        $pkgNameFmt =~ s/__/_/g;
        $$adIcons{$pkgNameFmt} = $iconFile;
      }
    }

    run "mkdir", "-p", $aliendalvikBackupAppIconDir;
    for my $appName(sort keys %androidLaunchers){
      my ($isWaitMount, $pkg, $intent, $iconPath) = @{$androidLaunchers{$appName}};
      my $pkgNameFmt = $pkg;
      $pkgNameFmt =~ s/\W/_/g;
      my $iconFile = $$adIcons{$pkgNameFmt};
      if(defined $iconFile){
        run "cp", "-ar", "$iconFile", "$aliendalvikBackupAppIconDir/$pkg.png";
      }else{
        print "\n\nWARNING: no aliendalvik icon found for $appName - $pkg\n";
      }
    }

    run "mkdir", "-p", $aliendalvikDestAppIconDir;
    run "rsync", "-avP",
      "$aliendalvikBackupAppIconDir/",
      "$aliendalvikDestAppIconDir/",
    ;
    run "chown",
      "-R",
      "$aliendalvikDestAppIconDir/",
      "--reference=$aliendalvikDestAppIconDir/../",
    ;
  }

  if($USE_WAYDROID){
    for my $appName(sort keys %androidLaunchers){
      createAndroidDesktopFile($appName, "waydroid");
    }
  }
  if($USE_ALIENDALVIK){
    for my $appName(sort keys %androidLaunchers){
      createAndroidDesktopFile($appName, "aliendalvik");
    }
  }
}

sub createAndroidDesktopFile($$){
  my ($appName, $androidEnvName) = @_;

  my ($isWaitMount, $pkg, $action, $iconPathOverride) = @{$androidLaunchers{$appName}};

  my $fullIntent = "$pkg/$action";

  my $cmd;
  my $desktopFilePrefix;
  my $androidIconDir;
  if($androidEnvName eq "aliendalvik"){
    $cmd = "ad";
    $cmd .= " --skip-mount" unless $isWaitMount;
    $cmd .= " --app $appName '$fullIntent'";
    $desktopFilePrefix = "android-ad";
    $androidIconDir = $aliendalvikDestAppIconDir;
  }elsif($androidEnvName eq "waydroid"){
    $cmd = "/usr/local/bin/udo wd --launch-intent=\"$fullIntent\"";
    $desktopFilePrefix = "android-wd";
    $androidIconDir = $waydroidDestAppIconDir;
  }else{
    die "ERROR: unknown android environment $androidEnvName\n";
  }

  my $iconPath = "$androidIconDir/$pkg.png";
  $iconPath = $iconPathOverride if defined $iconPathOverride;

  my $desktopFile = "$activeDir/$desktopFilePrefix-$appName.desktop";
  my $desktopContents = ""
    . "[Desktop Entry]\n"
    . "Name=$appName\n"
    . "Comment=Run $fullIntent in $androidEnvName\n"
    . "Icon=$iconPath\n"
    . "Exec=$cmd\n"
    . "Type=Application\n"
    . "X-Nemo-Single-Instance=no\n"
    . "Custom-Launcher=yes\n"
    . "\n"
    . "[X-Sailjail]\n"
    . "Sandboxing=Disabled\n"
    ;

  open FH, "> $desktopFile" or die "ERROR: cannot write $desktopFile\n$!\n";
  print FH $desktopContents;
  close FH;
}

sub ensureExecScriptOnDevice(@){
  my @args = @_;
  my $targetHost = `ipmagic $ipmagicName 2>/dev/null`;
  chomp $targetHost;
  my $actualHost = `hostname`;
  chomp $actualHost;
  if(length $targetHost > 0 and $targetHost ne $actualHost){
    my $host = `ipmagic $ipmagicName`;
    chomp $host;
    my $name = basename $0;
    print "copying to the device\n";
    system "scp", $0, "root\@$host:/tmp/$name";
    print "rerunning on the device\n";
    exec "ssh", "root\@$host", "/tmp/$name", @args;
  }
}

sub applyConfigReplacements($$){
  my ($configItems, $validAppNames) = @_;

  my %replacements;
  for my $fakeAppName(sort keys %replacementRegexs){
    my $regex = $replacementRegexs{$fakeAppName};
    my $found = 0;
    for my $validAppName(@$validAppNames){
      if($validAppName =~ /$regex/){
        $replacements{$fakeAppName} = $validAppName;
        $found = 1;
      }
    }
    if(not $found){
      $replacements{$fakeAppName} = $regex;
    }
  }

  my @replacedConfigItems;
  for my $item(@$configItems){
    if($item =~ /^(dir%.*%\[|\])$/){ #dont replace dir start/end items
      push @replacedConfigItems, $item;
    }else{
      my $replacementItem = $replacements{$item};
      push @replacedConfigItems, defined $replacementItem ? $replacementItem : $item;
    }
  }

  return @replacedConfigItems;
}

sub getAppNamesInDir($){
  my ($dir) = @_;
  my @desktopFiles = glob "$dir/*.desktop";
  my @appNames = map {/\/([^\/]+)\.desktop$/} @desktopFiles;
  return @appNames;
}

sub getValidAppNames(){
  my %okAppNames;
  %okAppNames = (%okAppNames, map {$_ => 1} getAppNamesInDir $allDir);
  %okAppNames = (%okAppNames, map {$_ => 1} getAppNamesInDir $activeDir);
  return sort keys %okAppNames;
}

sub addMissingPrefixes($$){
  my ($apps, $validAppNames) = @_;
  my %jollas;
  my %harbours;
  for my $valid(@$validAppNames){
    if($valid =~ m/^jolla-(.*)$/){
      $jollas{$1} = 1
    }
    if($valid =~ m/^harbour-(.*)$/){
      $harbours{$1} = 1
    }
  }
  my @newApps;
  for my $app(@$apps){
    if(defined $jollas{$app}){
      $app = "jolla-$app";
      print "prepending 'jolla-' to $app\n";
    }elsif(defined $harbours{$app}){
      $app = "harbour-$app";
      print "prepending 'harbour-' to $app\n";
    }
    push @newApps, $app;
  }
  return @newApps;
}

sub createAppDesktopFiles(@){
  my @appNames = @_;
  run "mkdir", "-p", $allDir;
  my @active = glob "$activeDir/*.desktop";

  run "rsync", "-avP", "--ignore-existing", @active, $allDir;
  run "rm -rf $activeDir";
  run "mkdir", "-p", $activeDir;
  for my $app(@appNames){
    run "cp", "-a", "$allDir/$app.desktop", $activeDir;
  }
  for my $pattern(@extraIncludePatterns){
    for my $file(glob "$allDir/$pattern"){
      run "cp", "-a", $file, $activeDir;
    }
  }

  if($USE_ALIENDALVIK){
    run "mkdir", "-p", $origLaunchersUserDir;

    my @userLaunchers = glob "$userDir/*.desktop";
    run "chattr", "-i", @userLaunchers;

    for my $appName(sort keys %androidLaunchers){
      my ($pkg, $intent, $iconPath) = @{$androidLaunchers{$appName}};
      my $pkgNameFmt = $pkg;
      $pkgNameFmt =~ s/_/__/g;
      $pkgNameFmt =~ s/\W/_/g;
      my @launchers = glob "$userDir/apkd_launcher_$pkgNameFmt-*.desktop";
      for my $launcher(@launchers){
        if(not -z $launcher){
          #backup desktop file, create empty placeholder, and prevent deletion
          run "mv", "--no-clobber", $launcher, "$origLaunchersUserDir/";
          run "rm", "-f", $launcher;
          run "touch", $launcher;
          run "chattr", "+i", $launcher;
        }
      }
    }
  }
}

sub writeLipstickConfig(@){
  my @config = @_;
  my $xml = "";
  $xml .= "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n";
  $xml .= "<Menu>\n";
  $xml .= "    <Name></Name>\n";
  my $depth = 1;
  my $indent = "    " x $depth;
  my %dirs;
  for my $configItem(@config){
    if($configItem =~ /^dir%(.*)%(.*)\[$/){
      my ($dirName, $icon) = ($1, $2);
      $icon = $defaultIconName if $icon eq "";
      my $dirFilePath = "$directoryFileDir/dir-$dirName.directory";
      $xml .= "$indent<Menu>\n";
      $xml .= "$indent    <Name>$dirName</Name>\n";
      $xml .= "$indent    <Directory>$dirFilePath</Directory>\n";
      $dirs{$dirFilePath} = $icon;
      $depth++;
      $indent = "    " x $depth;
    }elsif($configItem =~ /^\]$/){
      $depth--;
      $indent = "    " x $depth;
      $xml .= "$indent</Menu>\n"
    }else{
      $xml .= "$indent<Filename>$configItem.desktop</Filename>\n";
    }
  }
  $xml .= "</Menu>\n";

  run "rm -f $directoryFileDir/*.directory";
  for my $dirFilePath(sort keys %dirs){
    my $icon = $dirs{$dirFilePath};
    my $content = "[Desktop Entry]\nIcon=$icon\n";
    open FH, "> $dirFilePath" or die "Couldnt write to $dirFilePath\n";
    print FH $content;
    close FH;
    run "chown", "$user.$user", $dirFilePath;
  }

  open FH, "> $confFileDest" or die "Couldnt write to $confFileDest\n";
  print FH $xml;
  close FH;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
