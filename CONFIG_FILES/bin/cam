#!/usr/bin/perl
use strict;
use warnings;

sub editCamOverridesFile($);
sub readFile($);
sub writeFileSudo($$);
sub run(@);

my @KILL_CAMERA_CMDS = (
  [qw(sudo pkill jolla-camera)],
  [qw(sudo pkill -f ^/usr/bin/harbour-advanced-camera)],
  [qw(sudo pkill -f ^net.sourceforge.opencamera)],
  [qw(sudo pkill -f -9 ^/vendor/bin/hw/android.hardware.camera.provider)],
  [qw(sudo pkill -9 cameraserver)],
);

my $CAM_OVERRIDES_FILE = "/vendor/etc/camera/camxoverridesettings.txt";
my $MULTI_ENABLE_KEY = "multiCameraEnable";

my $USAGE = "Usage:
  $0 -h|--help
    show this message

  $0
  $0 f | -f | fix | --fix
  $0 k | -k | kill | --kill
    run:\n" . join("\n", map {"      @$_"} @KILL_CAMERA_CMDS) . "

  $0 g | -g | get | --get
    -read $MULTI_ENABLE_KEY from
      $CAM_OVERRIDES_FILE
    -print 'multi' if TRUE
    -print 'single' if FALSE
    -print 'unknown' otherwise

  $0 m | -m | multi | --multi
    same as: $0 --set-multi=TRUE

  $0 s | -s | single | --single
    same as: $0 --set-multi=TRUE

  $0 --set-multi=MULTI_ENABLE_VALUE
    -set $MULTI_ENABLE_KEY to MULTI_ENABLE_VALUE (TRUE or FALSE) in
      $CAM_OVERRIDES_FILE
    -kill cameraserver as in $0 --kill

  MULTI_ENABLE_VALUE
    TRUE | true | on | 1
      set $MULTI_ENABLE_KEY=TRUE
    FALSE | false | off | 0
      set $MULTI_ENABLE_KEY=FALSE
";

my $MODE_KILL = "kill";
my $MODE_GET_MULTI = "get-multi";
my $MODE_SET_MULTI = "set-multi";

sub main(@){
  my $mode = $MODE_KILL;
  my $multiVal = undef;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(f|-f|fix|--fix)$/){
      $mode = $MODE_KILL;
    }elsif($arg =~ /^(k|-k|kill|--kill)$/){
      $mode = $MODE_KILL;
    }elsif($arg =~ /^(g|-g|get|--get)$/){
      $mode = $MODE_GET_MULTI;
    }elsif($arg =~ /^(m|-m|multi|--multi)$/){
      $mode = $MODE_SET_MULTI;
      $multiVal = "TRUE";
    }elsif($arg =~ /^(s|-s|single|--single)$/){
      $mode = $MODE_SET_MULTI;
      $multiVal = "FALSE";
    }elsif($arg =~ /^--set-multi=(TRUE|true|on|1)$/){
      $mode = $MODE_SET_MULTI;
      $multiVal = "TRUE";
    }elsif($arg =~ /^--set-multi=(FALSE|false|off|0)$/){
      $mode = $MODE_SET_MULTI;
      $multiVal = "FALSE";
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($mode eq $MODE_KILL){
    run @$_ foreach @KILL_CAMERA_CMDS;
  }elsif($mode eq $MODE_GET_MULTI){
    if(-f $CAM_OVERRIDES_FILE){
      my $contents = readFile $CAM_OVERRIDES_FILE;
      if($contents =~ /^$MULTI_ENABLE_KEY\s*=\s*TRUE\s*$/m){
        print "multi\n";
      }elsif($contents =~ /^$MULTI_ENABLE_KEY\s*=\s*FALSE\s*$/m){
        print "single\n";
      }else{
        print "unknown\n";
      }
    }else{
      print "unknown\n";
    }
  }elsif($mode eq $MODE_SET_MULTI){
    editCamOverridesFile({$MULTI_ENABLE_KEY => $multiVal});
    run @$_ foreach @KILL_CAMERA_CMDS;
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub editCamOverridesFile($){
  my ($keyVals) = @_;
  my @lines = readFile $CAM_OVERRIDES_FILE;
  my $anyEdited = 0;
  for my $key(sort keys %$keyVals){
    my $val = $$keyVals{$key};
    my $keyFound = 0;
    for my $line(@lines){
      if($line =~ /^$key=(.+)$/){
        my $oldVal = $1;
        if($oldVal ne $val){
          $line =~ s/$key=(.+)/$key=$val/;
          print "edit $key: $oldVal => $val\n";
          $anyEdited = 1;
        }
        $keyFound = 1;
        last;
      }
    }
    if(not $keyFound){
      print "add $key: $val\n";
      push @lines, "$key=$val\n";
      $anyEdited = 1;
    }
  }

  if($anyEdited){
    writeFileSudo($CAM_OVERRIDES_FILE, join '', @lines);
  }
}

sub readFile($){
  my ($file) = @_;
  open my $fh, "<", $file or die "ERROR: could not read $file\n$!\n";
  my @lines = <$fh>;
  close $fh;
  if(wantarray){
    return @lines;
  }else{
    return join '', @lines;
  }
}
sub writeFileSudo($$){
  my ($file, $contents) = @_;
  my @writeCmd = ("sudo", "dd", "status=none", "of=$file");
  open my $fh, "|-", @writeCmd or die "ERROR: could not dd $file\n$!\n";
  print $fh $contents;
  close $fh;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
