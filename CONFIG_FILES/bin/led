#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub ensureRoot();
sub selectDevice();
sub readLEDs();
sub writeLEDs($$$);
sub readDevInt($);
sub writeDevInt($$);
sub allInts(@);

my $EXEC = basename $0;

my $DEFAULT_DEVICE_NAME = 'XC';

my $XC_DEV_LEDS_DIR = '/sys/devices/soc.0/7af6000.i2c/i2c-6/6-0042/leds';
my $XZ2C_DEV_LEDS_DIR = '/sys/devices/platform/soc/c440000.qcom,spmi/spmi-0/spmi0-03/c440000.qcom,spmi:qcom,pmi8998@3:qcom,leds@d000/leds';

my $DEVICES = {
  XC => {
    name           => 'Sony Xperia X Compact',
    led_red        => "$XC_DEV_LEDS_DIR/led:rgb_red/brightness",
    led_green      => "$XC_DEV_LEDS_DIR/led:rgb_green/brightness",
    led_blue       => "$XC_DEV_LEDS_DIR/led:rgb_blue/brightness",
    max_brightness => 12,
  },
  XZ2C => {
    name           => 'Sony Xperia XZ2 Compact',
    led_red        => "$XZ2C_DEV_LEDS_DIR/red/brightness",
    led_green      => "$XZ2C_DEV_LEDS_DIR/green/brightness",
    led_blue       => "$XZ2C_DEV_LEDS_DIR/blue/brightness",
    max_brightness => 127,
  },
};

my $DEV = selectDevice();

my @COLORS_ARR = (
  off    => [map{$$DEV{max_brightness}*$_} (0.0, 0.0, 0.0)],
  white  => [map{$$DEV{max_brightness}*$_} (1.0, 1.0, 1.0)],
  black  => [map{$$DEV{max_brightness}*$_} (0.0, 0.0, 0.0)],

  red    => [map{$$DEV{max_brightness}*$_} (1.0, 0.0, 0.0)],
  green  => [map{$$DEV{max_brightness}*$_} (0.0, 1.0, 0.0)],
  blue   => [map{$$DEV{max_brightness}*$_} (0.0, 0.0, 1.0)],

  yellow => [map{$$DEV{max_brightness}*$_} (1.0, 1.0, 0.0)],
  purple => [map{$$DEV{max_brightness}*$_} (1.0, 0.0, 1.0)],
  teal   => [map{$$DEV{max_brightness}*$_} (0.0, 1.0, 1.0)],
);
my %COLORS_BY_NAME = @COLORS_ARR;
my @COLOR_NAMES = map {$COLORS_ARR[$_]} grep {$_ % 2 == 0} (0..$#COLORS_ARR);
my $COLOR_FMT = join "\n    ", map {"$_ => @{$COLORS_BY_NAME{$_}}"} @COLOR_NAMES;

my $usage = "Usage:
  $EXEC -h|--help
    print this message

  $EXEC [-g|--get]
    read LED brightnesses and print them as integers on one line:
      RED_BRIGHTNESS GREEN_BRIGHTNESS BLUE_BRIGHTNESS

  $EXEC [-s|--set] RED_BRIGHTNESS GREEN_BRIGHTNESS BLUE_BRIGHTNESS
    set RGB LED brightness

  $EXEC COLOR
    same as `$EXEC --set` with the following red/green/blue values

  COLOR
    $COLOR_FMT
  RED_BRIGHTNESS
    integer value in $$DEV{led_red}
    (seems to be 0 - 27 on SFOS Xperia X Compact)
  GREEN_BRIGHTNESS
    integer value in $$DEV{led_green}
    (seems to be 0 - 20 on SFOS Xperia X Compact)
  BLUE_BRIGHTNESS
    integer value in $$DEV{led_blue}
    (seems to be 0 - 120 on SFOS Xperia X Compact)
";

sub main(@){
  if(@_ == 1 and $_[0] =~ /^(-h|--help)$/){
    print "$usage\n";
    exit 0;
  }elsif(@_ == 0 or (@_ == 1 and $_[0] =~ /^(-g|--get)$/)){
    my ($r, $g, $b) = readLEDs();
    print "$r $g $b\n";
  }elsif(@_ == 4 and $_[0] =~ /^(-s|--set)$/ and allInts(@_[1..3])){
    my ($r, $g, $b) = ($_[1], $_[2], $_[3]);
    ensureRoot();
    writeLEDs($r, $g, $b);
  }elsif(@_ == 3 and allInts(@_[0..2])){
    my ($r, $g, $b) = ($_[0], $_[1], $_[2]);
    ensureRoot();
    writeLEDs($r, $g, $b);
  }elsif(@_ == 1 and defined $COLORS_BY_NAME{$_[0]}){
    my ($colorName) = @_;
    my ($r, $g, $b) = @{$COLORS_BY_NAME{$colorName}};
    ensureRoot();
    writeLEDs($r, $g, $b);
  }else{
    die $usage;
  }
}

sub ensureRoot(){
  if(`whoami` ne "root\n"){
    exec "sudo", $0, @ARGV;
  }
}

sub selectDevice(){
  my $hwRelease = `cat /etc/hw-release 2>/dev/null`;
  my $targetDevName = $1 if $hwRelease =~ /^NAME="(.+)"$/m;
  if(defined $targetDevName){
    for my $devID(sort keys %$DEVICES){
      my $dev = $$DEVICES{$devID};
      if($targetDevName =~ /$$dev{name}/){
        return $dev;
      }
    }
  }
  return $$DEVICES{$DEFAULT_DEVICE_NAME};
}

sub readLEDs(){
  my $r = readDevInt $$DEV{led_red};
  my $g = readDevInt $$DEV{led_green};
  my $b = readDevInt $$DEV{led_blue};
  return ($r, $g, $b);
}

sub writeLEDs($$$){
  my ($r, $g, $b) = @_;
  writeDevInt $$DEV{led_red}, $r;
  writeDevInt $$DEV{led_green}, $g;
  writeDevInt $$DEV{led_blue}, $b;
}

sub readDevInt($){
  my ($dev) = @_;
  my $val = `cat $dev 2>/dev/null`;
  if($val =~ /^(-?\d+)$/){
    return $1;
  }else{
    die "ERROR: could not read integer from $dev\n";
  }
}

sub writeDevInt($$){
  my ($dev, $val) = @_;
  die "ERROR: $val is not an integer\n" if $val !~ /^-?\d+$/;
  system "echo $val > $dev";
  if($? != 0){
    die "ERROR: could not write $val to $dev\n";
  }
}

sub allInts(@){
  for my $arg(@_){
    if($arg !~ /^-?\d+$/){
      return 0;
    }
  }
  return 1;
}

&main(@ARGV);
