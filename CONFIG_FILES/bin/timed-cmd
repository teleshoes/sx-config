#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use List::Util qw(max);
use Time::HiRes qw(time);

sub readConf($);
sub addEvent($$$$$;$$);
sub addEventFromConfEntry($);
sub cancelEvents(@);
sub getEventCookiesAll();
sub getEventCookiesForApplication($);
sub formatTimedEventInfos(@);
sub getTimedEventInfo($);
sub formatTimedClientScheduleRecurrenceArgs($);
sub getSchedRecurAll();
sub applySchedArgToSchedRecur($$);
sub convertSchedArgsToSchedRecur(@);
sub convertSchedRecurToSchedArgs($);
sub dowLettersToArr($);
sub dowArrToLetters($);
sub arrSameElems($$);
sub run(@);
sub readProc(@);
sub readProcLines(@);
sub nowMillis();

my $CONF_FILE = "$ENV{HOME}/.config/timed-cmd.conf";
my $CONF_TYPE_CMD = "command";
my $CONF_TYPE_ALARM = "alarm";

my $EXEC = basename $0;
my $APPLICATION_TIMED_CMD = do{$_=$EXEC; s/\W+/_/g; $_};
my $APPLICATION_ALARM = "nemoalarms";

my $ATT_HOURS = "Hours";
my $ATT_MINUTES = "Minutes";
my $ATT_MONTHS = "Months";
my $ATT_DAYS_OF_WEEK = "Days of week";
my $ATT_DAYS_OF_MONTH = "Days of month";

my @ALL_HOURS         = (0..23);
my @ALL_MINUTES       = (0..59);
my @ALL_MONTHS        = qw(Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec);
my @ALL_DAYS_OF_WEEK  = qw(Mon Tue Wed Thu Fri Sat Sun);
my @ALL_DAYS_OF_MONTH = (0..31); #no idea why timed includes 0

my @DOW_NAME_ORDER = @ALL_DAYS_OF_WEEK;
my %DOW_NAME_TO_LETTER = (
  Mon=>"m", Tue=>"t", Wed=>"w", Thu=>"T", Fri=>"f", Sat=>"s", Sun=>"S",
);
my @DOW_LETTER_ORDER = map {$DOW_NAME_TO_LETTER{$_}} @DOW_NAME_ORDER;
my %DOW_LETTER_TO_NAME = map {$DOW_NAME_TO_LETTER{$_} => $_} @DOW_NAME_ORDER;

my $SCHED_ARG_REGEX = join("|",
  '(?:hours|minutes|months|days-of-week|days-of-month|date|dow)=(?:.+)',
  '(?:(?:\d|\d\d):(?:\d\d))',
  '(?:@\d+m)',
  '(?:@\d+h)',
);

my $ACTION_GET_EVENTS = "get-events";
my $ACTION_CLEAR = "clear";
my $ACTION_ADD = "add";
my $ACTION_GET_CONFIGS = "get-configs";
my $ACTION_TOGGLE_CONFIG = "toggle-config";
my $ACTION_APPLY_CONFIG = "apply-config";

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS]
  $EXEC [OPTS] -g | --get
    same as: $EXEC OPTS --get-configs

  $EXEC [OPTS] --get-events
    -fetch all timed event <COOKIE>s
      -if --application is given, limit to APPLICATION
    -extract event attributes and parse TITLE + COMMAND + SCHED_ARG_SET
      TITLE is the event attribute TITLE
      COMMAND is the event attribute Action0.COMMAND
      <SCHED_ARG_SET> is the set of <SCHED_ARG> args that will reproduce the sheduling info
    -for each event:
      -if COMMAND is defined, format as:
        <COOKIE> | <TITLE> | <SCHED_ARG_SET> | <COMMAND>
      -otherwise, format as:
        <COOKIE> | <TITLE> | <SCHED_ARG_SET>

  $EXEC [OPTS] --clear
    -fetch all timed event <COOKIE>s
      -if --application is given, limit to APPLICATION
    -for each <COOKIE>, run: timedclient-qt5 --cancel-event <COOKIE>

  $EXEC [OPTS] -a|--add [ONCE | SCHED_ARG SCHED_ARG ..] --title=TITLE --cmd=COMMAND
  $EXEC [OPTS] -a|--add [ONCE | SCHED_ARG SCHED_ARG ..] --title=TITLE --alarm
    -select application
      -if --application is given, use APPLICATION
      -if --cmd is given, use $APPLICATION_TIMED_CMD
      -if --alarm is given, use $APPLICATION_ALARM
    -create timed event to run <COMMAND>
    -defaults to run every minute (which is the smallest granularity)
    -<SCHED_ARG>s control the frequency that <COMMAND> is run

  $EXEC [OPTS] --get-configs
    -fetch the commands + alarms in: $CONF_FILE
    -for commands:
      -parse <TITLE> + <SCHED_ARG_SET> + <COMMAND> + <TIMEOUT>
    -for alarms:
      -parse <TITLE> + <SCHED_ARG_SET>
    -fetch all timed event <COOKIE>s and extract <TITLE> attribute for each <COOKIE>
    -for all commands + alarms, extract <ON_OFF_STATUS> + <COOKIE>
      -if TITLE is the same as for any event <COOKIE>:
        ON_OFF_STATUS=on
        COOKIE_FMT=<COOKIE>
      -otherwise:
        ON_OFF_STATUS=off
        COOKIE_FMT=none
    -print header for commands, and print each command formatted:
      <ON_OFF_STATUS> | <TITLE> | <COOKIE_FMT> | <TIMEOUT> | <COMMAND>
    -print header for alarms, and print each alarm formatted:
      <ON_OFF_STATUS> | <TITLE> | <COOKIE_FMT> | <TIME_HHMM> | <DAYS_OF_WEEK_LETTERS>

  $EXEC [OPTS] -t CONFIG_TITLE
  $EXEC [OPTS] --toggle CONFIG_TITLE
  $EXEC [OPTS] --toggle=CONFIG_TITLE
    -fetch all timed event <COOKIE>s and extract <TITLE> attribute for each <COOKIE>
    -find matching event where <CONFIG_TITLE> = <TITLE>
      -if found:
        -remove event <COOKIE>
      -otherwise:
        -add this event or alarm as in --apply-cookies

  $EXEC [OPTS] --apply-config
    -clear ALL EVENTS as in:
      `$EXEC --clear`
    -parse commands and alarms in $CONF_FILE
      alarm = TITLE | SCHED_ARG_SET
      cmd = TITLE | SCHED_ARG_SET | DAEMON_RUN_TIMEOUT | DAEMON_RUN_ENABLED | SHELL_CMD
      e.g.:
        alarm = work | 7:00 dow=mtwTf
        alarm = wknd | 9:00 dow=sS
        cmd = rotate-logs | \@8h   |   | false | /usr/local/bin/rotate-logs.sh
        cmd = backup-dcim | 05:00 |   | false | /usr/local/bin/backup-dcim.sh
    -for each cmd:
      -get FULL_CMD
        -if DAEMON_RUN_TIMEOUT is given and DAEMON_RUN_ENABLED is true:
          -use FULL_CMD = udo daemon-run --name=TITLE --timeout=DAEMON_RUN_TIMEOUT -v SHELL_CMD
        -otherwise:
          -use FULL_CMD = SHELL_CMD
      -add cmd as in:
        `$EXEC --add --title=TITLE --cmd='FULL_CMD' SCHED_ARG_SET
    -for each alarm:
      -add alarm as in:
        `$EXEC --add --alarm --title=TITLE TIME_HHMM dow=DAYS_OF_WEEK_LETTERS`

  TITLE:
    name of event
    can ONLY contain letters, numbers, underscores, or dashes, for convenience

  COMMAND:
    a shell command
    cannot contain apostrophes/single-quotes ('), for convenience

  ONCE:
    instead of scheduling repeating command,
    add a one-shot to run in TICK_SECONDS from now

    --once=TICK_SECONDS | --tick=TICK_SECOND
      set tick=TICK_SECONDS and dont use schedule

  SCHED_ARG:
    -restrict which minutes a timed event is run for
      (timed events are run at one-minute intervals)
    -<SCHED_ARG>s can restrict by hour, minute, day-of-month, day-of-week, and month
    -by default, all minutes are included
    -each <SCHED_ARG> can restrict the list, and effects are cumulative (applied left to right)
      e.g.:
        \@1h                     =>  once every hour, on the hour
        \@3h minutes=15          =>  once every 3 hours, at a quarter past the hour
        minutes=15 \@3h          =>  once every 3 hours, *ON THE HOUR* ('\@3h' sets 'minutes=0')
        \@5m dow=mtwTf           =>  once every 5 minutes, except on Sat or Sun
        day-of-month=2 05:00    =>  once per month, at 5am on the second day of that month
        09:00 dow=ST months=Nov =>  once daily at 9am, every sunday and thursday in november
        23:59 date=Dec24        =>  once per year, one minute before Newton's birthday

    hours=HOUR,HOUR,HOUR...
      set timed 'Hours: HOUR HOUR HOUR'
    minutes=MIN,MIN,MIN
      set timed 'Minutes: MIN MIN MIN'
    months=MONTH,MONTH,MONTH...
      set timed 'Months: MONTH MONTH MONTH'
    days-of-week=DAY_OF_WEEK,DAY_OF_WEEK,DAY_OF_WEEK
      set timed 'Days of week: DAY_OF_WEEK DAY_OF_WEEK DAY_OF_WEEK'
    days-of-month=DAY_OF_MONTH,DAY_OF_MONTH,DAY_OF_MONTH
      set timed 'Days of month: DAY_OF_MONTH DAY_OF_MONTH DAY_OF_MONTH'

    TIME_HHMM
      clock time, 24-hour, formatted 'HH:MM'
      same as: hours=HH minutes=MM

    date=<MONTH><DAY_OF_MONTH>
      same as: months=MONTH day-of-month=<DAY_OF_MONTH>

    \@<MIN>m
      -schedule the event to run every <MIN> minutes (assuming no other <SCHED_ARG>s)
      -set minutes to all integers from 0 to 59 divisible by <MIN>
      -value must be a proper divisor of 60, i.e.:
        MIN = 1 | 2 | 3 | 4 | 5 | 6 | 10 | 12 | 15 | 20 | 30
      -NOTE: '\@1m' just includes every minute, which is the default, so has no effect
      e.g.:
        \@30m  =>  minutes=0,30
        \@15m  =>  minutes=0,15,30,45
        \@5m   =>  minutes=0,5,10,15,20,25,30,35,40,45,50,55
        \@1m   =>  minutes=`seq -s \",\" 0 59`

    \@<HOUR>h
      -schedule the event to run every <HOUR> hours at minute 0 (assuming no other <SCHED_ARG>s)
      -set minutes to 0
      -set hours to all integers from 0 to 23 divisible by <HOUR>
      -value must be a proper divisor of 24, i.e.:
        HOUR = 1 | 2 | 3 | 4 | 6 | 8 | 12
      e.g.:
        \@8h   =>  minutes=0 hours=0,8,16
        \@4h   =>  minutes=0 hours=0,4,8,12,16,20
        \@1h   =>  minutes=0 hours=`seq -s \",\" 0 23`

    dow=DAYS_OF_WEEK_LETTERS
      parse DAYS_OF_WEEK_LETERS into <DAY_OF_WEEK>,<DAY_OF_WEEK> as follows:
      same as: days-of-week=DAY_OF_WEEK,DAY_OF_WEEK,DAY_OF_WEEK
        DAYS_OF_WEEK_LETTERS
          any combination of one or more of these CASE-SENSITIVE letters:
            m = Mon
            t = Tue
            w = Wed
            T = Thu
            f = Fri
            s = Sat
            S = Sun
          CASE-SENSITIVE, duplicates and order are ignored
            (the abbreviations come from nemoalarms app)
          e.g.:   mwf     => Mon,Wed,Fri
                  fmw     => Mon,Wed,Fri
                  ST      => Thu,Sun
                  SmtwTfs => Mon,Tue,Wed,Thu,Fri,Sat,Sun
                  S       => Sun

  OPTS
    --application=APPLICATION | --app=APPLICATION
      for --add:
        use APPLICATION instead of $APPLICATION_TIMED_CMD or $APPLICATION_ALARM
      for --get-events:
        only list events for APPLICATION
      for --clear:
        only cancel events for APPLICATION
    --app-cmd
      same as: --application=$APPLICATION_TIMED_CMD
    --app-alarm
      same as: --application=$APPLICATION_ALARM

    --alarm
      -use application=$APPLICATION_ALARM instead of $APPLICATION_TIMED_CMD
      -extract <HH> + <MM> and <DAYS_OF_WEEK_LETTERS> from <SCHED_ARG>s
        -fail unless exactly one 'hours' and one 'minutes' is specified
      -add EXTRA_EVENT_ARG entries as in:
        --event-arg=\"createdDate=`date +%s%3N`\" \\
        --event-arg=\"daysOfWeek=<DAYS_OF_WEEK_LETTERS>\" \\
        --event-arg=\"timeOfDayWithSeconds=\$((<HH>*60*60 + <MM>*60))\" \\
        --event-arg=\"maximalTimeoutSnoozeCounter=2\" \\
        --event-arg=\"type=clock\" \\
        --event-arg=\"alarm\" \\
        --event-arg=\"reminder\" \\
        --event-arg=\"boot\" \\
        --event-arg=\"keepAlive\" \\
        --event-arg=\"alignedSnooze\" \\

    --event-arg=EXTRA_EVENT_ARG
      (can be given multiple times)
      when adding events, append EXTRA_EVENT_ARG to semicolon-separated list
        in 'timedclient-t5 -e'
";

sub main(@){
  my $action = $ACTION_GET_CONFIGS;
  my $opts = {
    application    => undef,
    isAlarm        => 0,
    title          => undef,
    shellCmd       => undef,
    onceTick       => undef,
    schedRecur     => undef,
    extraEventArgs => undef,
  };
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print "$USAGE\n";
      exit 0;
    }elsif($arg =~ /^(?:--app|--application)=(.+)$/){
      $$opts{application} = $1;
    }elsif($arg =~ /^(--app-cmd)$/){
      $$opts{application} = $APPLICATION_TIMED_CMD;
    }elsif($arg =~ /^(--app-alarm)$/){
      $$opts{application} = $APPLICATION_ALARM;
    }elsif($arg =~ /^--event-arg=(.+)$/){
      $$opts{extraEventArgs} = [] if not defined $$opts{extraEventArgs};
      push @{$$opts{extraEventArgs}}, $1;
    }elsif($arg =~ /^(-g|--get)$/){
      $action = $ACTION_GET_CONFIGS;
    }elsif($arg =~ /^(--get-events)$/){
      $action = $ACTION_GET_EVENTS;
    }elsif($arg =~ /^(--clear)$/){
      $action = $ACTION_CLEAR;
    }elsif($arg =~ /^(-a|--add)$/){
      $action = $ACTION_ADD;
    }elsif($arg =~ /^--get-configs$/){
      $action = $ACTION_GET_CONFIGS;
    }elsif($arg =~ /^(-t|--toggle)$/ and @_ > 0){
      $action = $ACTION_TOGGLE_CONFIG;
      $$opts{title} = shift @_;
    }elsif($arg =~ /^--toggle=(.+)$/){
      $action = $ACTION_TOGGLE_CONFIG;
      $$opts{title} = $1;
    }elsif($arg =~ /^(--apply-config)$/){
      $action = $ACTION_APPLY_CONFIG;
    }elsif($arg =~ /^(--alarm)$/){
      $$opts{isAlarm} = 1;
    }elsif($arg =~ /^--title=(.+)$/){
      $$opts{title} = $1;
    }elsif($arg =~ /^--cmd=(.*)$/){
      $$opts{shellCmd} = $1;
    }elsif($arg =~ /^(?:--once|--tick)=(\d+)$/){
      $$opts{onceTick} = $1;
    }elsif($arg =~ /^($SCHED_ARG_REGEX)$/){
      $$opts{schedRecur} = getSchedRecurAll() if not defined $$opts{schedRecur};
      applySchedArgToSchedRecur($$opts{schedRecur}, $arg);
    }else{
      die "$USAGE\nERROR: unknown arg '$arg'\n";
    }
  }

  if($action eq $ACTION_GET_EVENTS){
    die "$USAGE\nERROR: cannot specify SCHED_ARG for action=$action\n" if defined $$opts{schedRecur};
    die "$USAGE\nERROR: cannot specify TITLE for action=$action\n" if defined $$opts{title};
    die "$USAGE\nERROR: cannot specify COMMAND for action=$action\n" if defined $$opts{shellCmd};

    my @cookies = defined $$opts{application} ?
      getEventCookiesForApplication($$opts{application}) : getEventCookiesAll();
    my @timedEventInfos = map {getTimedEventInfo($_)} @cookies;

    print formatTimedEventInfos(@timedEventInfos);
  }elsif($action eq $ACTION_CLEAR){
    die "$USAGE\nERROR: cannot specify SCHED_ARG for action=$action\n" if defined $$opts{schedRecur};
    die "$USAGE\nERROR: cannot specify TITLE for action=$action\n" if defined $$opts{title};
    die "$USAGE\nERROR: cannot specify COMMAND for action=$action\n" if defined $$opts{shellCmd};

    my @cookies = defined $$opts{application} ?
      getEventCookiesForApplication($$opts{application}) : getEventCookiesAll();
    cancelEvents(@cookies);
  }elsif($action eq $ACTION_ADD){
    die "$USAGE\nERROR: must specify TITLE for action=$action\n" if not defined $$opts{title};

    addEvent(
      $$opts{title}, $$opts{shellCmd}, $$opts{isAlarm}, $$opts{schedRecur}, $$opts{onceTick},
      $$opts{extraEventArgs}, $$opts{application}
    );
  }elsif($action eq $ACTION_GET_CONFIGS){
    die "$USAGE\nERROR: cannot specify SCHED_ARG for action=$action\n" if defined $$opts{schedRecur};
    die "$USAGE\nERROR: cannot specify TITLE for action=$action\n" if defined $$opts{title};
    die "$USAGE\nERROR: cannot specify COMMAND for action=$action\n" if defined $$opts{shellCmd};
    die "$USAGE\nERROR: cannot specify APPLICATION for action=$action\n" if defined $$opts{application};

    my $conf = readConf($CONF_FILE);

    my @allCookies = getEventCookiesAll();
    my %eventInfosByCookie = map {$_ => getTimedEventInfo $_} @allCookies;
    my %cookiesByTitle = map {$eventInfosByCookie{$_}{TITLE} => $_} sort @allCookies;

    my @events = @{$$conf{events}};

    my $maxTitleLen = max(map {length $$_{title}} @events);
    for my $event(@{$$conf{events}}){
      my $cookie = $cookiesByTitle{$$event{title}};
      my $status;
      if(defined $cookie){
        $status = "on";
      }else{
        $status = "off";
        $cookie = "none";
      }
      printf "%-3s | %-${maxTitleLen}s | %4s | %-15s | %5s | %s\n",
        $status,
        $$event{title},
        $cookie,
        $$event{schedArgs},
        ($$event{timeout} ? $$event{timeout} : "-"x5),
        ($$event{type} eq $CONF_TYPE_ALARM ? "<ALARM>" : $$event{cmd}),
      ;
    }
 }elsif($action eq $ACTION_TOGGLE_CONFIG){
    die "$USAGE\nERROR: cannot specify SCHED_ARG for action=$action\n" if defined $$opts{schedRecur};
    die "$USAGE\nERROR: cannot specify COMMAND for action=$action\n" if defined $$opts{shellCmd};
    die "$USAGE\nERROR: must specify TITLE for action=$action\n" if not defined $$opts{title};
    die "$USAGE\nERROR: cannot specify APPLICATION for action=$action\n" if defined $$opts{application};

    my $conf = readConf($CONF_FILE);
    my $targetConfEvent = undef;
    for my $event(@{$$conf{events}}){
      if($$event{title} eq $$opts{title}){
        $targetConfEvent = $event;
        last;
      }
    }
    if(not defined $targetConfEvent){
      die "ERROR: unknown conf entry title '$$opts{title}'\n";
    }

    my @allCookies = getEventCookiesAll();
    my %eventInfosByCookie = map {$_ => getTimedEventInfo $_} @allCookies;
    my %cookiesByTitle = map {$eventInfosByCookie{$_}{TITLE} => $_} sort @allCookies;

    if(defined $cookiesByTitle{$$opts{title}}){
      run "timedclient-qt5", "--cancel-event", $cookiesByTitle{$$opts{title}};
    }else{
      addEventFromConfEntry($targetConfEvent);
    }
  }elsif($action eq $ACTION_APPLY_CONFIG){
    die "$USAGE\nERROR: cannot specify SCHED_ARG for action=$action\n" if defined $$opts{schedArgs};
    die "$USAGE\nERROR: cannot specify TITLE for action=$action\n" if defined $$opts{title};
    die "$USAGE\nERROR: cannot specify COMMAND for action=$action\n" if defined $$opts{shellCmd};
    die "$USAGE\nERROR: cannot specify APPLICATION for action=$action\n" if defined $$opts{application};
    my $conf = readConf($CONF_FILE);

    print "\nclearing all events\n";
    run $EXEC, "--clear";
    print "\n\n\n";

    print "\nadding conf events\n";
    for my $event(@{$$conf{events}}){
      print "\n";
      addEventFromConfEntry($event);
    }

    print "\n\nall events:\n";
    run $EXEC, "--get-events";
  }else{
    die "ERROR: unknown action $action\n";
  }
}

sub readConf($){
  my ($file) = @_;
  my $conf = {
    events => [],
  };
  if(not -e $file){
    return $conf;
  }

  open my $fh, "<", $file or die "ERROR: could not read $file\n$!\n";
  my @lines = <$fh>;
  close $fh;

  my $schedArgsRegex = "(?:(?:(?:$SCHED_ARG_REGEX)\\s+)*(?:$SCHED_ARG_REGEX))";
  for my $line(@lines){
    next if $line =~ /^\s*(#.*)?$/;
    if($line =~ /^
      \s* alarm \s* =
      \s* (\w+) \s* \|
      \s* ($schedArgsRegex)
      $
    /xi){
      push @{$$conf{events}}, {
        type      => $CONF_TYPE_ALARM,
        title     => $1,
        schedArgs => $2,
        timeout   => undef,
        useDaemon => undef,
        cmd       => undef,
      };
    }elsif($line =~ /
      ^
      \s* cmd \s* =
      \s* ([a-zA-Z0-9_\-]+) \s* \|
      \s* ($schedArgsRegex) \s* \|
      \s* ((?:0|\d+(?:ms|s|m|min|h|d))?) \s* \|
      \s* (true|false) \s* \|
      \s* (\S.*\S)
      \s*
      $
    /xi){
      push @{$$conf{events}}, {
        type      => $CONF_TYPE_CMD,
        title     => $1,
        schedArgs => $2,
        timeout   => $3,
        useDaemon => $4,
        cmd       => $5,
      };
    }else{
      die "ERROR: malformed entry in $CONF_FILE\n$line";
    }
  }

  return $conf;
}

sub addEvent($$$$$;$$){
  my (
    $title, $shellCmd, $isAlarm, $schedRecur, $onceTick,
    $optionalExtraEventArgs, $optionalApplication
  ) = @_;

  my $application = $optionalApplication if defined $optionalApplication;
  my @extraEventArgs = @{$optionalExtraEventArgs} if defined $optionalExtraEventArgs;

  if($isAlarm){
    $application = $APPLICATION_ALARM if not defined $application;
    if(defined $shellCmd){
      die "$USAGE\nERROR: cannot specify COMMAND for --alarm\n";
    }
    my $hours = $$schedRecur{$ATT_HOURS} if defined $schedRecur;
    my $minutes = $$schedRecur{$ATT_MINUTES} if defined $schedRecur;
    my $daysOfWeek = $$schedRecur{$ATT_DAYS_OF_WEEK} if defined $schedRecur;
    if(not defined $hours or not defined $minutes or @$hours != 1 or @$minutes != 1){
      die "ERROR: must specify exactly one hour and one minute value for --alarm\n";
    }
    my $timeOfDayS = $$hours[0]*60*60 + $$minutes[0]*60;
    my $dowLettersStr = dowNamesArrToLettersStr(@$daysOfWeek) if defined $daysOfWeek;

    push @extraEventArgs, "createdDate=" . nowMillis();
    push @extraEventArgs, "timeOfDayWithSeconds=$timeOfDayS";
    if(not arrSameElems([@$daysOfWeek], [@ALL_DAYS_OF_WEEK])){
      push @extraEventArgs, "daysOfWeek=$dowLettersStr";
    }

    #NOTE: maximalTimeoutSnoozeCounter is not implemented in timedclient-qt5 as of 3.6.13-1.8.1
    push @extraEventArgs, "maximalTimeoutSnoozeCounter=2";
    push @extraEventArgs, "type=clock";
    push @extraEventArgs, "alarm";
    push @extraEventArgs, "reminder";
    push @extraEventArgs, "boot";
    push @extraEventArgs, "keepAlive";
    push @extraEventArgs, "alignedSnooze";
  }else{
    $application = $APPLICATION_TIMED_CMD if not defined $application;
    if(not defined $shellCmd){
      die "$USAGE\nERROR: must specify COMMAND if --alarm is not given\n";
    }
  }

  if(defined $onceTick and defined $schedRecur){
    die "ERROR: cannot specify both SCHED_ARG and ONCE\n";
  }

  if($title !~ /^[a-zA-Z0-9_\-]+$/){
    die "ERROR: TITLE can only contain letters/numbers/underscores/dashes\n";
  }
  if(defined $shellCmd and $shellCmd =~ /'/){
    die "ERROR: COMMAND cannot contain apostrophes (for $EXEC convenience)\n";
  }

  my @actionArgs = defined $shellCmd ? ("whenDue", "runCommand=$shellCmd") : ();
  my @eventArgs = (
    "APPLICATION=$application",
    "TITLE=$title",
    @extraEventArgs,
  );

  my @recurrenceArgs;
  if(defined $onceTick){
    print "adding command with ticker=$onceTick\n";
    push @eventArgs, "ticker=$onceTick";
    @recurrenceArgs = ();
  }else{
    printf "adding command with schedule: %s\n",
      join(" ", convertSchedRecurToSchedArgs($schedRecur));
    @recurrenceArgs = formatTimedClientScheduleRecurrenceArgs($schedRecur);
  }

  my @cmd = ("timedclient-qt5");
  @cmd = (@cmd, "-a", join(";", @actionArgs)) if @actionArgs > 0;
  @cmd = (@cmd, "-r", join(";", @recurrenceArgs)) if @recurrenceArgs > 0;
  @cmd = (@cmd, "-e", join(";", @eventArgs)) if @eventArgs > 0;

  run @cmd;
}

sub addEventFromConfEntry($){
  my ($confEntry) = @_;
  if($$confEntry{type} eq $CONF_TYPE_CMD){
    my $title = $$confEntry{title};
    my @schedArgs = split /\s+/, $$confEntry{schedArgs};
    my $timeout = $$confEntry{timeout};
    my $useDaemon = $$confEntry{useDaemon};
    my $shellCmd = $$confEntry{cmd};

    $timeout = "" if not defined $timeout;

    my @fullCmd;
    if($useDaemon =~ /true/){
      if($timeout !~ /^(0|\d+(ms|s|m|min|h|d))$/){
        die "ERROR: invalid timeout for '$title' conf\n";
      }
      @fullCmd = (
        "udo", "daemon-run",
        "--name=$title", "--timeout=$timeout",
        "--verbose",
        $shellCmd,
      );
    }else{
      if($timeout ne ""){
        die "ERROR: cannot specify timeout with daemon=false for '$title' conf\n";
      }
      @fullCmd = ($shellCmd);
    }

    run $EXEC, "--add", "--title=$title", "--cmd=@fullCmd", @schedArgs;
  }elsif($$confEntry{type} eq $CONF_TYPE_ALARM){
    my $title = $$confEntry{title};
    my @schedArgs = split /\s+/, $$confEntry{schedArgs};
    run $EXEC, "--add", "--alarm", "--title=$title", @schedArgs;
  }else{
    die "ERROR: unknown conf type $$confEntry{type}\n";
  }
}

sub cancelEvents(@){
  my @cookies = @_;
  for my $cookie(@cookies){
    run "timedclient-qt5", "--cancel-event", $cookie;
  }
}

sub getEventCookiesAll(){
  my $out = readProc "timedclient-qt5", "--list";
  return $out =~ /\d+/g;
}

sub getEventCookiesForApplication($){
  my ($application) = @_;
  my $out = readProc "timedclient-qt5", "--search", "APPLICATION=$application";
  return $out =~ /\d+/g;
}

sub formatTimedEventInfos(@){
  my @timedEventInfos = @_;

  my $fmt = "";

  my $maxTitleLen = max(map {length $$_{TITLE}} @timedEventInfos);
  for my $eventInfo(@timedEventInfos){
    my $eventFmt;
    if(defined $$eventInfo{COMMAND}){
      $eventFmt = sprintf "%4d | %-${maxTitleLen}s | %3s | %s",
        $$eventInfo{cookie},
        $$eventInfo{TITLE},
        $$eventInfo{timingFmt},
        $$eventInfo{COMMAND},
      ;
    }else{
      $eventFmt = sprintf "%4d | %-${maxTitleLen}s | %s",
        $$eventInfo{cookie},
        $$eventInfo{TITLE},
        $$eventInfo{timingFmt},
      ;
    }
    $fmt .= "$eventFmt\n";
  }

  return $fmt;
}

sub getTimedEventInfo($){
  my ($eventCookie) = @_;
  my @lines = readProcLines "timedclient-qt5", "--get-event=$eventCookie";
  my $okAtts = join "|", qw(APPLICATION TITLE COMMAND USER);
  my @schedAtts = (
    $ATT_HOURS, $ATT_MINUTES, $ATT_MONTHS, $ATT_DAYS_OF_WEEK, $ATT_DAYS_OF_MONTH,
  );
  my $okSchedAtts = join "|", @schedAtts;

  my $event = {
    cookie => $eventCookie,
    schedRecur => undef,
    tick => undef,
    tickFmt => undef,
    timingFmt => undef,
  };
  for my $line(@lines){
    if($line =~ /^\s*($okAtts)\s*=\s*'(.+)'$/){
      $$event{$1} = $2;
    }elsif($line =~ /^\s*($okSchedAtts)\s*:\s*(.+)$/){
      my ($att, $val) = ($1, $2);
      my @vals = split /\s+/, $val;
      $$event{schedRecur} = {} if not defined $$event{schedRecur};
      $$event{schedRecur}{$att} = [@vals];
    }elsif($line =~ /^\s*Tick: (\d+) \((.*)\)$/){
      $$event{tick} = $1;
      $$event{tickFmt} = $2;
    }
  }

  if(defined $$event{schedRecur}){
    for my $schedAtt(@schedAtts){
      if(not defined $$event{schedRecur}{$schedAtt}){
        die "ERROR: missing $schedAtt for event=$eventCookie\n";
      }
    }
  }

  if(defined $$event{schedRecur}){
    $$event{timingFmt} = join(" ", convertSchedRecurToSchedArgs($$event{schedRecur}));
  }elsif(defined $$event{tick}){
    $$event{timingFmt} = "$$event{tick} ($$event{tickFmt})";
  }

  return $event;
}

sub formatTimedClientScheduleRecurrenceArgs($){
  my ($schedRecur) = @_;
  my @args;

  for my $hour(@{$$schedRecur{$ATT_HOURS}}){
    push @args, "hour=$hour";
  }

  for my $minute(@{$$schedRecur{$ATT_MINUTES}}){
    push @args, "minute=$minute";
  }

  if(arrSameElems($$schedRecur{$ATT_DAYS_OF_MONTH}, [@ALL_DAYS_OF_MONTH])){
    push @args, "everyDayOfMonth";
  }else{
    @args = (@args, map {"dayOfMonth=$_"} @{$$schedRecur{$ATT_DAYS_OF_MONTH}});
  }

  if(arrSameElems($$schedRecur{$ATT_DAYS_OF_WEEK}, [@ALL_DAYS_OF_WEEK])){
    push @args, "everyDayOfWeek";
  }else{
    @args = (@args, map {"dayOfWeek=$_"} @{$$schedRecur{$ATT_DAYS_OF_WEEK}});
  }

  if(arrSameElems($$schedRecur{$ATT_MONTHS}, [@ALL_MONTHS])){
    push @args, "everyMonth";
  }else{
    for my $month(@{$$schedRecur{$ATT_MONTHS}}){
      my %monthIds = (
        Jan => 2,
        Feb => 3,
        Mar => 4,
        Apr => 5,
        May => 6,
        Jun => 7,
        Jul => 8,
        Aug => 9,
        Sep => 10,
        Oct => 11,
        Nov => 12,
        Dec => undef,
      );
      my $monthId = $monthIds{$month};
      if(not defined $monthId){
        die "ERROR: some ridiculous timed off-by-one bug thinks Jan=2 and Dec=13\n"
          . "this completely prevents adding December, except with everyMonth...\n"
          ;
      }
      push @args, "month=$monthId";
    }
  }
  return @args;
}

sub getSchedRecurAll(){
  return {
    $ATT_DAYS_OF_WEEK   => [@ALL_DAYS_OF_WEEK],
    $ATT_MONTHS         => [@ALL_MONTHS],
    $ATT_DAYS_OF_MONTH  => [@ALL_DAYS_OF_MONTH],
    $ATT_HOURS          => [@ALL_HOURS],
    $ATT_MINUTES        => [@ALL_MINUTES],
  };
}

sub applySchedArgToSchedRecur($$){
  my ($schedRecur, $schedArg) = @_;

  my $okMonthsRegex = join "|", @ALL_MONTHS;

  if($schedArg =~ /^(\d|\d\d):(\d\d)$/){
    my ($h, $m) = ($1, $2);
    $$schedRecur{$ATT_HOURS} = [0+$h];
    $$schedRecur{$ATT_MINUTES} = [0+$m];
  }elsif($schedArg =~ /^\@(\d+)m$/){
    my $minFactor = 0+$1;
    if($minFactor !~ /^(1|2|3|4|5|6|10|12|15|20|30)$/){
      die "ERROR: \@<MIN>m can only accept proper divisors of 60\n";
    }
    my @divisibleMin = grep {$_ % $minFactor == 0} @ALL_MINUTES;
    $$schedRecur{$ATT_MINUTES} = [@divisibleMin];
  }elsif($schedArg =~ /^\@(\d+)h$/){
    my $hrFactor = 0+$1;
    if($hrFactor !~ /^(1|2|3|4|6|8|12)$/){
      die "ERROR: \@<HOUR>h can only accept proper divisors of 24\n";
    }
    my @divisibleHr = grep {$_ % $hrFactor == 0} @ALL_HOURS;
    $$schedRecur{$ATT_HOURS} = [@divisibleHr];
    $$schedRecur{$ATT_MINUTES} = [0];
  }elsif($schedArg =~ /^date=($okMonthsRegex)(\d+)$/){
    my ($month, $dayOfMonth) = ($1, $2);
    $dayOfMonth = 0 + $dayOfMonth;
    die "ERROR: invalid day-of-month in $schedArg\n" if $dayOfMonth > 31;
    $$schedRecur{$ATT_MONTHS} = [$month];
    $$schedRecur{$ATT_DAYS_OF_MONTH} = [$dayOfMonth];
  }elsif($schedArg =~ /^dow=(.+)$/){
    my $dowLetterStr = $1;
    $$schedRecur{$ATT_DAYS_OF_WEEK} = [dowLettersStrToNamesArr($dowLetterStr)];
  }else{
    my ($field, $valStr) = ($1, $2) if $schedArg =~ /^([^=]+)=([^=]+)$/;
    $field = "" if not defined $field;
    $valStr = "" if not defined $valStr;

    my @vals = split /,/, $valStr;
    @vals = map {$_ =~ /^0+([1-9]\d*)/ ? $1 : $_} @vals; #strip leading 0s safely
    my %okVals = map {$_ => 1} @vals;

    my ($att, @allVals);
    if($field eq "hours"){
      $att = $ATT_HOURS;
      @allVals = @ALL_HOURS;
    }elsif($field eq "minutes"){
      $att = $ATT_MINUTES;
      @allVals = @ALL_MINUTES;
    }elsif($field eq "months"){
      $att = $ATT_MONTHS;
      @allVals = @ALL_MONTHS;
    }elsif($field eq "days-of-week"){
      $att = $ATT_DAYS_OF_WEEK;
      @allVals = @ALL_DAYS_OF_WEEK;
    }elsif($field eq "days-of-month"){
      $att = $ATT_DAYS_OF_MONTH;
      @allVals = @ALL_DAYS_OF_MONTH;
    }else{
      die "ERROR: unknown SCHED_ARG $schedArg\n";
    }

    my $allValRegex = join "|", @allVals;
    for my $val(@vals){
      die "ERROR: unknown '$att' value $val\n" if $val !~ /^($allValRegex)$/;
    }
    $$schedRecur{$att} = [grep {$okVals{$_}} @allVals];
  }
}

sub convertSchedArgsToSchedRecur(@){
  my @schedArgs = @_;
  my $schedRecur = getSchedRecurAll();
  applySchedArgToSchedRecur($schedRecur, $_) foreach @schedArgs;
  return $schedRecur;
}

sub convertSchedRecurToSchedArgs($){
  my ($schedRecur) = @_;
  my $hr = $$schedRecur{$ATT_HOURS};
  my $min = $$schedRecur{$ATT_MINUTES};
  my $months = $$schedRecur{$ATT_MONTHS};
  my $daysOfWeek = $$schedRecur{$ATT_DAYS_OF_WEEK};
  my $daysOfMonth = $$schedRecur{$ATT_DAYS_OF_MONTH};

  if(@$hr == 0 or @$min ==0 or @$months == 0 or @$daysOfWeek == 0 or @$daysOfMonth == 0){
    die "ERROR: must have at least one element in every field of schedRecur\n";
  }

  my $isAllHr = arrSameElems($hr, [@ALL_HOURS]);
  my $isAllMin = arrSameElems($min, [@ALL_MINUTES]);
  my $isAllMonths = arrSameElems($months, [@ALL_MONTHS]);
  my $isAllDaysOfWeek = arrSameElems($daysOfWeek, [@ALL_DAYS_OF_WEEK]);
  my $isAllDaysOfMonth = arrSameElems($daysOfMonth, [@ALL_DAYS_OF_MONTH]);

  my $isTimeParsed = 0;

  my @schedArgs;

  #handle special cases for hour + min
  if(not $isTimeParsed and @$hr == 1 and @$min == 1){
    ## <HH>:<MM>
    push @schedArgs, sprintf("%02d:%02d", $$hr[0], $$min[0]);
    $isTimeParsed = 1;
  }
  if(not $isTimeParsed and @$min == 1){
    # @<HOUR>h
    for my $hrFactor(qw(1 2 3 4 6 8 12)){
      my @divisibleHours = grep {$_ % $hrFactor == 0} @ALL_HOURS;
      if(arrSameElems($hr, [@divisibleHours])){
        push @schedArgs, "\@${hrFactor}h";
        push @schedArgs, "minutes=$$min[0]" if $$min[0] != 0;
        $isTimeParsed = 1;
        last;
      }
    }
  }
  if(not $isTimeParsed and $isAllHr){
    # @<MIN>m
    for my $minFactor(qw(1 2 3 4 5 6 10 12 15 20 30)){
      my @divisibleMin = grep {$_ % $minFactor == 0} @ALL_MINUTES;
      if(arrSameElems($min, [@divisibleMin])){
        push @schedArgs, "\@${minFactor}m";
        $isTimeParsed = 1;
        last;
      }
    }
  }

  my $isDateParsed = 0;
  if(@$months == 1 and @$daysOfMonth == 1){
    push @schedArgs, sprintf("date=%s%02d", $$months[0], $$daysOfMonth[0]);
    $isDateParsed = 1;
  }

  my $skipHours = $isTimeParsed || $isAllHr;
  my $skipMinutes = $isTimeParsed || $isAllHr;
  my $skipMonths = $isDateParsed || $isAllMonths;
  my $skipDow = $isAllDaysOfWeek;
  my $skipDaysOfMonth = $isDateParsed || $isAllDaysOfMonth;

  push @schedArgs, sprintf("hours=%s", join(",", @$hr)) if not $skipHours;
  push @schedArgs, sprintf("minutes=%s", join(",", @$min)) if not $skipMinutes;
  push @schedArgs, sprintf("months=%s", join(",", @$months)) if not $skipMonths;
  push @schedArgs, sprintf("dow=%s", dowNamesArrToLettersStr(@$daysOfWeek)) if not $skipDow;
  push @schedArgs, sprintf("days-of-month=%s", join(",", @$daysOfMonth)) if not $skipDaysOfMonth;

  return @schedArgs;
}

sub dowLettersStrToNamesArr($){
  my ($dowLettersStr) = @_;
  my %okLetters = map {$_ => 1} split //, $dowLettersStr;
  for my $letter(sort keys %okLetters){
    die "ERROR: unknown DOW letter $letter\n" if not defined $DOW_LETTER_TO_NAME{$letter};
  }
  return map {$DOW_LETTER_TO_NAME{$_}} grep {defined $okLetters{$_}} @DOW_LETTER_ORDER
}
sub dowNamesArrToLettersStr(@){
  my (@dowNamesArr) = @_;
  my %okNames = map {$_ => 1} @dowNamesArr;
  for my $name(sort keys %okNames){
    die "ERROR: unknown DOW name $name\n" if not defined $DOW_NAME_TO_LETTER{$name};
  }
  return join "", map {$DOW_NAME_TO_LETTER{$_}} grep {defined $okNames{$_}} @DOW_NAME_ORDER;
}

sub arrSameElems($$){
  my ($arrRef1, $arrRef2) = @_;
  return 0 if @$arrRef1 != @$arrRef2;

  my @arr1 = sort @$arrRef1;
  my @arr2 = sort @$arrRef2;
  for(my $i=0; $i<@arr1; $i++){
    if($arr1[$i] ne $arr2[$i]){
      return 0;
    }
  }
  return 1;
}

sub run(@){
  print "@_\n";
  system @_;
  die "ERROR: \"@_\" failed\n" if $? != 0;
}
sub readProc(@){
  return join '', readProcLines @_;
}
sub readProcLines(@){
  open CMD, "-|", @_ or die "ERROR: could not run \"@_\"\n$!\n";
  my @lines = <CMD>;
  close CMD;
  die "ERROR: \"@_\" failed\n" if $? != 0;
  return @lines;
}

sub nowMillis(){
  return int(time * 1000.0 + 0.5);
}

&main(@ARGV);
