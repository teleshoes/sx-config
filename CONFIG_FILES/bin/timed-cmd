#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use Time::HiRes qw(time);

sub readConf($);
sub getTimedCmdEventCookies($);
sub getTimedCmdEventInfo($);
sub getScheduleRecurrenceArgs($);
sub parseSchedule($);
sub parseDaysOfTheWeekLetters($);
sub filterByDivisible($$);
sub arrSameElems($$);
sub run(@);
sub readProc(@);
sub readProcLines(@);
sub nowMillis();

my $CONF_FILE = "$ENV{HOME}/.config/timed-cmd.conf";

my $EXEC = basename $0;
my $APPLICATION_TIMED_CMD = do{$_=$EXEC; s/\W+/_/g; $_};
my $ALARM_APPLICATION = "nemoalarms";

my $ATT_DAYS_OF_WEEK = "Days of week";
my $ATT_MONTHS = "Months";
my $ATT_DAYS_OF_MONTH = "Days of month";
my $ATT_HOURS = "Hours";
my $ATT_MINUTES = "Minutes";

my $SCHED_ALL = {
  $ATT_DAYS_OF_WEEK  => [qw(Sun Mon Tue Wed Thu Fri Sat)],
  $ATT_MONTHS        => [qw(Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec)],
  $ATT_DAYS_OF_MONTH => [0..31], #why 0, i couldnt say
  $ATT_HOURS         => [0..23],
  $ATT_MINUTES       => [0..59],
};

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS]
  $EXEC [OPTS] -g | --get
    -fetch timed event cookies with APPLICATION=$APPLICATION_TIMED_CMD
    -extract schedule/command info for each cookie
    -print one per line:
      cookie,title,human-readable-schedule,command

  $EXEC [OPTS] --clear
    -fetch timed event cookies with APPLICATION=$APPLICATION_TIMED_CMD
    -run --cancel-event for each cookie

  $EXEC [OPTS] -a|--add [ONCE | SCHED SCHED ..] --title=TITLE --cmd=COMMAND
    -create timed event to run <COMMAND>
    -defaults to run every minute (which is the smallest granularity)
    -<SCHED> reduces the frequency that <COMMAND> is run

  $EXEC [OPTS] --get-alarms
    same as: $EXEC OPTS --application=$ALARM_APPLICATION --get

  $EXEC [OPTS] --clear-alarms
    same as: $EXEC OPTS --application=$ALARM_APPLICATION --clear

  $EXEC [OPTS] --add-alarm TITLE TIME_HHMM DAYS_OF_WEEK_LETTERS
    same as: $EXEC OPTS \\
      --add \\
      --application=$ALARM_APPLICATION \\
      --title=TITLE \\
      --no-cmd \\
      --time=TIME_HHMM \\
      --days-of-week-letters=DAYS_OF_WEEK_LETTERS \\
      --event-arg=\"createdDate=`date +%s%3N`\" \\
      --event-arg=\"daysOfWeek=DAYS_OF_WEEK_LETTERS\" \\
      --event-arg=\"timeOfDayWithSeconds=\$((HH*60*60 + MM*60))\" \\
      --event-arg=\"maximalTimeoutSnoozeCounter=2\" \\
      --event-arg=\"type=clock\" \\
      --event-arg=\"alarm\" \\
      --event-arg=\"reminder\" \\
      --event-arg=\"boot\" \\
      --event-arg=\"keepAlive\" \\
      --event-arg=\"alignedSnooze\" \\
    ;

  $EXEC [OPTS] --apply-config
    -clear all $APPLICATION_TIMED_CMD events as in:
      `$EXEC --clear`
    -parse commands and alarms in $CONF_FILE
      alarm = TITLE TIME_HHMM DAYS_OF_WEEK_LETTERS
      cmd = TITLE | SCHED | DAEMON_RUN_TIMEOUT | DAEMON_RUN_ENABLED | SHELL_CMD
    -if TITLE_PATTERN is given, ignore any alarm or cmd that does not match TITLE_PATTERN
    -for each cmd:
      -get FULL_CMD
        -if DAEMON_RUN_TIMEOUT is given and DAEMON_RUN_ENABLED is true:
          -use FULL_CMD = udo daemon-run --name=TITLE --timeout=DAEMON_RUN_TIMEOUT -v SHELL_CMD
        -otherwise:
          -use FULL_CMD = SHELL_CMD
      -add cmd as in:
        `$EXEC --add SCHED --title=TITLE --cmd='FULL_CMD'
    -for each alarm:
      -add alarm as in:
        `$EXEC --add-alarm TITLE_TIME_HH_MM DAYS_OF_WEEK_LETTERS`

  TITLE:
    name of event
    can ONLY contain letters, numbers, underscores, or dashes, for convenience

  COMMAND:
    a shell command
    cannot contain apostrophes/single-quotes ('), for convenience

  ONCE:
    instead of scheduling repeating command,
    add a one-shot to run in TICK_SECONDS from now

    --once=TICK_SECONDS | --tick=TICK_SECOND
      set tick=TICK_SECONDS and dont use schedule

  SCHED:
    restrict which minutes the job will run it
      (default is every minute of any hour of any day)
    effects are cumulative, and the order matters (applied left to right)
      e.g.:
        1h
          => every hour, on the hour
        3h --minutes=15
          => every 3 hours, at a quarter past the hour
        --minutes=15 3h
          => every 3 hours, on the hour
        5m --weekdays
          => every 5 minutes, EXCEPT on Sat or Sun
        --hours=9 --minutes=0 --weekdays='Tue Thu' --months=Nov
          => 09:00am on tuesdays and thursdays in november
        --hours=0 --day-of-month=25 --months=Dec
          => midnight on Newton's birthday

    <MIN>m | <MIN>min | --every-n-min=<MIN> | --every-<MIN>-min
      set minutes = every minute evenly divisible by <MIN>
      MIN = 1 | 2 | 3 | 4 | 5 | 6 | 10 | 12 | 15 | 20 | 30 | 60
      (if this is the only <SCHED>, this means run job every <MIN> minutes)
        e.g.:
          30m                => --minutes='0 30'
          --every-n-min=5    => --minutes='0 5 10 15 20 25 30 35 40 45 50 55'
          --every-15-min     => --minutes='0 15 30 45'

    <HOUR>h | --every-n-hours=<HOUR> | --every-<HOUR>-hours
      set hours = every hour divisible by <HOUR>, AND set minutes = 0
      HOUR = 1 | 2 | 3 | 4 | 6 | 8 | 12 | 24
      (if this is the only <SCHED>, this means run job every <HOUR> hours)
        e.g.:
          4h                 => --hours='0 4 8 12 16 20' --minutes=0
          --every-6-hours    => --hours='0 6 12 18'      --minutes=0
          --every-n-hours=8  => --hours='0 8 16'         --minutes=0

    --time=TIME_HHMM
      clock time formatted as HH:MM
      same as: --hours=HH --minutes=MM

    --every-min
      same as: 1m   (this is the default)

    --hourly
      same as: 60m

    --daily
      same as: 24h

    --weekdays
      same as: --days-of-week=\"Mon Tue Wed Thu Fri\"
    --weekends
      same as: --days-of-week=\"Sat Sun\"

    --days-of-week-leters=DAYS_OF_WEEK_LETTERS
      parse DAYS_OF_WEEK_LETERS into \"<DAY_OF_WEEK> <DAY_OF_WEEK> ...\"
      same as: --days-of-week=\"<DAY_OF_WEEK> <DAY_OF_WEEK> ...\"

    --days-of-week=\"<DAY_OF_WEEK> <DAY_OF_WEEK> ...\"
      set days-of-week
    --months=\"<MONTH> <MONTH> ...\"
      set months
    --days-of-month=\"<DAY_OF_MONTH> <DAY_OF_MONTH> ...\"
      set days-of-month
    --hours=\"<HOUR> <HOUR> ...\"
      set hours
    --minutes=\"<MIN> <MIN> ...\"
      set minutes

  TIME_HHMM
    clock time, 24-hour, formatted 'HH:MM'

  DAY_OF_WEEK_LETTERS
    case-sensitive string of up to 7 characters, one for each day of the week to include
      e.g.:   mtwTfsS = every day
              mtwTf   = weekdays
    m = monday
    t = tuesday
    w = wednesday
    T = thursday
    f = friday
    s = saturday
    S = sunday

  OPTS
    --application=APPLICATION | --app=APPLICATION
      use APPLICATION instead of $APPLICATION_TIMED_CMD

    --no-cmd
      same as: --cmd=''
      do not include a 'whenDue runComand' action

    --event-arg=EXTRA_EVENT_ARG
      (can be given multiple times)
      when adding events, append EXTRA_EVENT_ARG to semicolon-separated list
        in 'timedclient-t5 -e'
";

my $ACTION_GET = "get";
my $ACTION_CLEAR = "clear";
my $ACTION_ADD = "add";
my $ACTION_APPLY_CONFIG = "apply-config";

sub main(@){
  my $action = $ACTION_GET;
  my $title = undef;
  my $shellCmd = undef;
  my $onceTick = undef;
  my $schedArgs = {};
  my $application = $APPLICATION_TIMED_CMD;
  my @extraEventArgs;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print "$USAGE\n";
      exit 0;
    }elsif($arg =~ /^(?:--app|--application)=(.+)$/){
      $application = $1;
    }elsif($arg =~ /^--event-arg=(.+)$/){
      push @extraEventArgs, $1;
    }elsif($arg =~ /^(-g|--get)$/){
      $action = $ACTION_GET;
    }elsif($arg =~ /^--get-alarms$/){
      $action = $ACTION_GET;
      $application = $ALARM_APPLICATION;
    }elsif($arg =~ /^(--clear)$/){
      $action = $ACTION_CLEAR;
    }elsif($arg =~ /^(--clear-alarms)$/){
      $action = $ACTION_CLEAR;
      $application = $ALARM_APPLICATION;
    }elsif($arg =~ /^(-a|--add)$/){
      $action = $ACTION_ADD;
    }elsif($arg =~ /^(--apply-config)$/){
      $action = $ACTION_APPLY_CONFIG;
    }elsif($arg =~ /^(--add-alarm)$/){
      if(@_ != 3){
        die "$USAGE\nERROR: --add-alarm requires 3 arguments\n";
      }
      my ($titleArg, $hhmmArg, $daysOfTheWeekLettersArg) = @_;
      @_ = ();

      my ($h, $m);
      if($hhmmArg =~ /^(\d|\d\d):(\d\d)$/){
        ($h, $m) = ($1, $2);
      }else{
        die "ERROR: malformed HH:MM $hhmmArg\n";
      }
      die "ERROR: malformed HH:MM $hhmmArg\n" if $h >= 24;
      die "ERROR: malformed HH:MM $hhmmArg\n" if $m >= 60;

      $action = $ACTION_ADD;
      $application = $ALARM_APPLICATION;
      $title = $titleArg;
      $shellCmd = '';
      $$schedArgs{$ATT_HOURS} = [0+$h];
      $$schedArgs{$ATT_MINUTES} = [0+$m];
      $$schedArgs{$ATT_DAYS_OF_WEEK} = parseDaysOfTheWeekLetters($daysOfTheWeekLettersArg);
      push @extraEventArgs, "createdDate=" . nowMillis();
      push @extraEventArgs, "daysOfWeek=$daysOfTheWeekLettersArg";
      push @extraEventArgs, "timeOfDayWithSeconds=" . ($h*60*60 + $m*60);
      push @extraEventArgs, "maximalTimeoutSnoozeCounter=2"; #not implemented in timedclient-qt5 as of 3.6.13-1.8.1
      push @extraEventArgs, "type=clock";
      push @extraEventArgs, "alarm";
      push @extraEventArgs, "reminder";
      push @extraEventArgs, "boot";
      push @extraEventArgs, "keepAlive";
      push @extraEventArgs, "alignedSnooze";
    }elsif($arg =~ /^--title=(.+)$/){
      $title = $1;
    }elsif($arg =~ /^--cmd=(.*)$/){
      $shellCmd = $1;
    }elsif($arg =~ /^--no-cmd$/){
      $shellCmd = ""; #empty command omits '-a'
    }elsif($arg =~ /^(?:--once|--tick)=(\d+)$/){
      $onceTick = $1;
    }elsif($arg =~ /^(--days-of-week|--days-of-week-letters|--months|--days-of-month|--hours|--minutes|--time)=(.+)$/){
      my ($field, $valStr) = ($1, $2);
      my $vals = [split /[ ,]+/, $valStr];
      if($field =~ /^(--days-of-week)$/){
        for my $dayOfWeek(@$vals){
          die "ERROR: malformed day-of-week: $dayOfWeek\n"
            if $dayOfWeek !~ /^(Mon|Tue|Wed|Thu|Fri|Sat|Sun)$/;
        }
        $$schedArgs{$ATT_DAYS_OF_WEEK} = $vals;
      }elsif($field =~ /^(--days-of-week-letters)$/){
        $$schedArgs{$ATT_DAYS_OF_WEEK} = parseDaysOfTheWeekLetters($valStr);
      }elsif($field =~ /^(--months)$/){
        for my $month(@$vals){
          die "ERROR: malformed month: $month\n"
            if $month !~ /^(Jan|Feb|Mar|Apr|May|Jun|Jul|Aug|Sep|Oct|Nov|Dec)$/;
        }
        $$schedArgs{$ATT_MONTHS} = $vals;
      }elsif($field =~ /^(--days-of-month)$/){
        for my $dayOfMonth(@$vals){
          die "ERROR: malformed day-of-month: $dayOfMonth\n"
            if $dayOfMonth !~ /^\d+$/ or $dayOfMonth > 31;
        }
        $$schedArgs{$ATT_DAYS_OF_MONTH} = [map {0+$_} @$vals];
      }elsif($field =~ /^(--hours)$/){
        for my $hour(@$vals){
          die "ERROR: malformed hour: $hour\n" if $hour !~ /^\d+$/ or $hour >= 24;
        }
        $$schedArgs{$ATT_HOURS} = [map {0+$_} @$vals];
      }elsif($field =~ /^(--minutes)$/){
        for my $min(@$vals){
          die "ERROR: malformed min: $min\n" if $min !~ /^\d+$/ or $min >=60;
        }
        $$schedArgs{$ATT_MINUTES} = [map {0+$_} @$vals];
      }elsif($field =~ /^(--time)$/){
        for my $time(@$vals){
          if($time =~ /^(\d|\d\d):(\d\d)$/){
            my ($h, $m) = ($1, $2);
            push @{$$schedArgs{$ATT_HOURS}}, 0+$h;
            push @{$$schedArgs{$ATT_MINUTES}}, 0+$m;
          }else{
            die "ERROR: malformed time $time\n";
          }
        }
      }else{
        die "ERROR: malformed sched arg: $arg\n";
      }
    }elsif($arg =~ /^(\d+m|\d+min|--every-n-min=\d+|--every-\d+-min)$/){
      my $factor = 0+$1 if $arg =~ /(\d+)/;
      if(not defined $factor or $factor !~ /^(1|2|3|4|5|6|10|12|15|20|30|60)$/){
        die "ERROR: --every-n-minute can only accept divisors of 60\n";
      }
      $$schedArgs{$ATT_MINUTES} = filterByDivisible $factor, $$SCHED_ALL{$ATT_MINUTES};
    }elsif($arg =~ /^(\d+h|--every-n-hours=\d+|--every-\d+-hours)$/){
      my $factor = 0+$1 if $arg =~ /(\d+)/;
      if(not defined $factor or $factor !~ /^(1|2|3|4|6|8|12)$/){
        die "ERROR: --every-n-hours can only accept divisors of 24\n";
      }
      $$schedArgs{$ATT_HOURS} = filterByDivisible $factor, $$SCHED_ALL{$ATT_HOURS};
      $$schedArgs{$ATT_MINUTES} = [0];
    }elsif($arg =~ /^(--every-min)$/){
      $$schedArgs{$ATT_MINUTES} = filterByDivisible 1, $$SCHED_ALL{$ATT_MINUTES};
    }elsif($arg =~ /^(--hourly)$/){
      $$schedArgs{$ATT_MINUTES} = filterByDivisible 60, $$SCHED_ALL{$ATT_MINUTES};
    }elsif($arg =~ /^(--daily)$/){
      $$schedArgs{$ATT_HOURS} = filterByDivisible 24, $$SCHED_ALL{$ATT_HOURS};
      $$schedArgs{$ATT_MINUTES} = [0];
    }elsif($arg =~ /^(--weekdays)$/){
      $$schedArgs{$ATT_DAYS_OF_WEEK} = [qw(Mon Tue Wed Thu Fri)];
    }elsif($arg =~ /^(--weekends)$/){
      $$schedArgs{$ATT_DAYS_OF_WEEK} = [qw(Sat Sun)];
    }else{
      die "$USAGE\nERROR: unknown arg '$arg'\n";
    }
  }

  if($action eq $ACTION_GET){
    die "$USAGE\nERROR: cannot specify SCHEDULE for action=$action\n" if %$schedArgs > 0;
    die "$USAGE\nERROR: cannot specify TITLE for action=$action\n" if defined $title;
    die "$USAGE\nERROR: cannot specify COMMAND for action=$action\n" if defined $shellCmd;

    my @cookies = getTimedCmdEventCookies($application);
    my %eventsByCookie = map {$_ => getTimedCmdEventInfo $_} @cookies;
    for my $cookie(@cookies){
      my $event = $eventsByCookie{$cookie};
      my $cmd = defined $$event{COMMAND} ? $$event{COMMAND} : "";
      print "$cookie: $$event{timingFmt} $$event{TITLE} '$cmd'\n";
    }
  }elsif($action eq $ACTION_CLEAR){
    die "$USAGE\nERROR: cannot specify SCHEDULE for action=$action\n" if %$schedArgs > 0;
    die "$USAGE\nERROR: cannot specify TITLE for action=$action\n" if defined $title;
    die "$USAGE\nERROR: cannot specify COMMAND for action=$action\n" if defined $shellCmd;

    my @cookies = getTimedCmdEventCookies($application);
    for my $cookie(@cookies){
      run "timedclient-qt5", "--cancel-event", $cookie;
    }
  }elsif($action eq $ACTION_ADD){
    die "$USAGE\nERROR: must specify TITLE for action=$action\n" if not defined $title;
    die "$USAGE\nERROR: must specify COMMAND for action=$action\n" if not defined $shellCmd;

    if(defined $onceTick and %$schedArgs > 0){
      die "ERROR: cannot specify both SCHED and ONCE\n";
    }

    if($title !~ /^[a-zA-Z0-9_\-]+$/){
      die "ERROR: TITLE can only contain letters/numbers/underscores/dashes\n";
    }
    if($shellCmd =~ /'/){
      die "ERROR: COMMAND cannot contain apostrophes (for $EXEC convenience)\n";
    }

    my @actionArgs = length $shellCmd > 0 ? ("whenDue", "runCommand=$shellCmd") : ();
    my @eventArgs = ("APPLICATION=$application", "TITLE=$title", @extraEventArgs);

    my @recurrenceArgs;
    if(defined $onceTick){
      print "adding command with ticker=$onceTick\n";
      push @eventArgs, "ticker=$onceTick";
      @recurrenceArgs = ();
    }else{
      my $sched = {%$SCHED_ALL, %$schedArgs};

      print "adding command with schedule: " . parseSchedule($sched) . "\n";
      @recurrenceArgs = getScheduleRecurrenceArgs($sched);
    }

    my @cmd = ("timedclient-qt5");
    @cmd = (@cmd, "-a", join(";", @actionArgs)) if @actionArgs > 0;
    @cmd = (@cmd, "-r", join(";", @recurrenceArgs)) if @recurrenceArgs > 0;
    @cmd = (@cmd, "-e", join(";", @eventArgs)) if @eventArgs > 0;

    run @cmd;
  }elsif($action eq $ACTION_APPLY_CONFIG){
    my $conf = readConf($CONF_FILE);

    print "\nclearing commands\n";
    run $EXEC, "--clear";
    print "\n\n\n";

    print "\nadding commands\n";
    for my $cmdEntry(@{$$conf{commands}}){
      my $name = $$cmdEntry{name};
      my $sched = $$cmdEntry{sched};
      my $timeout = $$cmdEntry{timeout};
      my $useDaemon = $$cmdEntry{useDaemon};
      my $shellCmd = $$cmdEntry{cmd};

      my @fullCmd;
      if($useDaemon =~ /true/){
        @fullCmd = (
          "udo", "daemon-run",
          "--name=$name", "--timeout=$timeout",
          "--verbose",
          $shellCmd,
        );
      }else{
        @fullCmd = ($shellCmd);
      }

      print "\n";
      run $EXEC, "--add", "$sched", "--title=$name", "--cmd=@fullCmd";
    }

    print "\n";

    print "\nclearing alarms\n";
    system $EXEC, "--clear-alarms";

    print "\nadding alarms\n";
    for my $alarmEntry(@{$$conf{alarms}}){
      my $name = $$alarmEntry{name};
      my $timeHHMM = $$alarmEntry{timeHHMM};
      my $dayOfWeekLetters = $$alarmEntry{dayOfWeekLetters};
      run $EXEC, "--add-alarm", $name, $timeHHMM, $dayOfWeekLetters;
    }

    print "\n";

    print "\n\ncommands:\n";
    run $EXEC, "--get";

    print "\n\nalarms:\n";
    run $EXEC, "--get-alarms";
  }else{
    die "ERROR: unknown action $action\n";
  }
}

sub readConf($){
  my ($file) = @_;
  my $conf = {
    commands => [],
    alarms   => [],
  };
  if(not -e $file){
    return $conf;
  }

  open my $fh, "<", $file or die "ERROR: could not read $file\n$!\n";
  my @lines = <$fh>;
  close $fh;

  for my $line(@lines){
    next if $line =~ /^\s*(#.*)?$/;
    if($line =~ /^
      \s* alarm \s* =
      \s* (\w+) \s* \|
      \s* (\d+:\d+) \s* \|
      \s* ([mtwTfsS]+) \s*
      $
    /xi){
      push @{$$conf{alarms}}, {
        name             => $1,
        timeHHMM         => $2,
        dayOfWeekLetters => $3,
      };
    }elsif($line =~ /
      ^
      \s* cmd \s* =
      \s* ([a-zA-Z0-9_\-]+) \s* \|
      \s* (\w+) \s* \|
      \s* (\w+) \s* \|
      \s* (true|false) \s* \|
      \s* (\S.*\S)
      \s*
      $
    /xi){
      push @{$$conf{commands}}, {
        name      => $1,
        sched     => $2,
        timeout   => $3,
        useDaemon => $4,
        cmd       => $5,
      };
    }else{
      die "ERROR: malformed entry in $CONF_FILE\n$line";
    }
  }

  return $conf;
}

sub getTimedCmdEventCookies($){
  my ($application) = @_;
  my $out = readProc "timedclient-qt5", "--search", "APPLICATION=$application";
  return $out =~ /\d+/g;
}

sub getTimedCmdEventInfo($){
  my ($eventCookie) = @_;
  my @lines = readProcLines "timedclient-qt5", "--get-event=$eventCookie";
  my $okAtts = join "|", qw(APPLICATION TITLE COMMAND USER);
  my $okSchedAtts = join "|", (
    $ATT_DAYS_OF_WEEK, $ATT_MONTHS, $ATT_DAYS_OF_MONTH, $ATT_HOURS, $ATT_MINUTES,
  );

  my $event = {
    schedule => undef,
    scheduleFmt => undef,
    tick => undef,
    tickFmt => undef,
    timingFmt => undef,
  };
  for my $line(@lines){
    if($line =~ /^\s*($okAtts)\s*=\s*'(.+)'$/){
      $$event{$1} = $2;
    }elsif($line =~ /^\s*($okSchedAtts)\s*:\s*(.+)$/){
      my ($att, $val) = ($1, $2);
      my @vals = split /\s+/, $val;
      $$event{schedule} = {} if not defined $$event{schedule};
      $$event{schedule}{$att} = [@vals];
    }elsif($line =~ /^\s*Tick: (\d+) \((.*)\)$/){
      $$event{tick} = $1;
      $$event{tickFmt} = $2;
    }
  }
  if(defined $$event{schedule}){
    $$event{scheduleFmt} = parseSchedule($$event{schedule});
  }

  if(defined $$event{schedule}){
    $$event{timingFmt} = $$event{scheduleFmt};
  }elsif(defined $$event{tick}){
    $$event{timingFmt} = "$$event{tick} ($$event{tickFmt})";
  }

  return $event;
}

sub getScheduleRecurrenceArgs($){
  my ($sched) = @_;
  my @args;

  for my $hour(@{$$sched{$ATT_HOURS}}){
    push @args, "hour=$hour";
  }

  for my $minute(@{$$sched{$ATT_MINUTES}}){
    push @args, "minute=$minute";
  }

  if(arrSameElems($$sched{$ATT_DAYS_OF_MONTH}, $$SCHED_ALL{$ATT_DAYS_OF_MONTH})){
    push @args, "everyDayOfMonth";
  }else{
    for my $dayOfMonth(@{$$sched{$ATT_DAYS_OF_MONTH}}){
      push @args, "dayOfMonth=$dayOfMonth";
    }
  }

  if(arrSameElems($$sched{$ATT_DAYS_OF_WEEK}, $$SCHED_ALL{$ATT_DAYS_OF_WEEK})){
    push @args, "everyDayOfWeek";
  }else{
    for my $dayOfWeek(@{$$sched{$ATT_DAYS_OF_WEEK}}){
      push @args, "dayOfWeek=$dayOfWeek";
    }
  }

  if(arrSameElems($$sched{$ATT_MONTHS}, $$SCHED_ALL{$ATT_MONTHS})){
    push @args, "everyMonth";
  }else{
    for my $month(@{$$sched{$ATT_MONTHS}}){
      my %monthIds = (
        Jan => 2,
        Feb => 3,
        Mar => 4,
        Apr => 5,
        May => 6,
        Jun => 7,
        Jul => 8,
        Aug => 9,
        Sep => 10,
        Oct => 11,
        Nov => 12,
        Dec => undef,
      );
      my $monthId = $monthIds{$month};
      if(not defined $monthId){
        die "ERROR: some ridiculous timed off-by-one bug thinks Jan=2 and Dec=13\n"
          . "this completely prevents adding December, except with everyMonth...\n"
          ;
      }
      push @args, "month=$monthId";
    }
  }
  return @args;
}

sub parseSchedule($){
  my ($schedule) = @_;
  my $hr = $$schedule{$ATT_HOURS};
  my $min = $$schedule{$ATT_MINUTES};
  my $isAllHr = arrSameElems($hr, $$SCHED_ALL{$ATT_HOURS});
  my $isAllMin = arrSameElems($min, $$SCHED_ALL{$ATT_MINUTES});

  my @schedArgs;
  if(@schedArgs == 0 and @$min == 1){
    for my $hrFactor(qw(1 2 3 4 6 8 12 24)){
      my $factorHrs = filterByDivisible $hrFactor, $$SCHED_ALL{$ATT_HOURS};
      if(arrSameElems($hr, $factorHrs)){
        push @schedArgs, "${hrFactor}h";
        push @schedArgs, "--minutes=$$min[0]" if $$min[0] != 0;
        last;
      }
    }
  }

  if(@schedArgs == 0 and $isAllHr){
    for my $minFactor(qw(1 2 3 4 5 6 10 12 15 20 30)){
      my $factorMins = filterByDivisible $minFactor, $$SCHED_ALL{$ATT_MINUTES};
      if(arrSameElems($min, $factorMins)){
        push @schedArgs, "${minFactor}m";
        last;
      }
    }
  }

  if(@schedArgs == 0){
    push @schedArgs, "--hours='@$hr'" if not $isAllHr;
    push @schedArgs, "--minutes='@$min'" if not $isAllMin;
  }

  if(not arrSameElems($$schedule{$ATT_DAYS_OF_WEEK}, $$SCHED_ALL{$ATT_DAYS_OF_WEEK})){
    push @schedArgs, "--days-of-week='@{$$schedule{$ATT_DAYS_OF_WEEK}}'";
  }
  if(not arrSameElems($$schedule{$ATT_MONTHS}, $$SCHED_ALL{$ATT_MONTHS})){
    push @schedArgs, "--months='{$$schedule{$ATT_MONTHS}}'";
  }
  if(not arrSameElems($$schedule{$ATT_DAYS_OF_MONTH}, $$SCHED_ALL{$ATT_DAYS_OF_MONTH})){
    push @schedArgs, "--days-of-month='{$$schedule{$ATT_DAYS_OF_MONTH}}'";
  }

  return "@schedArgs";
}

sub parseDaysOfTheWeekLetters($){
  my ($daysOfTheWeekLetters) = @_;
  my @letters = split //, $daysOfTheWeekLetters;
  my $daysOfTheWeek = [];

  for my $letter(@letters){
    if($letter eq "m"){
      push @$daysOfTheWeek, "Mon";
    }elsif($letter eq "t"){
      push @$daysOfTheWeek, "Tue";
    }elsif($letter eq "w"){
      push @$daysOfTheWeek, "Wed";
    }elsif($letter eq "T"){
      push @$daysOfTheWeek, "Thu";
    }elsif($letter eq "f"){
      push @$daysOfTheWeek, "Fri";
    }elsif($letter eq "s"){
      push @$daysOfTheWeek, "Sat";
    }elsif($letter eq "S"){
      push @$daysOfTheWeek, "Sun";
    }else{
      die "ERROR: malformed day-of-week letter $letter\n";
    }
  }

  return $daysOfTheWeek;
}

sub filterByDivisible($$){
  my ($factor, $arr) = @_;
  return [grep {$_ % $factor == 0} @$arr];
}

sub arrSameElems($$){
  my ($arrRef1, $arrRef2) = @_;
  return 0 if @$arrRef1 != @$arrRef2;

  my @arr1 = sort @$arrRef1;
  my @arr2 = sort @$arrRef2;
  for(my $i=0; $i<@arr1; $i++){
    if($arr1[$i] ne $arr2[$i]){
      return 0;
    }
  }
  return 1;
}

sub run(@){
  print "@_\n";
  system @_;
  die "ERROR: \"@_\" failed\n" if $? != 0;
}
sub readProc(@){
  return join '', readProcLines @_;
}
sub readProcLines(@){
  open CMD, "-|", @_ or die "ERROR: could not run \"@_\"\n$!\n";
  my @lines = <CMD>;
  close CMD;
  die "ERROR: \"@_\" failed\n" if $? != 0;
  return @lines;
}

sub nowMillis(){
  return int(time * 1000.0 + 0.5);
}

&main(@ARGV);
