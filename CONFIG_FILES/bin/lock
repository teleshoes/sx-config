#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub isLocked();
sub setLocked($);
sub run(@);

my $CMD_GET = "get";
my $CMD_TOGGLE = "toggle";
my $CMD_LOCK = "lock";
my $CMD_UNLOCK = "unlock";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC --get
    -read locked status with dbus method: com.nokia.mce.request.get_display_status
    -print either 'locked' or 'unlocked'

  $EXEC
  $EXEC --toggle
    -toggle locked/unlocked
    -same as: if [ `$EXEC --get` == locked ]; then $EXEC --unlock; else $EXEC --lock; fi

  $EXEC --lock
    -use mcetool to set lock screen and blank display

  $EXEC --unlock
    -use mcetool to set unlock screen and unblank display
";

sub main(@){
  my $cmd = $CMD_TOGGLE;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--get)$/){
      $cmd = $CMD_GET;
    }elsif($arg =~ /^(--toggle)$/){
      $cmd = $CMD_TOGGLE;
    }elsif($arg =~ /^(--lock)$/){
      $cmd = $CMD_LOCK;
    }elsif($arg =~ /^(--unlock)$/){
      $cmd = $CMD_UNLOCK;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($cmd eq $CMD_GET){
    print isLocked() ? "locked\n" : "unlocked\n";
  }elsif($cmd eq $CMD_TOGGLE){
    setLocked(isLocked() ? 0 : 1);
  }elsif($cmd eq $CMD_LOCK){
    setLocked 1;
  }elsif($cmd eq $CMD_UNLOCK){
    setLocked 0;
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

sub isLocked(){
  my @cmd = qw(
    dbus-send
      --system
      --dest=com.nokia.mce
      --print-reply
      --type=method_call
      /com/nokia/mce/request
      com.nokia.mce.request.get_display_status
  );
  open CMD, "-|", @cmd or die "could not run @cmd\n$!\n";
  my $out = join '', <CMD>;
  close CMD;

  my $status = "";
  if($out =~ /^method.*\n\s*string\s*['"](on|off)['"]\s*$/){
    $status = $1;
  }

  if($status eq "on"){
    return 0;
  }elsif($status eq "off"){
    return 1;
  }else{
    die "could not read display status with @cmd\n";
  }
}

sub setLocked($){
  if($_[0]){
    run "mcetool", "--set-tklock-mode=locked", "--blank-screen";
  }else{
    run "mcetool", "--tklock-close", "--unblank-screen";
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
