#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub isLocked();
sub setLocked($);
sub dimScreen();
sub unblankAndBlankLPM();
sub isLPMEnabled();
sub enableLPM();
sub disableLPM();
sub run(@);

my $CMD_GET = "get";
my $CMD_TOGGLE = "toggle";
my $CMD_LPM_REFRESH = "lpm-refresh";
my $CMD_LPM_LOCK = "lpm-lock";
my $CMD_LOCK = "lock";
my $CMD_UNLOCK = "unlock";
my $CMD_DIM = "dim";
my $CMD_IS_LPM_ENABLED = "is-lpm-enabled";
my $CMD_LPM_ENABLE = "lpm-enable";
my $CMD_LPM_DISABLE = "lpm-disable";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC -g | g | get | --get
    -read locked status with dbus method: com.nokia.mce.request.get_display_status
    -print either 'locked' or 'unlocked'

  $EXEC
  $EXEC -t | t | toggle | --toggle
    -toggle locked/unlocked
    -same as: if [ `$EXEC --get` == locked ]; then $EXEC --unlock; else $EXEC --lock; fi

  $EXEC -r | r | --lpm-refresh
    -refresh LPM display if device is locked (do nothing if not locked)
    -same as: if [ `$EXEC --get` == locked ]; then $EXEC --lpm-lock; fi

  $EXEC --lpm-lock
    -enter LPM mode, refreshing the LPM display if already locked
      (unblank+dim the screen, then lock + blank the screen, entering LPM if enabled)
    -same as: $EXEC --dim && $EXEC --lock

  $EXEC -l | l | lock | --lock
    -use mcetool to set lock screen and blank display
      (note: enters LPM if enabled, but does not refresh LPM display)

  $EXEC -u | u | unlock | --unlock
    -use mcetool to set unlock screen and unblank display

  $EXEC -d | d | dim | --dim
    -use mcetool to unblank display and dim the screen

  $EXEC --is-lpm-enabled
    -extract 'Use low power mode' from mcetool
    -if 'enabled':  print enabled
    -if 'disabled': print disabled

  $EXEC --lpm-enable | --enable-lpm
    -use mcetool to enable low power mode and associated settings
    -also does --lpm-refresh to switch from lockscreen to LPM
  $EXEC --lpm-disable | --disable-lpm
    -use mcetool to disable low power mode and associated settings
";

sub main(@){
  my $cmd = $CMD_TOGGLE;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-g|g|get|--get)$/){
      $cmd = $CMD_GET;
    }elsif($arg =~ /^(-t|t|toggle|--toggle)$/){
      $cmd = $CMD_TOGGLE;
    }elsif($arg =~ /^(-r|r|--lpm-refresh)$/){
      $cmd = $CMD_LPM_REFRESH;
    }elsif($arg =~ /^(--lpm-lock)$/){
      $cmd = $CMD_LPM_LOCK;
    }elsif($arg =~ /^(-l|l|lock|--lock)$/){
      $cmd = $CMD_LOCK;
    }elsif($arg =~ /^(-u|u|unlock|--unlock)$/){
      $cmd = $CMD_UNLOCK;
    }elsif($arg =~ /^(-d|d|dim|--dim)$/){
      $cmd = $CMD_DIM;
    }elsif($arg =~ /^(--is-lpm-enabled)$/){
      $cmd = $CMD_IS_LPM_ENABLED;
    }elsif($arg =~ /^(--lpm-enable|--enable-lpm)$/){
      $cmd = $CMD_LPM_ENABLE;
    }elsif($arg =~ /^(--lpm-disable|--disable-lpm)$/){
      $cmd = $CMD_LPM_DISABLE;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($cmd eq $CMD_GET){
    print isLocked() ? "locked\n" : "unlocked\n";
  }elsif($cmd eq $CMD_TOGGLE){
    setLocked(isLocked() ? 0 : 1);
  }elsif($cmd eq $CMD_LPM_REFRESH){
    if(isLocked()){
      unblankAndBlankLPM();
    }
  }elsif($cmd eq $CMD_LPM_LOCK){
    unblankAndBlankLPM();
  }elsif($cmd eq $CMD_LOCK){
    setLocked 1;
  }elsif($cmd eq $CMD_UNLOCK){
    setLocked 0;
  }elsif($cmd eq $CMD_DIM){
    dimScreen();
  }elsif($cmd eq $CMD_IS_LPM_ENABLED){
    print isLPMEnabled() ? "enabled\n" : "disabled\n";
  }elsif($cmd eq $CMD_LPM_ENABLE){
    enableLPM();
  }elsif($cmd eq $CMD_LPM_DISABLE){
    disableLPM();
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

sub isLocked(){
  my @cmd = qw(
    dbus-send
      --system
      --dest=com.nokia.mce
      --print-reply
      --type=method_call
      /com/nokia/mce/request
      com.nokia.mce.request.get_display_status
  );
  open CMD, "-|", @cmd or die "could not run @cmd\n$!\n";
  my $out = join '', <CMD>;
  close CMD;

  my $status = "";
  if($out =~ /^method.*\n\s*string\s*['"](on|off)['"]\s*$/){
    $status = $1;
  }

  if($status eq "on"){
    return 0;
  }elsif($status eq "off"){
    return 1;
  }else{
    die "could not read display status with @cmd\n";
  }
}

sub setLocked($){
  if($_[0]){
    run "mcetool", "--set-tklock-mode=locked", "--blank-screen-lpm";
  }else{
    run "mcetool", "--tklock-close", "--unblank-screen";
  }
}

sub dimScreen(){
  run "mcetool", "--dim-screen";
}

sub unblankAndBlankLPM(){
  run "mcetool", "--dim-screen", "--blank-screen-lpm";
}

sub isLPMEnabled(){
  my $out = `mcetool`;
  my $state = "";
  $state = $1 if $out =~ /^Use low power mode:\s*(enabled|disabled)/m;
  if($state eq "enabled"){
    return 1;
  }elsif($state eq "disabled"){
    return 0;
  }else{
    die "ERROR: could not read LPM mode with mcetool\n";
  }
}

sub enableLPM(){
  run "mcetool", "--set-low-power-mode=enabled";
  run "mcetool", "--set-blank-from-lpm-on-timeout=130";
  run "mcetool", "--set-powerkey-blanking=lpm";

  run "mcetool", "--set-doubletap-mode=disabled";

  if(isLocked()){
    unblankAndBlankLPM();
  }
}

sub disableLPM(){
  run "mcetool", "--set-low-power-mode=disabled";
  run "mcetool", "--set-blank-from-lpm-on-timeout=0";
  run "mcetool", "--set-powerkey-blanking=off";

  run "mcetool", "--set-doubletap-mode=disabled";
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
