#!/usr/bin/perl
use strict;
use warnings;
use Time::HiRes qw(sleep);

my $SOUND_DELAY_MILLIS = 350;

my $usage = "Usage:
  $0 ACC_NAME
    play alert sounds for emails
";

sub filterMatches($$@);
sub searchMatches($$@);
sub isEmailSearchMatch(@);

sub main(@){
  my ($accName) = @_;
  die $usage if not defined $accName;

  my $profile = `profile`;
  if($profile =~ /^silent$/){
    print "PROFILE IS SILENT, QUITTING\n";
    exit 0;
  }elsif($profile =~ /^general$/){
    print "profile is general, continuing\n";
  }else{
    die "ERROR: profile is not 'general' or 'silent': '$profile'\n"
  }

  my @soundCommands;

  if($accName =~ "G"){
    if(searchMatches "G", 'subject~"new voicemail" from~"google voice"'){
      push @soundCommands, ["alert", "jumpdown"];
      push @soundCommands, ["alarm", "-s", "--volume", "100", "ff7-phoenixdown"];
    }else{
      push @soundCommands, ["alert", "jumpdown"];
    }
  }elsif($accName =~ "L"){
    if(filterMatches "L", "me"){
      push @soundCommands, ["alert", "jumpup"];
      push @soundCommands, ["alarm", "-s", "--volume", "100", "ff7-menuselect"];
      push @soundCommands, ["alarm", "-s", "--volume", "100", "ff7-menuselect"];
    }elsif(filterMatches "L", "crap", "--negate"){
      push @soundCommands, ["alert", "jumpup"];
    }else{
      #do nothing
    }
  }

  my $klompCmdIsPlayingOut = `klomp-cmd is-playing`;
  my $wasKlompPlaying = $klompCmdIsPlayingOut =~ /playing/ ? 1 : 0;

  if(@soundCommands > 0 and $wasKlompPlaying){
    system "klomp-cmd", "pause";
  }

  my $first = 1;
  for my $cmd(@soundCommands){
    sleep ($SOUND_DELAY_MILLIS / 1000.0) if not $first;
    system @$cmd;
    $first = 0;
  }

  if(@soundCommands > 0 and $wasKlompPlaying){
    sleep 2;
    system "klomp-cmd", "pause";
  }
}

sub filterMatches($$@){
  my ($accName, $filterName, @args) = @_;
  return isEmailSearchMatch(
    "email-search.pl",
    "--filter", "--match", "--new-unread", @args,
    $accName, $filterName,
    );
}
sub searchMatches($$@){
  my ($accName, $query, @args) = @_;
  return isEmailSearchMatch(
    "email-search.pl",
    "--search", "--match", "--new-unread", @args,
    $accName, $query,
    );
}

sub isEmailSearchMatch(@){
  my @cmd = @_;
  open CMD, "-|", @cmd or die "could not run @_\n$!\n";
  my $out = join '', <CMD>;
  close CMD;
  if($out =~ /^yes$/){
    return 1;
  }elsif($out =~ /^no$/){
    return 0;
  }else{
    die "malformed output of @_:\n$out\n";
  }
}

&main(@ARGV);
