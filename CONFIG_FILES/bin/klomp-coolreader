#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $EXEC = basename $0;
my $TTS_PROC_REGEX = "(com.google.android.tts|com.ivona.tts)";
my @KLOMP_TOGGLE_CMD = qw(klomp-cmd pause);
my @COOLREADER_TOGGLE_CMD = qw(coolreader-ctl ttsPlayPause);

my $USAGE = "Usage:
  $EXEC -g|--get
    -if `$EXEC --is-tts-running` = 'YES'
       -print \"coolreader\"
    -otherwise:
       -print \"klomp\"

  $EXEC -p|--play-pause|--play|--pause
    -if `$EXEC --get` = \"coolreader\":
      -if `profile` == general:
        run: @COOLREADER_TOGGLE_CMD
      -otherwise:
        run: sudo pkill -9 -f $TTS_PROC_REGEX
    -otherwise:
      -run: @KLOMP_TOGGLE_CMD

  $EXEC --is-tts-running
    -if `pgrep -f '^$TTS_PROC_REGEX\\s*\$'` is successful:
      print \"yes\"
    -otherwise:
      print \"no\"
";

my $CMD_GET = "get";
my $CMD_PLAY_PAUSE = "play-pause";
my $CMD_IS_TTS_RUNNING = "is-tts-running";

sub isCoolreader();
sub isTTSRunning();
sub isProfileGeneral();

sub main(@){
  my $cmd = undef;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-g|--get)$/){
      $cmd = $CMD_GET;
    }elsif($arg =~ /^(-p|--play-pause|--play|--pause)$/){
      $cmd = $CMD_PLAY_PAUSE;
    }elsif($arg =~ /^(--is-tts-running)$/){
      $cmd = $CMD_IS_TTS_RUNNING;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  die "$USAGE\nERROR: missing command\n" if not defined $cmd;

  if($cmd eq $CMD_GET){
    if(isCoolreader()){
      print "coolreader\n";
    }else{
      print "klomp\n";
    }
  }elsif($cmd eq $CMD_PLAY_PAUSE){
    if(isCoolreader()){
      if(isProfileGeneral()){
        system @COOLREADER_TOGGLE_CMD;
      }else{
        system "sudo", "pkill", "-9", "-f", $TTS_PROC_REGEX;
      }
    }else{
      system @KLOMP_TOGGLE_CMD;
    }
  }elsif($cmd eq $CMD_IS_TTS_RUNNING){
    if(isTTSRunning()){
      print "yes\n";
    }else{
      print "no\n";
    }
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

sub isCoolreader(){
  my $isTTSRunning = isTTSRunning;
  return 0 if not isTTSRunning();

  return 1;
}

sub isTTSRunning(){
  system "pgrep -f '^$TTS_PROC_REGEX\\s*\$' >/dev/null 2>/dev/null";
  return $? == 0 ? 1 : 0;
}

sub isProfileGeneral(){
  my $profile = `profile`;
  chomp $profile;
  return $profile =~ /^general$/ ? 1 : 0;
}

&main(@ARGV);
