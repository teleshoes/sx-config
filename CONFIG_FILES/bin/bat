#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub readCapacity();
sub fetchDaemonEnabled($);
sub isAnyDaemonEnabled();
sub isPSM();
sub setPSM($);

my $CAPACITY_DEV = "/sys/class/power_supply/battery/capacity";

my $EXEC = basename $0;

my %DAEMONS = (
);

my $ACTION_GET = "get";
my $ACTION_ON = "on";
my $ACTION_OFF = "off";
my $ACTION_TOGGLE = "toggle";

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC
  $EXEC -g | --get
    show the current battery capacity from $CAPACITY_DEV

  $EXEC -d get | --daemons get | daemons get
    print status of each battery-consuming daemon,
    formatted '<DAEMON_NAME> = on|off'

  $EXEC -d on | --daemons on | daemons on
    enable all battery-consuming daemons

  $EXEC -d off | --daemons off | daemons off
    disable all battery-consuming daemons

  $EXEC -d | --daemons | daemons
  $EXEC -d toggle | --daemons toggle | daemons toggle
    get status of battery-consuming daemons
      if all are enabled:  same as: $EXEC -d off
      if all are disabled: same as: $EXEC -d on
      otherwise:           same as: $EXEC -d off

  $EXEC --psm get | psm get
    get forced psm status with `mcetool`
    print 'enabled' or 'disabled'

  $EXEC --psm on | psm on
    enable forced psm with `mcetool -F`

  $EXEC --psm off | psm off
    disable forced psm with `mcetool -F`

  $EXEC --psm | psm
  $EXEC --psm toggle | psm toggle
    if forced psm is enabled:
      same as: $EXEC --psm off
    otherwise:
      same as: $EXEC --psm on
";

my $CMD_GET = "get";
my $CMD_DAEMONS_GET = "daemons-get";
my $CMD_DAEMONS_SET = "daemons-set";
my $CMD_PSM_GET = "psm-get";
my $CMD_PSM_SET = "psm-set";

sub main(@){
  my $cmd = $CMD_GET;
  my $actionArg = undef;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-g|--get)$/){
      $cmd = $CMD_GET;
    }elsif($arg =~ /^(-d|--daemons|daemons)$/){
      if(@_ > 0 and $_[0] =~ /^($ACTION_GET|$ACTION_ON|$ACTION_OFF|$ACTION_TOGGLE)$/){
        $actionArg = shift @_;
      }else{
        $actionArg = $ACTION_TOGGLE;
      }

      if($actionArg eq $ACTION_GET){
        $cmd = $CMD_DAEMONS_GET;
      }else{
        $cmd = $CMD_DAEMONS_SET;
      }
    }elsif($arg =~ /^(--psm|psm)$/){
      if(@_ > 0 and $_[0] =~ /^($ACTION_GET|$ACTION_ON|$ACTION_OFF|$ACTION_TOGGLE)$/){
        $actionArg = shift @_;
      }else{
        $actionArg = $ACTION_TOGGLE;
      }

      if($actionArg eq $ACTION_GET){
        $cmd = $CMD_PSM_GET;
      }else{
        $cmd = $CMD_PSM_SET;
      }
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($cmd eq $CMD_GET){
    print readCapacity() . "\n";
  }elsif($cmd eq $CMD_DAEMONS_GET){
    for my $daemonName(sort keys %DAEMONS){
      my $isEnabled = fetchDaemonEnabled($daemonName);
      print "$daemonName = " . ($isEnabled ? "on" : "off") . "\n";
    }
  }elsif($cmd eq $CMD_DAEMONS_SET){
    $actionArg = isAnyDaemonEnabled() ? $ACTION_OFF : $ACTION_ON if $actionArg eq $ACTION_TOGGLE;
    my @cmds;
    for my $daemonName(sort keys %DAEMONS){
      if($actionArg eq $ACTION_ON){
        push @cmds, $DAEMONS{$daemonName}{on};
      }else{
        push @cmds, $DAEMONS{$daemonName}{off};
      }
    }
    for my $cmd(@cmds){
      print "$cmd\n";
      system $cmd;
    }
    print "\n\n";
    for my $daemonName(sort keys %DAEMONS){
      my $isEnabled = fetchDaemonEnabled($daemonName);
      print "$daemonName = " . ($isEnabled ? "on" : "off") . "\n";
    }
  }elsif($cmd eq $CMD_PSM_GET){
    print isPSM() ? "enabled\n" : "disabled\n";
  }elsif($cmd eq $CMD_PSM_SET){
    $actionArg = isPSM() ? $ACTION_OFF : $ACTION_ON if $actionArg eq $ACTION_TOGGLE;
    my $val = $actionArg eq $ACTION_ON ? 1 : 0;
    setPSM($val);
  }else{
    die "ERROR: unknown cmd $cmd\n";
  }
}

sub readCapacity(){
  my $out = `cat $CAPACITY_DEV 2>/dev/null`;
  chomp $out;
  if($out =~ /^(\d+)$/){
    return $1;
  }else{
    die "ERROR: could not read $CAPACITY_DEV\n";
  }
}

sub fetchDaemonEnabled($){
  my ($daemonName) = @_;
  my $getCmd = $DAEMONS{$daemonName}{get};
  my $val = `$getCmd`;
  if($val =~ /^(on|enabled)$/i){
    return 1;
  }elsif($val =~ /^(off|disabled)$/i){
    return 0;
  }else{
    die "ERROR: could not parse `$getCmd`\n";
  }
}

sub isAnyDaemonEnabled(){
  for my $daemonName(sort keys %DAEMONS){
    if(fetchDaemonEnabled($daemonName)){
      return 1;
    }
  }
  return 0;
}

sub isPSM(){
  my $out = `mcetool`;
  my $status = "";
  $status = $1 if $out =~ /^Forced power saving mode:\s*(enabled|disabled)$/m;
  if($status eq "enabled"){
    return 1;
  }elsif($status eq "disabled"){
    return 0;
  }else{
    die "ERROR: could not read PSM status\n";
  }
}

sub setPSM($){
  my ($isPSM) = @_;
  system "mcetool", "-F", ($isPSM ? "enabled" : "disabled");
}

&main(@ARGV);
