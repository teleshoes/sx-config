#!/usr/bin/perl
use strict;
use warnings;
use Time::HiRes qw(time);

my $user = "nemo";
my $ipmagicName = "sx";

my $skipMissing = "skip-missing";

my $localDir = "$ENV{HOME}/Code/sx/backup/DCIM";
my @backups = (
  [ $localDir, "/media/sdcard/phone/Pictures/Camera",      "sd-phone_sf"],
  [ $localDir, "/media/sdcard/phone/Videos/Camera",        "sd-phone_sf_vid"],
  [ $localDir, "/media/sdcard/phone/Pictures/AdvancedCam", "sd-phone-adv_sf"],
  [ $localDir, "/media/sdcard/phone/Videos/AdvancedCam",   "sd-phone-adv_sf_vid"],

  [ $localDir, "/home/nemo/android_storage/DCIM/OpenCamera", "int_oc"],

  [ $localDir, "/home/nemo/Pictures/Camera",               "int_sf"],
  [ $localDir, "/home/nemo/Videos/Camera",                 "int_sf_vid"],
  [ $localDir, "/home/nemo/Videos/Camera/.recording",      "int_sf_vid_RECORDING"],
  [ $localDir, "/home/nemo/Pictures/AdvancedCam",          "int-adv_sf"],
  [ $localDir, "/home/nemo/Videos/AdvancedCam",            "int-adv_sf_vid"],
  [ $localDir, "/home/nemo/Pictures/",                     "int-pix_sf", $skipMissing],
  [ $localDir, "/home/nemo/Pictures/Screenshots",          "int_scrn"],
);
my $remoteTmpDir = "/media/sdcard/phone/dcim-tmp";
my $localTmpDir = "$ENV{HOME}/dcim-tmp";

my @filetypes = qw(jpg jpeg png gif mp4);

sub dcimBackup($$$);
sub reorganizeBackedUpRemotes($$$$$);
sub md5sumMaps(\@);
sub runMd5sum($);
sub wrapQuotes($);
sub findDCIMFiles($);
sub updateMd5sums($);
sub updateLocalDCIMInfo($);
sub readLocalDCIMInfoCache($);
sub writeLocalDCIMInfoCache($$);
sub getRemoteDCIMInfo($);
sub getFileKey($);
sub remoteDirExists($;$);
sub readProc($@);
sub readProcLines($@);
sub run($@);

sub main(@){
  for my $backup(@backups){
    my ($localDCIM, $remoteDCIM, $backupSuffix, $extraArg) = @$backup;
    dcimBackup($localDCIM, $remoteDCIM, $backupSuffix);
  }
  for my $backup(@backups){
    my ($localDCIM, $remoteDCIM, $backupSuffix, $extraArg) = @$backup;
    if(defined $extraArg and $extraArg =~ /$skipMissing/){
      print "SKIP check-missing for $remoteDCIM\n";
    }else{
      checkMissing($localDCIM, $remoteDCIM);
    }
  }
}

sub dcimBackup($$$){
  my ($localDCIM, $remoteDCIM, $backupSuffix) = @_;

  die "local $localDCIM does not exist\n" if not -d $localDCIM;
  die "remote $remoteDCIM does not exist\n" if not remoteDirExists $remoteDCIM;
  chdir $localDCIM;
  print "\n\n===============\n";
  print "Fetching remote md5sums: $remoteDCIM\n";
  my @remoteMd5sums = `ipmagic $ipmagicName -u $user -s 'find $remoteDCIM/ -maxdepth 1 -type f -exec md5sum {} \\;'`;

  print "===all:\n" . join "", @remoteMd5sums;
  print "===\n\n";

  my $localByMd5sum = updateMd5sums($localDCIM);

  my ($remoteByMd5sum, $remoteByFile) = md5sumMaps @remoteMd5sums;

  my (@backedUp, @needsBackUp);
  for my $md5sum(keys %$remoteByMd5sum){
    my $file = $$remoteByMd5sum{$md5sum};
    if(defined $$localByMd5sum{$md5sum}){
      push @backedUp, $file;
    }else{
      push @needsBackUp, $file;
    }
  }

  reorganizeBackedUpRemotes($remoteDCIM, \@backedUp,
    $localByMd5sum, $remoteByMd5sum, $remoteByFile);

  print "\n\n===NEEDS BACKUP:\n" . join "\n", sort @needsBackUp;
  print "===\n\n";

  if(@needsBackUp > 0){
    my $now = `date +%Y-%m-%d_%s`;
    chomp $now;
    my $dir = "$localDCIM/backup_${now}_${backupSuffix}";
    system "mkdir -p $dir";

    system "ipmagic $ipmagicName -u $user --rsync -avP --exclude=backup_*/ :$remoteDCIM/ $dir/";
    die "failed rsync backup\n" if $? != 0;

    if(-e "$dir/.recording"){
      system "rmdir $dir/.recording";
    }

    print "updating local md5sums again and re-reorganizing remote\n";
    $localByMd5sum = updateMd5sums($localDCIM);

    reorganizeBackedUpRemotes($remoteDCIM, \@needsBackUp,
      $localByMd5sum, $remoteByMd5sum, $remoteByFile);
  }
}

sub reorganizeBackedUpRemotes($$$$$){
  my $remoteDCIM = shift;
  my @backedUpFiles = @{shift()};
  my $localByMd5sum = shift;
  my $remoteByMd5sum = shift;
  my $remoteByFile = shift;

  my @reorganizeCmds;
  for my $file(sort @backedUpFiles){
    my $md5sum = $$remoteByFile{$file};
    my $localFile = $$localByMd5sum{$md5sum};
    if(not defined $localFile){
      die "\n\nSERIOUS ERROR: local backup doesnt exist: $file!\n";
    }
    my $newRemoteFile = "$remoteDCIM/$localFile";
    my $dir = $newRemoteFile;
    $dir =~ s/\/[^\/]*$//;
    $dir = wrapQuotes $dir;
    $file = wrapQuotes $file;
    $newRemoteFile = wrapQuotes $newRemoteFile;
    push @reorganizeCmds, "mkdir -p $dir; mv $file $newRemoteFile;\n";
  }
  my $total = @reorganizeCmds;
  print "\n\nReorganizing $total remote files to match local\n";
  if(@reorganizeCmds > 0){

    my @cmdChunks;
    push @cmdChunks, [ splice @reorganizeCmds, 0, 100 ] while @reorganizeCmds;
    for my $cmdChunk(@cmdChunks){
      my $cmd = join '', @$cmdChunk;
      my $count = @$cmdChunk;
      print "\n\n#running $count mv commands\n$cmd";
      system "ipmagic", $ipmagicName, "-u", $user, "-s", $cmd;
      die "failed reorganizing remotes\n" if $? != 0;
    }
  }
}

sub md5sumMaps(\@){
  my %byMd5sum;
  my %byFile;
  my @md5sumLines = @{shift()};
  for my $line(@md5sumLines){
    if($line =~ /^([0-9a-f]{32})  (.+)\n$/){
      $byFile{$2} = $1;
      $byMd5sum{$1} = $2;
    }else{
      die "ERROR: malformed md5sum line $line\n";
    }
  }
  return (\%byMd5sum, \%byFile);
}

sub runMd5sum($){
  my $file = shift;
  $file = wrapQuotes $file;
  my $md5sum = `md5sum $file`;
  die "failed generating md5sum for $file\n" if $? != 0;
  return $md5sum;
}

sub wrapQuotes($){
  my $s = shift;
  $s =~ s/'/'\\''/g;
  $s = "'$s'";
  return $s;
}

sub findDCIMFiles($){
  my ($localDCIM) = @_;
  my @files = `cd $localDCIM && find -not -type d`;
  my @okFiles;
  my $okFiletypes = join "|", @filetypes;
  for my $file(sort @files){
    chomp $file;
    if($file =~ /\.($okFiletypes)$/i){
      push @okFiles, $file;
    }
  }
  return @okFiles;
}

sub updateMd5sums($){
  my $localDCIM = shift;
  my @lines = `cat $localDCIM/md5sums`;
  my ($byMd5sum, $byFile) = md5sumMaps @lines;

  my @files = findDCIMFiles $localDCIM;
  @files = grep {not defined $$byFile{$_}} @files;

  print "md5summing local files\n";
  @lines = map {runMd5sum $_} @files;
  print @lines;

  my ($extraByMd5sum, $extraByFile) = md5sumMaps @lines;
  my %newByMd5sum = (%$byMd5sum, %$extraByMd5sum);
  my %newByFile = (%$byFile, %$extraByFile);

  my $out;
  for my $file(sort keys %newByFile){
    if(-e "$localDCIM/$file"){
      $out .= "$newByFile{$file}  $file\n";
    }else{
      print "removing md5sum for $file\n";
      my $md5 = $newByFile{$file};
      delete $newByFile{$file};
      delete $newByMd5sum{$md5};
    }
  }

  open FH, "> $localDCIM/md5sums";
  print FH $out;
  close FH;

  return \%newByMd5sum;
}

sub checkMissing($$){
  my ($localDCIM, $remoteDCIM) = @_;
  print "checking $remoteDCIM for missing files"
    . " (by rough filename and exact mtime+size)\n";
  my $localDCIMInfo = updateLocalDCIMInfo $localDir;

  my $localInfosByKey = {};
  for my $file(sort keys %$localDCIMInfo){
    my $key = getFileKey($file);

    $$localInfosByKey{$key} = [] if not defined $$localInfosByKey{$key};
    push @{$$localInfosByKey{$key}}, $$localDCIMInfo{$file};
  }

  my $remoteDCIMInfo = getRemoteDCIMInfo $remoteDCIM;
  my $fileErrors = {};
  for my $file(sort keys %$remoteDCIMInfo){
    my $key = getFileKey($file);

    my @infos;
    if(defined $$localInfosByKey{$key}){
      @infos = @{$$localInfosByKey{$key}};
    }

    my $mtime = $$remoteDCIMInfo{$file}{mtime};
    my $size = $$remoteDCIMInfo{$file}{size};
    my $infoFound = 0;
    for my $info(@infos){
      if($$info{mtime} == $mtime and $$info{size} == $size){
        $infoFound = 1;
        last;
      }
    }

    if(@infos == 0){
      $$fileErrors{$file} = "no local file matching key $key found";
    }elsif(not $infoFound){
      $$fileErrors{$file} = "mismatched mtime/size for key $key";
    }
  }

  if(%$fileErrors == 0){
    print "no missing files for $remoteDCIM\n";
  }else{
    my @files = sort keys %$fileErrors;
    my $missingCount = @files;
    print "$missingCount REMOTE FILES MISSING FROM LOCAL\n";
    for my $file(sort keys %$fileErrors){
      my $err = $$fileErrors{$file};
      print "  $file - $err\n";
    }

    my $nowMillis = int(time * 1000.0);
    my $subdir = "dcim-missing-$nowMillis";

    print ""
      . "\n\n"
      . "copy command:\n"
      . "ipmagic $ipmagicName -s '"
      . "  mkdir -p $remoteTmpDir/$subdir &&"
      . "  cd $remoteDCIM &&"
      . "  cp -ar @files $remoteTmpDir/$subdir"
      . "'\n"
      . "ipmagic $ipmagicName -u $user --rsync -avP :$remoteTmpDir/ $localTmpDir/\n";
  }
  print "\n";
}

sub updateLocalDCIMInfo($){
  my ($localDCIM) = @_;
  my $infoByFile = readLocalDCIMInfoCache $localDCIM;

  my @files = findDCIMFiles $localDCIM;

  my @newFiles = grep {not defined $$infoByFile{$_}} @files;

  my $newCount = @newFiles;
  my $oldCount = %$infoByFile;
  print "getting mtime+size of $newCount local files (skipping $oldCount)\n";
  for my $file(@newFiles){
    my @stat = stat "$localDCIM/$file";
    my $mtime = $stat[9];
    my $size = $stat[7];

    if(not defined $mtime or not defined $size){
      die "stat failed for file: $file\n";
    }
    $$infoByFile{$file} = {mtime=>$mtime, size=>$size};
  }

  writeLocalDCIMInfoCache $localDCIM, $infoByFile;

  return $infoByFile;
}

sub readLocalDCIMInfoCache($){
  my ($localDCIM) = @_;
  my @lines = `cat $localDCIM/info 2>/dev/null`;
  my $infoByFile = {};
  for my $line(@lines){
    if($line !~ /^\s*(\d+)\s+(\d+)\s+(\.\/.+)$/){
      die "malformed info line: $line";
    }
    my ($mtime, $size, $file) = ($1, $2, $3);
    die "duplicate file: $file\n" if defined $$infoByFile{$file};
    $$infoByFile{$file} = {mtime=>$mtime, size=>$size};
  }
  return $infoByFile;
}

sub writeLocalDCIMInfoCache($$){
  my ($localDCIM, $infoByFile) = @_;
  open FH, "> $localDCIM/info";
  for my $file(sort keys %$infoByFile){
    printf FH "%10d  %12d  %s\n",
      $$infoByFile{$file}{mtime}, $$infoByFile{$file}{size}, $file;
  }
  close FH;
}

sub getRemoteDCIMInfo($){
  my ($remoteDCIM) = @_;
  my $findStatCmd = "cd $remoteDCIM && find -type f -exec stat -c %Y-%s-%n {} \\;";

  my $infoByFile = {};
  print "fetching remote file info for $remoteDCIM\n";
  my @stats = `ipmagic $ipmagicName -s '$findStatCmd'`;

  my $statCount = @stats;
  print "fetched info for $statCount files\n";
  for my $stat(@stats){
    chomp $stat;
    if($stat !~ /^(\d+)-(\d+)-(.+)$/){
      die "malformed remote stat output: $stat\n";
    }
    my ($mtime, $size, $file) = ($1, $2, $3);
    if(defined $$infoByFile{$file}){
      die "duplicate file: $file\n";
    }
    $$infoByFile{$file} = {mtime=>$mtime, size=>$size};
  }

  return $infoByFile;
}

sub getFileKey($){
  my ($file) = @_;

  my $key = lc $file;
  $key =~ s/^.*\///;
  $key =~ s/^VID_//i;
  $key =~ s/_[a-z][a-z0-9_\-]*\./\./i;
  $key =~ s/jpeg$/jpg/i;

  return $key;
}

sub remoteDirExists($;$){
  my ($dir, $attempts) = @_;
  $attempts = 3 if not defined $attempts;
  if($attempts <= 0){
    return 0;
  }

  my $out = `ipmagic $ipmagicName -u $user -s 'if [ -d "$dir" ]; then echo exists; fi' 2>/dev/null`;
  if($out =~ /^exists$/){
    return 1;
  }else{
    sleep 0.1;
    return remoteDirExists $dir, ($attempts - 1);
  }
}

sub readProc($@){
  my ($config, @cmd) = @_;
  return run({readProc => 1, %$config}, @cmd);
}
sub readProcLines($@){
  my ($config, @cmd) = @_;
  my $stdout = readProc($config, @cmd);
  my @lines = split /\r\n|\n/, $stdout;
  return @lines;
}

sub run($@){
  my ($config, @cmd) = @_;

  my $conf = {
    wrapShell      => 0,
    printCmd       => 1,
    readProc       => 0,
    failOnError    => 1,
    retryAttempts  => 0,
    timeoutSeconds => 0,
    %$config,
  };

  for my $key(sort keys %$conf){
    if($key !~ /^(wrapShell|printCmd|readProc|failOnError|retryAttempts|timeoutSeconds)$/){
      die "ERROR: unknown run conf opt $key\n";
    }
  }

  if($$conf{wrapShell}){
    @cmd = ("sh", "-c", "@cmd");
  }
  if($$conf{timeoutSeconds} > 0){
    @cmd = ("timeout", $$conf{timeoutSeconds}, @cmd);
  }

  print "@cmd\n" if $$conf{printCmd};

  my $exitCode;
  my $error;
  my $stdout;
  my $retryAttempts = $$conf{retryAttempts};
  while(1){
    if($$conf{readProc}){
      my $cmdFH;
      if(open $cmdFH, "-|", @cmd){
        $stdout = join '', <$cmdFH>;
        close $cmdFH;
        $exitCode = $?;
        $error = $!;
      }else{
        $stdout = "";
        $exitCode = -1;
        $error = $1;
      }
    }else{
      system @cmd;
      $exitCode = $?;
      $error = $!;
    }

    if($exitCode != 0){
      if($retryAttempts > 0){
        $retryAttempts--;
        print "COMMAND FAILED, RETRYING IN 3s...\n";
        sleep 3;
      }else{
        last;
      }
    }else{
      last;
    }
  }

  if($exitCode != 0 and $$conf{failOnError}){
    die "\n\nERROR: '@cmd' failed\n$error\n";
  }

  if($$conf{readProc}){
    return $stdout;
  }else{
    return $exitCode;
  }
}

&main(@ARGV);
