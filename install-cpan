#!/usr/bin/perl
use strict;
use warnings;

my $ipmagicName = "sx";

sub run(@);

my %MODULES = (
  qtemail => [qw(
    Net::SSLeay
    Mail::IMAPClient
    IO::Socket::SSL
    Date::Parse
    MIME::Lite
    MIME::Parser
  )],
  klompbigtext => [qw(
    Text::CSV
  )],
  robinhood => [qw(
    JSON
  )],
  tasmota => [qw(
    Parallel::ForkManager
  )],
);
my @GROUPS = sort keys %MODULES;

my $modulesFmt = join '', map {"    $_ => @{$MODULES{$_}}\n"} @GROUPS;

my $usage = "Usage:
  $0 [MODULE_GROUP MODULE_GROUP ..]
    install cpan module groups indicated
      (default is to install all if none are given)

    \n$modulesFmt
";

sub main(@){
  die $usage if @_ == 1 and $_[0] =~ /^(-h|--help)$/;

  run "ipmagic", $ipmagicName, "cpanp 's conf pushy_https 0; s save'";
  run "ipmagic", $ipmagicName, "cpanp 's conf prereqs 1; s save'";

  run "ipmagic", $ipmagicName, ''
    . '( echo;'
    . '  echo o conf pushy_https 0;'
    . '  echo o conf prerequisites_policy follow;'
    . '  echo o conf commit;'
    . ') | cpan';

  run "ipmagic", $ipmagicName, "perl -MCPAN -e \"CPAN::Shell->notest('install', 'CPAN')\"";

  my @groups = @_;
  @groups = @GROUPS if @_ == 0;
  for my $group(@groups){
    die "$usage\n\nunknown module group: $group\n" if not defined $MODULES{$group};
    my @modules = @{$MODULES{$group}};

    run "ipmagic", $ipmagicName, "cpan -T install @modules";
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
