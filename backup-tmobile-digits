#!/usr/bin/perl
use strict;
use warnings;
use Time::HiRes qw(time);

my $IPMAGIC_NAME = "sx";
my $USER = "nemo";

sub getFileIdName($$);
sub md5($);
sub mtime($);
sub nowMillis();

my $REMOTE_DIGITS_DB_FILE = "/home/.android/data/data/com.tmobile.digits/databases/cDigits3x.db";
my $REMOTE_DIGITS_ATT_FILES_DIR = "/home/$USER/android_storage/Android/data/com.tmobile.digits/files/DIGITS/.Media";

my $BACKUP_DIGITS_DB_DIR = "$ENV{HOME}/Code/sx/backup/backup-mms/tmobile-digits/android/db";
my $BACKUP_DIGITS_ATT_FILES_BASE_DIR = "$ENV{HOME}/Code/sx/backup/backup-mms/tmobile-digits/android/att-files";
my $BACKUP_DIGITS_ATT_FILES_REPO_DIR = "$BACKUP_DIGITS_ATT_FILES_BASE_DIR/repo";

my $USAGE = "Usage:
  $0 -h | --help
    show this message

  $0
    backup messages_repository.db from app data dir
    backup media files from user data dir
";

sub main(@){
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^-h|--help$/){
      print $USAGE;
      exit 0;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }
  system "mkdir", "-p", $BACKUP_DIGITS_DB_DIR;
  my $host = `ipmagic $IPMAGIC_NAME`;
  chomp $host;

  my $nowMillis = nowMillis();
  my $dtm = `date +%Y-%m-%d_%H%M%S`;
  chomp $dtm;

  my $tmpDbFile = "/tmp/backup-tmobile-digits-$nowMillis.db";
  system "ipmagic", $IPMAGIC_NAME, "-u", "root",
    "sqlite3 $REMOTE_DIGITS_DB_FILE 'vacuum into \"$tmpDbFile\"'"
  ;

  my $db = "$BACKUP_DIGITS_DB_DIR/${dtm}_${nowMillis}_cdigits3x.sqlite";
  system "rsync", "-avP", "root\@$host:$tmpDbFile", $db;

  my $attsDir = "$BACKUP_DIGITS_ATT_FILES_BASE_DIR/${dtm}_${nowMillis}";

  #copy previous repo files to new atts dir to speedup rsync
  my @prevRepoAttFiles = glob "$BACKUP_DIGITS_ATT_FILES_REPO_DIR/*";
  @prevRepoAttFiles = sort {mtime($a) <=> mtime($b)} @prevRepoAttFiles;

  for my $file(@prevRepoAttFiles){
    if($file =~ /^(?:.*\/)?(.*)-([0-9a-f]{32})$/){
      my $relFile = $1;
      $relFile =~ s/%/\//g;
      my $relDir = $relFile;
      $relDir =~ s/\/[^\/]*$//;
      my $destDir = "$attsDir/$relDir";
      my $destFile = "$attsDir/$relFile";
      system "mkdir", "-p", $destDir;
      system "cp", "-ar", "--reflink=auto", $file, $destFile;
    }
  }

  #fetch new digits att files
  system "rsync", "-avP", "--del", "$USER\@$host:$REMOTE_DIGITS_ATT_FILES_DIR/", $attsDir;

  #replace att files with symlinks
  my @files = `find $attsDir -type f`;
  for my $file(@files){
    chomp $file;

    my $relFile = $file;
    $relFile =~ s/^$attsDir\///;

    my $fileIdName = getFileIdName($relFile, md5($file));
    my $dest = "$BACKUP_DIGITS_ATT_FILES_REPO_DIR/$fileIdName";

    system "rsync", "-avP", $file, $dest;

    system "rm", $file;
    system "ln", "-s", $dest, $file;
    system "touch", "-h", "-r", $dest, $file;
  }

  system "cd $BACKUP_DIGITS_ATT_FILES_REPO_DIR && git add -A && git commit -m 'automatic commit'";
}

sub getFileIdName($$){
  my ($relFile, $md5) = @_;

  my $boingFile = $relFile;
  $boingFile =~ s/^\/+//;
  $boingFile =~ s/\/+/%/g;

  return "${boingFile}-${md5}";
}

sub md5($){
  my ($file) = @_;
  die "ERROR: could not find file $file\n" if not -e $file;
  my $md5 = `md5sum "$file"`;
  if($md5 =~ /^([0-9a-f]{32})\s/){
    return $1;
  }else{
    die "ERROR: could not md5sum file $file\n";
  }
}

sub mtime($){
  my ($file) = @_;
  my @stat = stat $file;
  return $stat[9];
}

sub nowMillis(){
  return int(time * 1000.0 + 0.5);
}

&main(@ARGV);
