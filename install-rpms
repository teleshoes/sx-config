#!/usr/bin/perl
use strict;
use warnings;

# hold these packages
my @PACKAGES_TO_LOCK = qw(lipstick2vnc);

my $ipmagicName = "sx";
my $user = "nemo";

my $srcDir = "$ENV{HOME}/Code/sx/rpms",
my $destDir = "/media/sdcard/phone/rpms";
my $destDirHome = "/home/nemo/rpms";

my $usage = "Usage:
  $0 -h | --help
    show this message

  $0 [OPTS] [RPM_GLOB_PATTERN]
    -copy $srcDir to $destDir
    -run pkcon -y install-local $destDir/*RPM_GLOB_PATTERN*.rpm
    -add a package hold to zypper for the following packages: @PACKAGES_TO_LOCK

  RPM_GLOB_PATTERN
    only install packages that match \"*RPM_GLOB_PATTERN*.rpm\"
    defaults to empty, i.e.: all packages

  OPTS
    --home
      use $destDirHome instead of $destDir
";

sub main(@){
  my $rpmGlobPattern = undef;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $usage;
      exit 0;
    }elsif($arg =~ /^(--home)$/){
      $destDir = $destDirHome;
    }elsif(not defined $rpmGlobPattern){
      $rpmGlobPattern = $arg;
    }else{
      die "$usage\nERROR: unknown arg $arg\n";
    }
  }

  $rpmGlobPattern = "" if not defined $rpmGlobPattern;

  my $host = `ipmagic $ipmagicName`;
  chomp $host;
  die "could not find host\n" if $host !~ /\w/;

  system "rsync", "-avP", "--del", "$srcDir/", "$user\@$host:$destDir/";

  my $fullRpmGlobPath = "$destDir/*$rpmGlobPattern*.rpm";

  system "ipmagic", $ipmagicName, "-u", "root", "pkcon -y install-local $fullRpmGlobPath";

  for my $pkg(@PACKAGES_TO_LOCK){
    system "ipmagic", $ipmagicName, "-u", "root", "zypper addlock $pkg";
  }
}

&main(@ARGV);
