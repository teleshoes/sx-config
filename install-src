#!/usr/bin/perl
use strict;
use warnings;

my $ipmagicName = "sx";
my $user = "nemo";

my $srcDir = "/home/$user/Code";

sub install($$$);

my %INSTALLS = (
  lirrtraintime  => ["https://github.com/teleshoes/lirrtraintime", "./install.sh"],
  net            => ["https://github.com/teleshoes/net",           "./install.pl"],
  qtbigtext      => ["https://github.com/teleshoes/qtbigtext",     "./install.pl"],
  qtemail        => ["https://github.com/teleshoes/qtemail",       "./install.sh"],
);

my @INSTALL_NAMES = sort keys %INSTALLS;
my %REPOS = map {$_ => ${$INSTALLS{$_}}[0]} @INSTALL_NAMES;
my %CMDS = map {$_ => ${$INSTALLS{$_}}[1]} @INSTALL_NAMES;

my $repoFmt = join '', map {sprintf "      %-20s => %s\n", $_, $REPOS{$_}} @INSTALL_NAMES;

my $usage = "Usage:
  $0 [NAME NAME ..]
    install from named repos (if no repos are named, default is install all)

    NAME => REPO:\n$repoFmt
";

sub main(@){
  die $usage if @_ == 1 and $_[0] =~ /^(-h|--help)$/;

  my @names = @_;
  @names = @INSTALL_NAMES if @_ == 0;
  for my $name(@names){
    die "$usage\n\nunknown repo name: $name\n" if not defined $REPOS{$name};
    install $name, $REPOS{$name}, $CMDS{$name};
  }
}

sub install($$$){
  my ($repoName, $repo, $cmd) = @_;
  print "\n\n\ninstalling $repoName\n";
  system "ipmagic", $ipmagicName, "-s", ""
    . " set -x"
    . " && rm -rf $srcDir/$repoName"
    . " && mkdir -p $srcDir"
    . " && git clone $repo $srcDir/$repoName"
    . " && cd $srcDir/$repoName"
    . " && echo -ne \"=====\\n=====\\n=====\\n\""
    . " && $cmd"
    ;
}

&main(@ARGV);
