#!/usr/bin/perl
use strict;
use warnings;
use Time::HiRes qw(time);

my $IPMAGIC_NAME = "sx";
my $USER = "nemo";

my $DATA_DIR = "/home/nemo/.local/share/waydroid/data";

my $SETTINGS = {
  secure => {
    "tts_default_synth" => "com.google.android.tts",
  },
};

my $JITSI_DB = "$DATA_DIR/data/org.jitsi.meet/databases/RKStorage";
my $JITSI_DB_CONF = {
  '@jitsi-meet/features/recent-list' => ''
    . '['
    . '{' . '"conference":"https://meet.jit.si/%%%JITSI_ROOM%%%"'
    . ',' . '"date":%%%NOW_MILLIS%%%'
    . ',' . '"duration":0'
    . '}'
    . ']'
  ,
  '@jitsi-meet/features/base/settings' => ''
    . '{' . '"avatarID":"ffffffffffffffffffffffffffffffff"'
    . ',' . '"displayName":"%%%JITSI_NAME%%%"'
    . ',' . '"email":""'
    . ',' . '"localFlipX":true'
    . ',' . '"startAudioOnly":true'
    . ',' . '"startWithAudioMuted":false'
    . ',' . '"startWithVideoMuted":true'
    . '}'
  ,
};

sub applySettings();
sub setupJitsi();
sub nowMillis();
sub run(@);
sub runQuiet(@);

sub main(@){
  applySettings();
  setupJitsi();
}

sub applySettings(){
  print "applying settings\n";

  my $setCmds = "";
  for my $namespace(sort keys %$SETTINGS){
    for my $key(sort keys %{$$SETTINGS{$namespace}}){
      my $val = $$SETTINGS{$namespace}{$key};
      $setCmds .= "settings put '$namespace' '$key' '$val';\n";
    }
  }

  run "ipmagic", $IPMAGIC_NAME, "wd shell \"$setCmds\"";
}

sub setupJitsi(){
  print "\nsetting up jitsi:\n";
  my $nowMillis = nowMillis();
  my $jitsiName = `jitsi-meet --name`;
  chomp $jitsiName;
  my $jitsiRoom = `jitsi-meet --room`;
  chomp $jitsiRoom;

  for my $rowKey(sort keys %$JITSI_DB_CONF){
    my $rowValue = $$JITSI_DB_CONF{$rowKey};
    $rowValue =~ s/%%%NOW_MILLIS%%%/$nowMillis/g;
    $rowValue =~ s/%%%JITSI_ROOM%%%/$jitsiRoom/g;
    $rowValue =~ s/%%%JITSI_NAME%%%/$jitsiName/g;
    $rowValue =~ s/"/"\\""/g;
    print "catalystLocalStorage.$rowKey:\n";
    run "ipmagic", $IPMAGIC_NAME, "-u", "root", "
      sqlite3 $JITSI_DB \"
        select value FROM catalystLocalStorage WHERE key = '$rowKey';
        select ' => ';
        update catalystLocalStorage
          set value = '$rowValue'
          where key = '$rowKey';
        select value FROM catalystLocalStorage WHERE key = '$rowKey';
      \"
    ";
  }
}

sub nowMillis(){
  return int(time * 1000.0 + 0.5);
}

sub run(@){
  print "@_\n";
  system @_;
}
sub runQuiet(@){
  system @_;
}

&main(@ARGV);
