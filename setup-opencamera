#!/usr/bin/perl
use strict;
use warnings;
use lib `dirname $0 | tr -d '\n'`;
use PhoneBackupUtils;

my $IPMAGIC_NAME = "sx";
my $USER = "nemo";

my $OPENCAMERA_DATA_DIR = "/home/.android/data/data/net.sourceforge.opencamera";
my $PREFS_XML_FILE = "$OPENCAMERA_DATA_DIR/shared_prefs/net.sourceforge.opencamera_preferences.xml";

my $SDCARD_BLOCK_DEV = "/dev/mmcblk0p2";
my $DCIM_PATH = "DCIM/OpenCamera",

my $PREFS_TEMPLATE = [
  ["preference_camera_api"              => "string"   => "preference_camera_api_camera2"],
  ["focus_value_0_false"                => "string"   => "focus_mode_manual2"],
  ["preference_focus_distance"          => "float"    => "3.3533278"],
  ["preference_using_saf"               => "boolean"  => "true"],
  ["preference_save_location_saf"       => "string"   => "%DCIM_URI%"],
];

sub editPrefs();

sub main(@){
  editPrefs();
}

sub getDCIMUri(){
  my $sdcardUUID = getIpmagicBlockDevUUID($IPMAGIC_NAME, $SDCARD_BLOCK_DEV);
  my $path = $DCIM_PATH;
  $path =~ s/\//%2F/g;
  return "content://com.android.externalstorage.documents/tree/$sdcardUUID%3A$path";
}

sub editPrefs(){
  my $dcimUri = getDCIMUri();

  my $contents = "";
  if(ipmagicTest $IPMAGIC_NAME, "root", "-f", $PREFS_XML_FILE){
    $contents = readProc "ipmagic", $IPMAGIC_NAME, "-u", "root", "cat", $PREFS_XML_FILE;
  }

  my $prefs = {};
  for my $prefArr(@$PREFS_TEMPLATE){
    my ($key, $type, $val) = @$prefArr;
    $val =~ s/%DCIM_URI%/$dcimUri/g;
    $$prefs{$key} = {type => $type, value => $val};
  }

  my @prefLines;

  print "\n\n";
  my $oldPrefs = {};
  for my $line(split /\n/, $contents){
    next if $line =~ /^(<\?xml|<map>|<\/map>)/;
    next if $line =~ /^\s+$/;

    my ($type, $key, $val);
    if($line =~ /^\s*<(boolean|float|int)\s+name="([^"]+)"\s+value="([^"]*)"\s*\/\s*>\s*$/){
      ($type, $key, $val) = ($1, $2, $3);
    }elsif($line =~ /^\s*<(string)\s+name="([^"]+)">(.*)<\s*\/\s*string>\s*$/){
      ($type, $key, $val) = ($1, $2, $3);
    }else{
      die "ERROR: malformed opencamera prefs line $line\n";
    }

    if(defined $$prefs{$key}){
      my $newType = $$prefs{$key}{type};
      my $newVal = $$prefs{$key}{value};
      if($type eq $newType and $val eq $newVal){
        #pref is already set
        delete $$prefs{$key};
        push @prefLines, $line;
      }else{
        print "removing old $key=$val\n";
      }
    }else{
      push @prefLines, $line;
    }
  }

  my $isChanged = 0;
  for my $key(sort keys %$prefs){
    my $type = $$prefs{$key}{type};
    my $val = $$prefs{$key}{value};
    my $line;
    if($type eq "string"){
      $line = "    <$type name=\"$key\">$val</$type>";
    }else{
      $line = "    <$type name=\"$key\" value=\"$val\" />";
    }
    print "adding $key=$val\n";
    $isChanged = 1;
    push @prefLines, $line;
  }

  print "\n\n";

  my @lines = (
    "<?xml version='1.0' encoding='utf-8' standalone='yes' ?>",
    "<map>",
    @prefLines,
    "</map>",
  );
  my $newContents = join("", map {"$_\n"} @lines);

  if($isChanged){
    my $tmpFile = "/tmp/setup-opencamera-" . nowMillis();
    writeFile $tmpFile, $newContents;
    run "ipmagic", $IPMAGIC_NAME, "-u", "root",
      "mkdir", "-p", "$OPENCAMERA_DATA_DIR/shared_prefs";
    run "ipmagic", $IPMAGIC_NAME, "-u", "root",
      "--rsync", "-avP", $tmpFile, ":$PREFS_XML_FILE";
    run "ipmagic", $IPMAGIC_NAME, "-u", "root",
      "chown", "-R", "$OPENCAMERA_DATA_DIR/shared_prefs",
      "--reference=$OPENCAMERA_DATA_DIR/cache";

    if(defined $$prefs{preference_save_location_saf}){
      print "\n\n\n#####\nNOTE: URI GRANT NEEDED FOR SAF LOCATION UPDATE\n";
    }
  }else{
    print "NO CHANGE IN PREFS\n";
  }
}

&main(@ARGV);
