#!/usr/bin/perl
use strict;
use warnings;
use Cwd 'abs_path';

sub gitHead($);
sub gitCommit($$$$);
sub gitGetCommitsSince($$);
sub gitStatCommit($$);
sub gitAssertRepoDir($);
sub run(@);
sub runQuiet(@);

my $BASE_DIR = "$ENV{HOME}/Code/s5";
my $BACKUP_DIR = "$BASE_DIR/backup";

my $DB_DIR = "$BACKUP_DIR/backup-dbs";
my $DB_RAW_DIR = "$DB_DIR/raw";
my $DB_REPO = "$DB_DIR/repo";

my $SMS_DIR = "$BACKUP_DIR/backup-sms";
my $SMS_RAW_DIR = "$SMS_DIR/raw";
my $SMS_REPO = "$SMS_DIR/repo";
my $SMS_BY_CONTACT_DIR = "$SMS_DIR/by-contact";

my $CALL_DIR = "$BACKUP_DIR/backup-call";
my $CALL_RAW_DIR = "$CALL_DIR/raw";
my $CALL_REPO = "$CALL_DIR/repo";
my $CALL_BY_CONTACT_DIR = "$CALL_DIR/by-contact";

my $CONTACTS_DIR = "$BACKUP_DIR/backup-contacts";
my $CONTACTS_RAW_DIR = "$CONTACTS_DIR/raw";
my $CONTACTS_REPO = "$CONTACTS_DIR/repo";

my $COMM_TOOLS_DIR = "$BASE_DIR/comm-tools";
my $CMD_SQLITE_DUMP = "$COMM_TOOLS_DIR/sqlite-dump.pl";
my $CMD_ADD_COMM_TO_REPO = "$COMM_TOOLS_DIR/add-comm-to-repo.pl";
my $CMD_SMS_DB_IMPORTER = "$COMM_TOOLS_DIR/sms_db_importer.py";
my $CMD_CALL_DB_EXTRACT = "$COMM_TOOLS_DIR/call-db-extract.pl";
my $CMD_CONTACTS_DB_EXTRACT = "$COMM_TOOLS_DIR/contacts-db-extract";
my $CMD_CONTACTS_SORT = "$COMM_TOOLS_DIR/contacts-sort.pl";
my $CMD_CONTACTS_SYMLINKS = "$COMM_TOOLS_DIR/contacts-symlinks.pl";

my $DATA_MOUNTPOINT = "/mnt/android-data";
my $MMSSMS_DB_REMOTE_FILE = "user_de/0/com.android.providers.telephony/databases/mmssms.db";
my $CONTACTS_DB_REMOTE_FILE = "data/com.android.providers.contacts/databases/contacts2.db",
my $MMS_PARTS_REMOTE_DIR = "user_de/0/com.android.providers.telephony/app_parts",

my $MMS_MSG_REPO = "$BACKUP_DIR/backup-mms/repo";
my $MMS_PARTS_REPO = "$BACKUP_DIR/backup-mms/parts-repo";

sub main(@){
  my $host = `s5`;
  die "failed to get host" if $? != 0;
  chomp $host;

  my $now = `date +'%Y-%m-%d_%s'`;
  chomp $now;

  my $mmssmsDb = "$DB_RAW_DIR/mmssms-$now.db";
  my $contactsDb = "$DB_RAW_DIR/contacts2-$now.db";

  #########################################################
  # copy raw data (dbs, mms files) phone => computer
  #########################################################
  print "\n\n=====COPY RAW DATA\n";
  run "scp", "root\@$host:$DATA_MOUNTPOINT/$MMSSMS_DB_REMOTE_FILE", $mmssmsDb;
  run "scp", "root\@$host:$DATA_MOUNTPOINT/$CONTACTS_DB_REMOTE_FILE", $contactsDb;
  run "rsync", "-avP", "$host:$DATA_MOUNTPOINT/$MMS_PARTS_REMOTE_DIR/", "$MMS_PARTS_REPO/";

  #########################################################
  # prepare git repo summary
  #########################################################
  my @allRepos = ($DB_REPO, $MMS_PARTS_REPO, $CONTACTS_REPO,
    $SMS_REPO, $MMS_MSG_REPO, $CALL_REPO);
  my %oldRepoHeads = map {$_ => gitHead $_} @allRepos;

  #########################################################
  # add raw data to repos
  #########################################################
  print "\n\n=====ADD RAW DATA TO REPOS\n";
  run "rm", "-f", "$DB_REPO/mmssms-db";
  run "$CMD_SQLITE_DUMP", $mmssmsDb, "$DB_REPO/mmssms-db";
  gitCommit $DB_REPO, "mmssms-db", "nodiff", "mmssms db automatic commit";

  run "rm", "-f", "$DB_REPO/contacts-db";
  run "$CMD_SQLITE_DUMP", $contactsDb, "$DB_REPO/contacts-db";
  gitCommit $DB_REPO, "contacts-db", "nodiff", "contacts db automatic commit";

  gitCommit $MMS_PARTS_REPO, "-A", "nodiff", "automatic commit";

  #########################################################
  # parse contacts into repo
  #########################################################
  print "\n\n=====PARSE CONTACTS\n";
  my $vcfFile = "$CONTACTS_RAW_DIR/$now.vcf";
  run "$CMD_CONTACTS_DB_EXTRACT $contactsDb > $vcfFile";
  die "error running $CMD_CONTACTS_DB_EXTRACT\n" if $? != 0;
  run "cp", "-ar", $vcfFile, "$CONTACTS_REPO/contacts.vcf";
  run "$CMD_CONTACTS_SORT";
  gitCommit $CONTACTS_REPO, "contacts.vcf", "diff", "automatic commit";

  #########################################################
  # parse SMS+MMS into repo
  #########################################################
  print "\n\n=====PARSE SMS+MMS\n";
  my $smsFile = "$SMS_RAW_DIR/sms-$now.sms";
  run ($CMD_SMS_DB_IMPORTER,
    "--csv-file", $smsFile,
    "--db-file", $mmssmsDb,
    "--mms-msg-dir", $MMS_MSG_REPO,
    "--mms-parts-dir", $MMS_PARTS_REPO,
    "export-from-db",
  );
  die "error running $CMD_SMS_DB_IMPORTER\n" if $? != 0;
  run "$CMD_ADD_COMM_TO_REPO", "--sms", $smsFile;
  print "SMS REPO\n";
  gitCommit $SMS_REPO, "*.sms", "diff", "automatic commit";
  print "===\n";
  print "MMS REPO\n";
  gitCommit $MMS_MSG_REPO, "-A", "diff", "automatic commit";

  #########################################################
  # parse calls into repo
  #########################################################
  print "\n\n=====PARSE CALLS\n";
  my $callFile = "$CALL_RAW_DIR/call-$now.call";
  run "$CMD_CALL_DB_EXTRACT $contactsDb > $callFile";
  die "error running $CMD_CALL_DB_EXTRACT\n" if $? != 0;
  run "$CMD_ADD_COMM_TO_REPO", "--call", $callFile;
  gitCommit $CALL_REPO, "*.call", "diff", "automatic commit";

  #########################################################
  # by-contact symlinks
  #########################################################
  print "\n\n=====SMS/CALL BY-CONTACT SYMLINKS\n";
  run "$CMD_CONTACTS_SYMLINKS", "$CONTACTS_REPO/contacts.vcf",
    "sms", $SMS_REPO, $SMS_BY_CONTACT_DIR;
  run "$CMD_CONTACTS_SYMLINKS", "$CONTACTS_REPO/contacts.vcf",
    "call", $CALL_REPO, $CALL_BY_CONTACT_DIR;

  #########################################################
  # show git repo summary
  #########################################################
  print "\n\n=====GIT REPO SUMMARY\n";
  my %newRepoHeads = map {$_ => gitHead $_} @allRepos;
  for my $repo(@allRepos){
    my $oldHead = $oldRepoHeads{$repo};
    my $newHead = $newRepoHeads{$repo};
    if($oldHead ne $newHead){
      my @commits = gitGetCommitsSince $repo, $oldHead;
      die "error listing commits since $oldHead\n" if @commits == 0;
      for my $commit(reverse @commits){
        print gitStatCommit($repo, $commit);
      }
    }
  }
}

sub gitCommit($$$$){
  my ($repoDir, $addArg, $diff, $commitMessage) = @_;
  chdir $repoDir;
  gitAssertRepoDir $repoDir;

  runQuiet "git", "--no-pager", "diff" if defined $diff and $diff eq "diff";
  runQuiet "git", "add", $addArg;

  my @status = `git status --short 2>&1`;
  for my $line(@status){
    die "unexpected git status: @status" if $line !~ /^(M|A)\s/;
  }

  if(@status > 0){
    runQuiet "git", "commit", "-m", $commitMessage;
  }

  @status = `git status --short 2>&1`;
  if(@status > 0){
    die "git repo $repoDir has changes or errors: @status";
  }
}

sub gitHead($){
  my ($repoDir) = @_;
  chdir $repoDir;
  gitAssertRepoDir $repoDir;

  my $head = `git rev-parse HEAD`;
  chomp $head;
  die "invalid commit: $head\n" if $head !~ /^[0-9a-f]{40}$/;
  return $head;

  my @status = `git status --short 2>&1`;
  if(@status > 0){
    die "git repo $repoDir has changes or errors: @status";
  }
}

sub gitGetCommitsSince($$){
  my ($repoDir, $commit) = @_;
  chdir $repoDir;
  gitAssertRepoDir $repoDir;

  my @commits = `git rev-list $commit..HEAD`;
  chomp foreach @commits;
  for my $commit(@commits){
    die "invalid output of rev-list: $commit\n" if $commit !~ /^[0-9a-f]{40}$/;
  }
  return @commits;
}

sub gitStatCommit($$){
  my ($repoDir, $commit) = @_;
  chdir $repoDir;
  gitAssertRepoDir $repoDir;

  my $stat = `git show $commit --format='' --stat`;
  return $stat;
}

sub gitAssertRepoDir($){
  my ($repoDir) = @_;
  my $gitDir = `git rev-parse --show-toplevel`;
  chomp $gitDir;
  my $absGitDir = abs_path $gitDir;
  my $absRepoDir = abs_path $repoDir;
  if($absGitDir ne $absRepoDir){
    die "git toplevel (\"$gitDir\") does not match expected (\"$repoDir\")";
  }
}

sub copyDbToRepo($$){
  my ($sqliteDb, $repo) = @_;
}

sub run(@){
  print "@_\n";
  runQuiet @_;
}
sub runQuiet(@){
  system @_;
  if($? != 0){
    die "\n\nERROR: $_[0]\n";
  }
}

&main(@ARGV);
