#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $IPMAGIC_NAME = "sx";
my $USER = "nemo";

my $WAYDROID_SRC_DIR = "$ENV{HOME}/Code/sx/apks-waydroid",
my $WAYDROID_DEST_DIR_SD = "/media/sdcard/phone/apks-waydroid";
my $WAYDROID_DEST_DIR_HOME = "/home/nemo/apks-waydroid";

my $ALIENDALVIK_SRC_DIR = "$ENV{HOME}/Code/sx/apks-aliendalvik",
my $ALIENDALVIK_DEST_DIR_SD = "/media/sdcard/phone/apks-aliendalvik";
my $ALIENDALVIK_DEST_DIR_HOME = "/home/nemo/apks-aliendalvik";

my $ALIENDALVIK4_SRC_DIR = "$ENV{HOME}/Code/sx/apks-aliendalvik4",
my $ALIENDALVIK4_DEST_DIR_SD = "/media/sdcard/phone/apks-aliendalvik4";
my $ALIENDALVIK4_DEST_DIR_HOME = "/home/nemo/apks-aliendalvik4";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC [OPTS] [PREFIX]
    -run apk-name on all local apk files:
      apk-name $WAYDROID_SRC_DIR/*.apk
    -copy all apks local to remote:
      $WAYDROID_SRC_DIR => $USER\@`$IPMAGIC_NAME`:$WAYDROID_DEST_DIR_SD
    -list <APK>s that match <PREFIX> (or all, if not given)
      $WAYDROID_SRC_DIR/<PREFIX>*.apk
    -for --waydroid (default):
      -on remote, run: wd -i $WAYDROID_DEST_DIR_SD/<APK> ..
    -for --aliendalvik:
      -on remote, run: apkd-install-preload $ALIENDALVIK_DEST_DIR_SD/<APK> ..
      -on remote, run: apkd-install $ALIENDALVIK_DEST_DIR_SD/<APK> ..
    -for --aliendalvik4:
      -on remote, run: apkd-install-preload $ALIENDALVIK4_DEST_DIR_SD/<APK> ..
      -on remote, run: apkd-install $ALIENDALVIK4_DEST_DIR_SD/<APK> ..

  OPTS
    --waydroid | --wd
      use waydroid apk dirs and install with `wd`
        src  => $WAYDROID_SRC_DIR
        dest => $WAYDROID_DEST_DIR_SD
      (this is the default)

    --aliendalvik | --ad | --alien
      use aliendalvik apk dirs and install with `apkd-install-preload`+`apkd-install`
        src  => $ALIENDALVIK_SRC_DIR
        dest => $ALIENDALVIK_DEST_DIR_SD

    --aliendalvik4
      use aliendalvik4 apk dirs and install with `apkd-install-preload`+`apkd-install`
        src  => $ALIENDALVIK4_SRC_DIR
        dest => $ALIENDALVIK4_DEST_DIR_SD

    --home
      use $WAYDROID_DEST_DIR_HOME instead of $WAYDROID_DEST_DIR_SD
        (or $ALIENDALVIK_DEST_DIR_HOME instead of $ALIENDALVIK_DEST_DIR_SD)
        (or $ALIENDALVIK4_DEST_DIR_HOME instead of $ALIENDALVIK4_DEST_DIR_SD)
";

sub run(@);
sub runQuiet(@);

my $MODE_WAYDROID = "waydroid";
my $MODE_ALIENDALVIK = "aliendalvik";
my $MODE_ALIENDALVIK4 = "aliendalvik4";

sub main(@){
  my $mode = $MODE_WAYDROID;
  my $prefix = undef;
  my $useHomeDir = 0;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--waydroid|--wd)$/){
      $mode = $MODE_WAYDROID;
    }elsif($arg =~ /^(--aliendalvik|--ad|--alien)$/){
      $mode = $MODE_ALIENDALVIK;
    }elsif($arg =~ /^(--aliendalvik4)$/){
      $mode = $MODE_ALIENDALVIK4;
    }elsif($arg =~ /^(--home)$/){
      $useHomeDir = 1;
    }elsif($arg =~ /^[a-zA-Z0-9]/){
      die "$USAGE\nERROR: only one PREFIX allowed\n" if defined $prefix;
      $prefix = $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  my ($srcDir, $destDir);
  if($mode eq $MODE_WAYDROID){
    $srcDir = $WAYDROID_SRC_DIR;
    $destDir = $useHomeDir ? $WAYDROID_DEST_DIR_HOME : $WAYDROID_DEST_DIR_SD;
  }elsif($mode eq $MODE_ALIENDALVIK){
    $srcDir = $ALIENDALVIK_SRC_DIR;
    $destDir = $useHomeDir ? $ALIENDALVIK_DEST_DIR_HOME : $ALIENDALVIK_DEST_DIR_SD;
  }elsif($mode eq $MODE_ALIENDALVIK4){
    $srcDir = $ALIENDALVIK4_SRC_DIR;
    $destDir = $useHomeDir ? $ALIENDALVIK4_DEST_DIR_HOME : $ALIENDALVIK4_DEST_DIR_SD;
  }else{
    die "ERROR: unknown mode $mode\n";
  }

  $prefix = "" if not defined $prefix;

  my $host = `ipmagic $IPMAGIC_NAME`;
  chomp $host;
  die "could not find host\n" if $host !~ /\w/;

  print "\n\nrenaming ALL local apk files\n";
  my @apkFiles = glob "$srcDir/*.apk";
  for my $apkFile(@apkFiles){
    runQuiet "apk-name", $apkFile;
    if($? != 0){
      die "ERROR: apk-name failed for $apkFile\n";
    }
  }

  print "\n\ncopying ALL apk files local => remote\n";
  run "rsync", "-avP", "--del", "$srcDir/", "$USER\@$host:$destDir/";

  my @baseApkFiles = map {/(^.*\/)?([^\/]+)/; $2} @apkFiles;
  my @filteredApkFiles = grep {/^$prefix/} @baseApkFiles;
  my @destApkFiles = map {"$destDir/$_"} @filteredApkFiles;

  if($mode eq $MODE_WAYDROID){
    print "\n\ninstalling apk files remotely with `wd -i`\n";
    run "ipmagic", $IPMAGIC_NAME, "wd", "-i", @destApkFiles;
  }elsif($mode eq $MODE_ALIENDALVIK or $mode eq $MODE_ALIENDALVIK4){
    print "\n\ninstalling apk files remotely with apkd\n";
    run "ipmagic", $IPMAGIC_NAME, "-u", "root",
      "echo", "apkd-install-preload", @destApkFiles;
    print "\n";
    run "ipmagic", $IPMAGIC_NAME,
      "apkd-install", @destApkFiles;

    print "\n\nignore the installation UIs\n";
    print "just watch each app install on its own in order, and then close the UIs\n";
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub run(@){
  print "@_\n";
  system @_;
}
sub runQuiet(@){
  system @_;
}

&main(@ARGV);
