#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

my $IPMAGIC_NAME = "sx";
my $USER = "nemo";

my $SRC_DIR_PREFIX = "$ENV{HOME}/Code/sx/apks";
my $DEST_DIR_SD_PREFIX = "/media/sdcard/phone/apks";
my $DEST_DIR_HOME_PREFIX = "/home/nemo/apks";

my $MODE_WAYDROID = "waydroid";
my $MODE_ALIENDALVIK = "aliendalvik";
my $MODE_ALIENDALVIK4 = "aliendalvik4";

my $DEFAULT_MODE = $MODE_ALIENDALVIK;

my %WD_AD_INSTALL_CMDS = (
  $MODE_WAYDROID     => "wd",
  $MODE_ALIENDALVIK  => "ad",
  $MODE_ALIENDALVIK4 => "ad",
);

my $ANDROID_DATA_DIR = "/home/.appsupport/instance/$USER/data";

my %OBB_DEST_DIRS = (
  $MODE_WAYDROID     => "/home/$USER/.local/share/waydroid/data/media/obb",
  $MODE_ALIENDALVIK  => "$ANDROID_DATA_DIR/media/obb",
  $MODE_ALIENDALVIK4 => "$ANDROID_DATA_DIR/media/obb",
);

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC [OPTS] [PREFIX]
    -select <MODE> and <WD_AD_INSTALL_CMD> based on OPTS
      -default is MODE=$DEFAULT_MODE and WD_AD_INSTALL_CMD=$WD_AD_INSTALL_CMDS{$DEFAULT_MODE}
    -run apk-name on all local apk files:
      apk-name $SRC_DIR_PREFIX-<MODE>/*.apk
    -copy all apks local to remote:
      $SRC_DIR_PREFIX-<MODE> => $USER\@`$IPMAGIC_NAME`:$DEST_DIR_SD_PREFIX-<MODE>
    -copy obb local to remote:
      for <MODE>=waydroid:
        $SRC_DIR_PREFIX-<MODE>/obb => root\@`$IPMAGIC_NAME:$OBB_DEST_DIRS{waydroid}
      for <MODE>=aliendalvik
        $SRC_DIR_PREFIX-<MODE>/obb => root\@`$IPMAGIC_NAME:$OBB_DEST_DIRS{aliendalvik}
      for <MODE>=aliendalvik4:
        $SRC_DIR_PREFIX-<MODE>/obb => root\@`$IPMAGIC_NAME:$OBB_DEST_DIRS{aliendalvik4}
    -list <APK>s that match <PREFIX> (or all, if not given)
      $SRC_DIR_PREFIX-<MODE>/<PREFIX>*.apk
    -install apks
      -on remote, run: <WD_AD_INSTALL_CMD> -i $DEST_DIR_SD_PREFIX-<MODE>/<APK> ..

  OPTS
    --waydroid | --wd
      MODE=waydroid and WD_AD_INSTALL_CMD=$WD_AD_INSTALL_CMDS{waydroid}

    --aliendalvik | --ad | --alien
      MODE=aliendalvik and WD_AD_INSTALL_CMD=$WD_AD_INSTALL_CMDS{aliendalvik}

    --aliendalvik4
      MODE=aliendalvik4 and WD_AD_INSTALL_CMD=$WD_AD_INSTALL_CMDS{aliendalvik4}

    --home
      use $DEST_DIR_HOME_PREFIX-<MODE> instead of $DEST_DIR_SD_PREFIX-<MODE>
";

sub run(@);
sub runQuiet(@);

sub main(@){
  my $mode = $DEFAULT_MODE;
  my $prefix = undef;
  my $useHomeDir = 0;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--waydroid|--wd)$/){
      $mode = $MODE_WAYDROID;
    }elsif($arg =~ /^(--aliendalvik|--ad|--alien)$/){
      $mode = $MODE_ALIENDALVIK;
    }elsif($arg =~ /^(--aliendalvik4)$/){
      $mode = $MODE_ALIENDALVIK4;
    }elsif($arg =~ /^(--home)$/){
      $useHomeDir = 1;
    }elsif($arg =~ /^[a-zA-Z0-9]/){
      die "$USAGE\nERROR: only one PREFIX allowed\n" if defined $prefix;
      $prefix = $arg;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  my $srcDir = "$SRC_DIR_PREFIX-$mode";
  my $destDir = ($useHomeDir ? $DEST_DIR_HOME_PREFIX : $DEST_DIR_SD_PREFIX) . "-$mode";
  my $obbSrcDir = "$SRC_DIR_PREFIX-$mode/obb";
  my $obbDestDir = $OBB_DEST_DIRS{$mode};
  my $wd_ad = $WD_AD_INSTALL_CMDS{$mode};

  $prefix = "" if not defined $prefix;

  my $host = `ipmagic $IPMAGIC_NAME`;
  chomp $host;
  die "could not find host\n" if $host !~ /\w/;

  print "\n\nrenaming ALL local apk files\n";
  my @apkFiles = glob "$srcDir/*.apk";
  for my $apkFile(@apkFiles){
    runQuiet "apk-name", $apkFile;
    if($? != 0){
      die "ERROR: apk-name failed for $apkFile\n";
    }
  }

  print "\n\ncopying ALL apk files local => remote\n";
  run "rsync", "-avP", "--del", "$srcDir/", "$USER\@$host:$destDir/";

  print "\n\ncopying obb files local => remote\n";
  run "rsync", "-avP", "--del", "$obbSrcDir/", "root\@$host:$obbDestDir/";
  run "ipmagic", $IPMAGIC_NAME, "-u", "root", "chown", "-R", "media_rw.media_rw", $obbDestDir;

  my @baseApkFiles = map {/(^.*\/)?([^\/]+)/; $2} @apkFiles;
  my @filteredApkFiles = grep {/^$prefix/} @baseApkFiles;
  my @destApkFiles = map {"$destDir/$_"} @filteredApkFiles;

  print "\n\ninstalling apk files remotely with `$wd_ad -i`\n";
  run "ipmagic", $IPMAGIC_NAME, $wd_ad, "-i", @destApkFiles;
}

sub run(@){
  print "@_\n";
  system @_;
}
sub runQuiet(@){
  system @_;
}

&main(@ARGV);
