#!/usr/bin/perl
use strict;
use warnings;
use lib `dirname $0 | tr -d '\n'`;
use PhoneBackupUtils;
use Time::HiRes qw(time);

my $IPMAGIC_NAME = "sx";
my $REMOTE_BASE_ANDROID_DIR = "/home/.android/data";
my $PACKAGE_RESTRICTIONS_XML_FILE = "$REMOTE_BASE_ANDROID_DIR/system/users/0/package-restrictions.xml";
my $NOTIFICATION_POLICY_XML_FILE = "$REMOTE_BASE_ANDROID_DIR/system/notification_policy.xml";
my $URI_GRANTS_XML_FILE = "$REMOTE_BASE_ANDROID_DIR/system/urigrants.xml";

my $ALIENDALVIK_START_SCRIPT = "/usr/sbin/start-aliendalvik.sh";
my $POST_ALIENDALVIK_START_SCRIPT = "/usr/local/bin/aliendalvik-post-start";

my $REMOTE_DATA_DIR = "$REMOTE_BASE_ANDROID_DIR/data";

#pkg, SFOS block device, path relative to storage device
#  uri=content://com.android.externalstorage.documents/tree/$BLOCK_UUID%:$RELATIVE_PATH"
my $EXTERNAL_STORAGE_URI_GRANTS = [
  {pkg => "net.sourceforge.opencamera", dev => "/dev/mmcblk0p2", path => "DCIM/OpenCamera"},
];

my $SETTINGS = {
  secure => {
    "anr_show_background"         => "1",
    "tts_default_synth"           => "com.google.android.tts",
    "tts_default_rate"            => "122",
    "tts_default_pitch"           => "92",
    "show_ime_with_hard_keyboard" => "1",
  },
  system => {
    "accelerometer_rotation" => "1",
  },
  global => {
    "debug_app"      => "org.coolreader",
    "time_zone"      => "America/New_York",
    "auto_time_zone" => "0",
  },
};

my $JITSI_DB = "$REMOTE_DATA_DIR/org.jitsi.meet/databases/RKStorage";
my $JITSI_DB_CONF = {
  '@jitsi-meet/features/recent-list' => ''
    . '['
    . '{' . '"conference":"https://meet.jit.si/%%%JITSI_ROOM%%%"'
    . ',' . '"date":%%%NOW_MILLIS%%%'
    . ',' . '"duration":0'
    . '}'
    . ']'
  ,
  '@jitsi-meet/features/base/settings' => ''
    . '{' . '"avatarID":"ffffffffffffffffffffffffffffffff"'
    . ',' . '"displayName":"%%%JITSI_NAME%%%"'
    . ',' . '"email":""'
    . ',' . '"localFlipX":true'
    . ',' . '"startAudioOnly":true'
    . ',' . '"startWithAudioMuted":false'
    . ',' . '"startWithVideoMuted":true'
    . '}'
  ,
};

my $PACKAGE_RESTRICTIONS = {
  "us.zoom.videomeetings" => ''
    . "    <pkg name=\"us.zoom.videomeetings\">\n"
    . "        <disabled-components>\n"
    . "            <item name=\"us.zoom.videomeetings.SendFileActivity\" />\n"
    . "        </disabled-components>\n"
    . "    </pkg>\n"
};

my $NOTIFICATIONS_ENABLED = {
  "com.google.android.apps.maps" => 0,
};

my $GRANT_PERMS = {
  #microg services
  "com.google.android.gms" => [qw(
    android.permission.FAKE_PACKAGE_SIGNATURE
    android.permission.ACCESS_BACKGROUND_LOCATION
    android.permission.ACCESS_COARSE_LOCATION
    android.permission.ACCESS_FINE_LOCATION
    android.permission.READ_PHONE_STATE
    android.permission.GET_ACCOUNTS
    android.permission.WRITE_EXTERNAL_STORAGE
    android.permission.READ_EXTERNAL_STORAGE
    android.permission.RECEIVE_SMS
    android.permission.SYSTEM_ALERT_WINDOW
  )],

  #fakestore
  "com.android.vending" => [qw(
    android.permission.FAKE_PACKAGE_SIGNATURE
  )],

  #Mozilla UnifiedNlp Backend
  "org.microg.nlp.backend.ichnaea" => [qw(
    android.permission.ACCESS_COARSE_LOCATION
    android.permission.ACCESS_FINE_LOCATION
    android.permission.ACCESS_BACKGROUND_LOCATION
    android.permission.READ_PHONE_STATE
  )],
};

my @NOMEDIA = qw(
  /home/nemo/Code/.nomedia

  /home/nemo/android_storage/Alarms/.nomedia
  /home/nemo/android_storage/Android/data/.nomedia
  /home/nemo/android_storage/Android/.nomedia
  /home/nemo/android_storage/Android/obb/.nomedia
  /home/nemo/android_storage/Audiobooks/.nomedia
  /home/nemo/android_storage/Books/.nomedia
  /home/nemo/android_storage/.cr3/.nomedia
  /home/nemo/android_storage/DCIM/.nomedia
  /home/nemo/android_storage/Documents/.nomedia
  /home/nemo/android_storage/Download/.nomedia
  /home/nemo/android_storage/Movies/.nomedia
  /home/nemo/android_storage/Music/.nomedia
  /home/nemo/android_storage/Notifications/.nomedia
  /home/nemo/android_storage/Pictures/.nomedia
  /home/nemo/android_storage/Podcasts/.nomedia
  /home/nemo/android_storage/Ringtones/.nomedia

  /media/sdcard/phone/Android/data/.nomedia
  /media/sdcard/phone/Android/obb/.nomedia
  /media/sdcard/phone/Backgrounds/.nomedia
  /media/sdcard/phone/comm-repos/.nomedia
  /media/sdcard/phone/DCIM-pixmirror-bydate/.nomedia
  /media/sdcard/phone/DCIM-pixmirror/.nomedia
  /media/sdcard/phone/email/.nomedia
  /media/sdcard/phone/Music/.nomedia
  /media/sdcard/phone/sheet_music/.nomedia
  /media/sdcard/phone/tmp/.nomedia
  /media/sdcard/phone/xbestddr/.nomedia
  /media/sdcard/phone/xgnuplotddr/.nomedia
);

sub applySettings();
sub editRemoteFile($$$);
sub editPackageRestrictions($);
sub editUriGrants($$);
sub editNotificationPolicy($);
sub editStartScript($);
sub parseUriGrants();
sub setupJitsi();
sub isJitsiInitialized();
sub initJitsi();
sub updateJitsiDB();

sub main(@){
  print "\n\n\napplying perms\n";
  my $permCmd = "echo start";
  for my $pkg(sort keys %$GRANT_PERMS){
    for my $perm(@{$$GRANT_PERMS{$pkg}}){
      $permCmd .= " && pm grant $pkg $perm";
    }
  }
  $permCmd .= " && echo %SUCCESS% || echo %FAILURE%";
  my $result = `ipmagic $IPMAGIC_NAME "ad '$permCmd'"`;
  print $result;
  if($result !~ /^start.*%SUCCESS%$/s){
    die "ERROR: could not set perms\n$permCmd\n";
  }

  print "\n\n\ncreating .nomedia files\n";
  my $nomediaCmd = "echo ok";
  for my $nomedia(@NOMEDIA){
    $nomediaCmd .= "; touch $nomedia";
    $nomediaCmd .= "; chown --reference $REMOTE_DATA_DIR/com.android.providers.media.module $nomedia";
    $nomediaCmd .= "; chmod 0670 $nomedia";
    $nomediaCmd .= "; setfacl -m group:nemo:rwx -m group::--- $nomedia";
  }
  system "ipmagic", $IPMAGIC_NAME, "-u", "root", "sh", "-c", $nomediaCmd;

  print "\n\n\napplying settings\n";
  applySettings();

  print "\n\n\nediting package restrictions\n";
  editRemoteFile(
    "package-restrictions",
    $PACKAGE_RESTRICTIONS_XML_FILE,
    \&editPackageRestrictions,
  );

  my $uriGrants = parseUriGrants();
  print "\n\n\nediting URI grants\n";
  editRemoteFile(
    "uri-grants",
    $URI_GRANTS_XML_FILE,
    sub($){ editUriGrants($_[0], $uriGrants) },
  );

  print "\n\n\nediting notification policy\n";
  editRemoteFile(
    "notification-policy",
    $NOTIFICATION_POLICY_XML_FILE,
    \&editNotificationPolicy,
  );

  print "\n\n\nediting start script\n";
  if(ipmagicTest $IPMAGIC_NAME, "root", "-f", $ALIENDALVIK_START_SCRIPT){
    editRemoteFile(
      "start-script",
      $ALIENDALVIK_START_SCRIPT,
      \&editStartScript,
    );
  }else{
    print "skipping edit of non-existent $ALIENDALVIK_START_SCRIPT\n";
  }

  print "\n\n\nsetting up jitsi:\n";
  setupJitsi();
}

sub applySettings(){
  my $getCmds = "";
  my $setCmds = "";
  for my $namespace(sort keys %$SETTINGS){
    for my $key(sort keys %{$$SETTINGS{$namespace}}){
      my $val = $$SETTINGS{$namespace}{$key};
      $getCmds .= "echo -ne $namespace.$key=; ad shell settings get '$namespace' '$key';\n";
      $setCmds .= "ad shell settings put '$namespace' '$key' '$val';\n";
    }
  }

  print "settings (before):\n";
  my $before = `ipmagic $IPMAGIC_NAME "$getCmds"`;
  print "$before\n\n";

  run "ipmagic", $IPMAGIC_NAME, "$setCmds";
  print "\n\n";

  print "settings (after):\n";
  my $after = `ipmagic $IPMAGIC_NAME "$getCmds"`;
  print "$after\n\n";

  if($before eq $after){
    print "NO CHANGE\n";
  }else{
    print "SETTINGS UPDATED\n";
  }
}

sub editRemoteFile($$$){
  my ($editName, $remoteFile, $editSub) = @_;
  print "checking $editName\n";
  my $host = `ipmagic $IPMAGIC_NAME`;
  chomp $host;

  my $nowMillis = nowMillis();
  my $tmpFileOld = "/tmp/setup-aliendalvik-$editName-$nowMillis-old";
  my $tmpFileNew = "/tmp/setup-aliendalvik-$editName-$nowMillis-new";

  run "scp", "root\@$host:$remoteFile", $tmpFileOld;
  run "cp", "-ar", $tmpFileOld, $tmpFileNew;

  if(not -e $tmpFileOld or not -e $tmpFileNew){
    die "error fetching $remoteFile\n";
  }

  my $isChanged = &$editSub($tmpFileNew);

  print "\n";
  if($isChanged){
    print "$editName changed, updating..\n";
    run "ipmagic", $IPMAGIC_NAME, "-u", "root",
      "systemctl stop aliendalvik.service";

    tryrun "diff", $tmpFileOld, $tmpFileNew;
    run "scp", $tmpFileNew, "root\@$host:$tmpFileNew";
    run "ipmagic", $IPMAGIC_NAME, "-u", "root", "
      cat $tmpFileNew > $remoteFile
      rm $tmpFileNew
    ";
    run "ipmagic", $IPMAGIC_NAME, "-u", "root",
      "systemctl start aliendalvik.service";
  }else{
    print "NO CHANGE to $editName\n";
  }

  run "rm", $tmpFileOld;
  run "rm", $tmpFileNew;
}

sub editPackageRestrictions($){
  my ($file) = @_;
  open FH, "< $file" or die "could not read $file\n$!\n";
  my $contents = join '', <FH>;
  close FH;

  my $ws = '(?:\s|\n)*';

  if($contents !~ /
    ^
    ($ws <\?xml\s[^>]*\?> $ws <package-restrictions> $ws)
    (
      (?:
        (?:$ws<pkg[^>]*\/>)
        |
        (?:$ws<pkg[^>]*[^\/]>.*?<\/pkg>)
      )*
    )
    (
      (?: (?:$ws<preferred-activities$ws\/>$ws)
        |
        (?:$ws <preferred-activities$ws>.*?<\/preferred-activities> $ws)
      )?
      (?: (?:$ws<persistent-preferred-activities$ws\/>$ws)
        |
        (?:$ws <persistent-preferred-activities$ws>.*?<\/persistent-preferred-activities> $ws)
      )?
      (?: (?:$ws<crossProfile-intent-filters$ws\/>$ws)
        |
        (?:$ws <crossProfile-intent-filters$ws>.*?<\/crossProfile-intent-filters> $ws)
      )?
      (?: (?:$ws<default-apps$ws\/>$ws)
        |
        (?:$ws <default-apps$ws>.*?<\/default-apps> $ws)
      )?

      $ws <\/package-restrictions> $ws
    )
    $/sxi){
    die "ERROR: malformed $PACKAGE_RESTRICTIONS_XML_FILE\n";
  }
  my ($prefix, $pkgXml, $suffix) = ($1, $2, $3, $4);

  my @pkgs = $pkgXml =~ /
    (
      (?:$ws<pkg[^>]*\/>)
      |
      (?:$ws<pkg[^>]*[^\/]>.*?<\/pkg>)
    )
  /gsxi;
  if($pkgXml ne join("", @pkgs)){
    die "ERROR: malformed <pkg> tags in $PACKAGE_RESTRICTIONS_XML_FILE\n";
  }

  my $isChanged = 0;

  for my $pkg(@pkgs){
    my $pkgName = $1 if $pkg =~ /<pkg name="([^"]+)"/;
    die "malformed package: $pkg\n" if not defined $pkgName;

    if(defined $$PACKAGE_RESTRICTIONS{$pkgName}){
      my $oldPkg = $pkg;
      my $newPkg = $$PACKAGE_RESTRICTIONS{$pkgName};

      my $oldPkgTrim = $oldPkg;
      $oldPkgTrim =~ s/^$ws//sxi;
      $oldPkgTrim =~ s/$ws$//sxi;

      my $newPkgTrim = $newPkg;
      $newPkgTrim =~ s/^$ws//sxi;
      $newPkgTrim =~ s/$ws$//sxi;

      my ($wsPrefix, $wsSuffix) = ($1, $2) if $pkg =~ /^($ws).*?($ws)$/sxi;
      $newPkg = "$wsPrefix$newPkgTrim$wsSuffix";

      if($oldPkgTrim ne $newPkgTrim){
        print "PACKAGE RESTRICTION UPDATED:\n=====\n$oldPkgTrim\n=>\n=====\n$newPkgTrim\n";
        $pkg = $newPkg;
        $isChanged = 1;
      }
    }
  }

  $pkgXml = join("", @pkgs);

  $contents = $prefix . $pkgXml . $suffix;

  if($isChanged){
    open FH, "> $file" or die "could not write $file\n$!\n";
    print FH $contents;
    close FH;
  }

  return $isChanged;
}

sub editUriGrants($$){
  my ($file, $uriGrants) = @_;
  open FH, "< $file" or die "could not read $file\n$!\n";
  my $contents = join '', <FH>;
  close FH;

  my @uriGrantLines = $contents =~ /(<uri-grant .*\/>)/g;
  s/$/\n/ foreach @uriGrantLines;

  my $oldUriGrants = [];
  for my $line(@uriGrantLines){
    my $pkg = $1 if $line =~ /targetPkg="([^"]+)"/;
    my $uri = $1 if $line =~ /uri="([^"]+)"/;
    if(not defined $pkg or not defined $uri){
      die "ERROR: malformed uri-grant line $line\n";
    }
    push @$oldUriGrants, {
      pkg => $pkg,
      uri => $uri,
    };
  }

  for my $newUriGrant(@$uriGrants){
    my $alreadyPresent = 0;
    for my $oldUriGrant(@$oldUriGrants){
      if($$oldUriGrant{pkg} eq $$newUriGrant{pkg} and $$oldUriGrant{uri} and $$newUriGrant{uri}){
        $alreadyPresent = 1;
        last;
      }
    }
    if(not $alreadyPresent){
      my $pkg = $$newUriGrant{pkg};
      my $uri = $$newUriGrant{uri};
      my $nowMillis = nowMillis();
      my $grantLine = "<uri-grant"
        . " sourceUserId=\"0\""
        . " targetUserId=\"0\""
        . " sourcePkg=\"com.android.externalstorage\""
        . " targetPkg=\"$pkg\""
        . " uri=\"$uri\""
        . " prefix=\"true\""
        . " modeFlags=\"3\""
        . " createdTime=\"$nowMillis\""
        . " />\n";
      push @uriGrantLines, $grantLine;
    }
  }

  my $newContents = ""
    . "<?xml version='1.0' encoding='utf-8' standalone='yes' ?>\n"
    . "<uri-grants>\n"
    . join("", @uriGrantLines)
    . "</uri-grants>\n";

  my $isChanged = $contents eq $newContents ? 0 : 1;

  if($isChanged){
    open FH, "> $file" or die "could not write $file\n$!\n";
    print FH $newContents;
    close FH;
  }

  return $isChanged;
}

sub editNotificationPolicy($){
  my ($file) = @_;
  open FH, "< $file" or die "could not read $file\n$!\n";
  my $contents = join '', <FH>;
  close FH;

  my $isChanged = 0;

  for my $pkg(sort keys %$NOTIFICATIONS_ENABLED){
    my $isEnabled = $$NOTIFICATIONS_ENABLED{$pkg};
    if($contents =~ /<package name="$pkg"([^>]*)>/){
      my $origAtts = $1;
      my $newAtts = $origAtts;
      $newAtts =~ s/ importance="[^"]*"//g;
      if(not $isEnabled){
        $newAtts = " importance=\"0\"$newAtts";
      }
      if($origAtts ne $newAtts){
        $contents =~ s/(<package name="$pkg")[^>]*>/$1$newAtts>/;
        $isChanged = 1;
      }
    }else{
      die "ERROR: could not set notifications enabled/disabled for $pkg\n";
    }
  }

  if($isChanged){
    open FH, "> $file" or die "could not write $file\n$!\n";
    print FH $contents;
    close FH;
  }

  return $isChanged;
}

sub editStartScript($){
  my ($file) = @_;
  open FH, "< $file" or die "could not read $file\n$!\n";
  my $contents = join '', <FH>;
  close FH;

  my $oldContents = $contents;

  $contents =~ s/###EDIT_START###(.|\n)*###EDIT_END###\n//g;

  my $editSection = ""
    . "###EDIT_START###\n"
    . "$POST_ALIENDALVIK_START_SCRIPT\n"
    . "###EDIT_END###\n"
    ;

  if($contents !~ s/(^if.*lxc_status.*active.*\n)/$1$editSection/m){
    die "could not find place where lxc_status is checked in $file\n";
  }

  my $isChanged = $contents eq $oldContents ? 0 : 1;

  if($isChanged){
    open FH, "> $file" or die "could not write $file\n$!\n";
    print FH $contents;
    close FH;
  }

  return $isChanged;
}

sub parseUriGrants(){
  my $devUUIDs = {};
  my $parsedUriGrants = [];
  for my $uriGrant(@$EXTERNAL_STORAGE_URI_GRANTS){
    my $pkg = $$uriGrant{pkg};
    my $dev = $$uriGrant{dev};
    my $path = $$uriGrant{path};

    if(not defined $$devUUIDs{$dev}){
      my $devUUID = getIpmagicBlockDevUUID($IPMAGIC_NAME, $dev);
      die "ERROR: could not fetch UUID of $dev\n" if not defined $devUUID;
      $$devUUIDs{$dev} = $devUUID;
    }

    $path =~ s/\//%2F/g;

    my $uuid = $$devUUIDs{$dev};
    my $uri = "content://com.android.externalstorage.documents/tree/$uuid%3A$path";
    push @$parsedUriGrants, {
      pkg => $pkg,
      uri => $uri,
    };
  }
  return $parsedUriGrants;
}

sub setupJitsi(){
  if(not isJitsiInitialized()){
    print "jitsi DB file not found, initializing jitsi\n";
    initJitsi();
  }

  if(not isJitsiInitialized()){
    print "WARNING: $JITSI_DB file does not exist\n";
  }else{
    updateJitsiDB();
  }
}

sub isJitsiInitialized(){
  my $file = `ipmagic $IPMAGIC_NAME -u root ls $JITSI_DB 2>/dev/null`;
  if($file =~ /$JITSI_DB/){
    return 1;
  }else{
    return 0;
  }
}

sub initJitsi(){
  print "starting jitsi..\n";
  run "ipmagic", $IPMAGIC_NAME, "ad", "--launch-intent=org.jitsi.meet/.MainActivity";

  print "waiting 2s\n";
  sleep 2;

  print "stopping jitsi..\n";
  run "ipmagic", $IPMAGIC_NAME,
    "ad", "shell", "am force-stop org.jitsi.meet";
}

sub updateJitsiDB(){
  my $nowMillis = nowMillis();
  my $jitsiName = `jitsi-meet --jitsi --print-name`;
  chomp $jitsiName;
  my $jitsiRoom = `jitsi-meet --jitsi --print-room`;
  chomp $jitsiRoom;

  for my $rowKey(sort keys %$JITSI_DB_CONF){
    my $rowValue = $$JITSI_DB_CONF{$rowKey};
    $rowValue =~ s/%%%NOW_MILLIS%%%/$nowMillis/g;
    $rowValue =~ s/%%%JITSI_ROOM%%%/$jitsiRoom/g;
    $rowValue =~ s/%%%JITSI_NAME%%%/$jitsiName/g;
    $rowValue =~ s/"/"\\""/g;
    print "====================\n";
    print "$JITSI_DB:\n";
    print "sqlite - update catalystLocalStorage.$rowKey\n";
    runQuiet "ipmagic", $IPMAGIC_NAME, "-u", "root", "
      sqlite3 $JITSI_DB \"
        select '   ' || value FROM catalystLocalStorage WHERE key = '$rowKey';
        select '    => ';
        update catalystLocalStorage
          set value = '$rowValue'
          where key = '$rowKey';
        select '   ' || value FROM catalystLocalStorage WHERE key = '$rowKey';
      \"
    ";
  }
}

&main(@ARGV);
