#!/usr/bin/perl
use strict;
use warnings;
use lib `dirname $0 | tr -d '\n'`;
use PhoneBackupUtils;

use Time::HiRes qw(time);

my $DIR_BASE = "$ENV{HOME}/Code/sx";
my $DIR_BACKUP = "$DIR_BASE/backup";

my $IPMAGIC_NAME = "sx";
my $IPMAGIC_USER = "nemo";

my $DIR_NESOID_LAST_BACKUP = "$DIR_BACKUP/games/nesoid-saves/last-backup";
my $DIR_NESOID_BY_MTIME = "$DIR_BACKUP/games/nesoid-saves/saves-by-mtime";

my $DIR_SNES9XEX_LAST_BACKUP = "$DIR_BACKUP/games/snes9xex-saves/last-backup";
my $DIR_SNES9XEX_BY_MTIME = "$DIR_BACKUP/games/snes9xex-saves/saves-by-mtime";

my $DIR_SRC_NES_FAV_ROMS = "$ENV{HOME}/Games/nes/fav_roms";
my $DIR_DEST_NESOID_DATA = "/media/sdcard/phone/Games/nesoid";

my $DIR_SRC_SNES_FAV_ROMS = "$ENV{HOME}/Games/snes/fav_roms";
my $DIR_DEST_SNES9XEX_DATA = "/media/sdcard/phone/Games/snes9xex";

my $DIR_KINGDOM_LATEST_BACKUP = "$DIR_BACKUP/games/kingdom-save";
my $DIR_KINGDOM_REMOTE_ANDROID_FILES = "/home/nemo/android_storage/Android/data/com.rawfury.kingdom2crowns/files";
my $DIR_KINGDOM_REMOTE_SAVE = "$DIR_KINGDOM_REMOTE_ANDROID_FILES/Release";

sub backupNesoidSaves();
sub backupSnes9xExSaves();
sub restoreNesoidSaves();
sub restoreSnes9xExSaves();
sub fetchKingdomSave();
sub cacheKingdomSave();
sub overwriteRemoteKingdomSave();
sub setupNesoid();
sub setupSnes9xEx();

sub checksumMatches($$);
sub sha1sum($);

my $usage = "Usage:
  $0 -h|--help
    show this message

  $0 [--backup]
    -backup nesoid saves
    -backup snes9xex saves
    -backup android kingdom save
    -cache newly backed up kingdom save with `kingdom-save`

  $0 --restore
    -copy NES roms
    -setup nesoid
    -restore nesoid saves
    -copy SNES roms
    -setup Snes9xEx
    -restore Snes9xEx saves
    -OVERWRITE remote kingdom save with last backup
";

sub main(@){
  my $command;
  if(@_ == 1 and $_[0] =~ /^(-h|--help)$/){
    print $usage;
    exit 0;
  }elsif(@_ == 1 and $_[0] =~ /^(--backup|--restore)$/){
    $command = $1;
  }elsif(@_ == 0){
    $command = "--backup";
  }else{
    die $usage;
  }

  if($command eq "--backup"){
    backupNesoidSaves();

    backupSnes9xExSaves();

    if(ipmagicTest $IPMAGIC_NAME, "root", "-d", $DIR_KINGDOM_REMOTE_SAVE){
      fetchKingdomSave();
      cacheKingdomSave();
    }else{
      print "skipping kingdom backup\n";
    }
  }elsif($command eq "--restore"){
    setupNesoid();
    restoreNesoidSaves();

    setupSnes9xEx();
    restoreSnes9xExSaves();

    if(ipmagicTest $IPMAGIC_NAME, "root", "-d", $DIR_KINGDOM_REMOTE_ANDROID_FILES){
      overwriteRemoteKingdomSave();
    }else{
      print "skipping kingdom restore (must run android app once first)\n";
    }
  }
}

sub backupNesoidSaves(){
  my $host = `ipmagic $IPMAGIC_NAME`;
  chomp $host;

  run "mkdir", "-p", $DIR_NESOID_LAST_BACKUP, $DIR_NESOID_BY_MTIME;

  run "rsync",
    "-avP",
    "--del",
    "$IPMAGIC_USER\@$host:$DIR_DEST_NESOID_DATA/", "$DIR_NESOID_LAST_BACKUP/",
    "--exclude=*.nes";

  for my $file(glob "$DIR_NESOID_LAST_BACKUP/*"){
    my $fileName = $file;
    $fileName =~ s/^(.*)\///;

    my $mtime = mtime($file);
    my $destFile = "$DIR_NESOID_BY_MTIME/$fileName-$mtime";
    if(-e $destFile){
      if(not checksumMatches($file, $destFile)){
        die "CHECKSUM MISMATCH: $file vs $destFile\n";
      }
    }else{
      run "cp", "-ar", $file, $destFile;
    }
  }
}

sub backupSnes9xExSaves(){
  my $host = `ipmagic $IPMAGIC_NAME`;
  chomp $host;

  run "mkdir", "-p", $DIR_SNES9XEX_LAST_BACKUP, $DIR_SNES9XEX_BY_MTIME;

  run "rsync",
    "-avP",
    "--del",
    "$IPMAGIC_USER\@$host:$DIR_DEST_SNES9XEX_DATA/", "$DIR_SNES9XEX_LAST_BACKUP/",
    "--exclude=*.smc";

  for my $file(glob "$DIR_SNES9XEX_LAST_BACKUP/*"){
    my $fileName = $file;
    $fileName =~ s/^(.*)\///;

    my $mtime = mtime($file);
    my $destFile = "$DIR_SNES9XEX_BY_MTIME/$fileName-$mtime";
    if(-e $destFile){
      if(not checksumMatches($file, $destFile)){
        die "CHECKSUM MISMATCH: $file vs $destFile\n";
      }
    }else{
      run "cp", "-ar", $file, $destFile;
    }
  }
}

sub restoreNesoidSaves(){
  my $host = `ipmagic $IPMAGIC_NAME`;
  chomp $host;

  run "rsync",
    "-avP",
    "--del",
    "$DIR_NESOID_LAST_BACKUP/",
    "$IPMAGIC_USER\@$host:$DIR_DEST_NESOID_DATA/",
    "--exclude=*.nes";
}

sub restoreSnes9xExSaves(){
  my $host = `ipmagic $IPMAGIC_NAME`;
  chomp $host;

  run "rsync",
    "-avP",
    "--del",
    "$DIR_SNES9XEX_LAST_BACKUP/",
    "$IPMAGIC_USER\@$host:$DIR_DEST_SNES9XEX_DATA/",
    "--exclude=*.smc";
}

sub fetchKingdomSave(){
  run "ipmagic", $IPMAGIC_NAME, "-u", "root", "--rsync",
    "-avP",
    "--del",
    ":$DIR_KINGDOM_REMOTE_SAVE/",
    "$DIR_KINGDOM_LATEST_BACKUP/",
  ;
}
sub cacheKingdomSave(){
  run "kingdom-save",
    "--no-notify",
    "--game=k2ca",
    "--save-dir=$DIR_KINGDOM_LATEST_BACKUP",
  ;
}
sub overwriteRemoteKingdomSave(){
  run "ipmagic", $IPMAGIC_NAME, "-u", "root", "--rsync",
    "-avP",
    "--del",
    "$DIR_KINGDOM_LATEST_BACKUP/",
    ":$DIR_KINGDOM_REMOTE_SAVE/",
  ;

  #chown the save dir to the parent's owner+group
  run "ipmagic", $IPMAGIC_NAME, "-u", "root", "-s",
    "chown",
    "-R",
    "$DIR_KINGDOM_REMOTE_SAVE/",
    "--reference=$DIR_KINGDOM_REMOTE_ANDROID_FILES",
  ;
}

sub setupNesoid(){
  my $host = `ipmagic $IPMAGIC_NAME`;
  chomp $host;

  run "ipmagic", $IPMAGIC_NAME, "mkdir -p $DIR_DEST_NESOID_DATA";
  run "rsync",
    "-avP",
    "--del",
    "--copy-links",
    "$DIR_SRC_NES_FAV_ROMS/",
    "$IPMAGIC_USER\@$host:$DIR_DEST_NESOID_DATA/",
    "--exclude=*.ss*",
  ;
}

sub setupSnes9xEx(){
  my $host = `ipmagic $IPMAGIC_NAME`;
  chomp $host;

  run "ipmagic", $IPMAGIC_NAME, "mkdir -p $DIR_DEST_SNES9XEX_DATA";
  run "rsync",
    "-avP",
    "--del",
    "--copy-links",
    "$DIR_SRC_SNES_FAV_ROMS/",
    "$IPMAGIC_USER\@$host:$DIR_DEST_SNES9XEX_DATA/",
    "--exclude=*.s96",
  ;
}

sub checksumMatches($$){
  my ($fileA, $fileB) = @_;
  return 0 if not -f $fileA;
  return 0 if not -f $fileB;

  my $sha1A = sha1sum $fileA;
  my $sha1B = sha1sum $fileB;
  if($sha1A eq $sha1B){
    return 1;
  }else{
    return 0;
  }
}

sub sha1sum($){
  my ($file) = @_;
  open FH, "-|", "sha1sum", $file or die "could not run sha1sum on $file\n$!\n";
  my @lines = <FH>;
  close FH;
  if(@lines != 1 or $lines[0] !~ /^([0-9a-f]{40})\s/){
    die "error running sha1sum on $file\n";
  }
  return $1;
}

&main(@ARGV);
